#!/usr/bin/env crow
import
	crow/io/file: write-file
	crow/io/process: shell-exec
	../site-src/search-dir-recursive: search-dir-recursive

main void() summon
	files = "src" search-dir-recursive x => x ends-with ".d"
	dependencies dependency[] = with out : build
		for file : files
			importer = file module-from-path
			guard !importer.ignore
			for x : file imports
				guard !x.ignore
				out ~= (importer, x)
	"bin/dependencies.dot" write-file dependencies.to-dot
	()

-imports string[](file string) summon
	"dub run dscanner --yes --quiet -- --imports {file}".shell-exec split "\n" filter x =>
		x != "" && !(x starts-with "core.")

-module-from-path string(a string)
	((a try-remove-start "src/")! try-remove-end ".d")! replace-all "/", "."

-dependency record(importer string, exporter string)

-to string(a dependency)
	"{a.importer.to::json} -> {a.exporter.to::json}"

-ignore bool(a string)
	# omit model and util since these are commonly imported dependencies
	ignore-base string[] = "core.", "model.", "std.", "test.", "util."
	a == "object" || ignore-base exists (x => a starts-with x) || a ends-with ".txt"

-to-dot string(dependencies dependency[])
	"""digraph crow \{
	{";\n\t" join for x : dependencies; x.to}
}"""
