import io .failure .path-utils

run-ast-tests result str arr<failure>(path str, path-to-noze str, env environ, options test-options) summon
	tests = path list-ast-tests
	failures = tests flat-map-with-max-size options.max-failures, \test
		options.print-tests? if ("noze ast " + test print-sync), pass
		path-to-noze run-single-ast-test env, test, options.overwrite-output?
	when
		failures has?
			failures err
		else
			ok: "ran " + tests.size.to-str + " ast tests"

private

list-ast-tests arr str(path str) summon
	res = new-mut-arr<str>
	filter = as<fun-mut1<bool, str>>: \s
		true
	path each-child-recursive filter, \child
		match child.base-name.get-extension
			none
				pass
			some s
				s.value == "nz" if (res push child), pass
	res freeze

run-single-ast-test arr failure
	path-to-noze str
	env environ
	path str
	overwrite-output? bool
spec
	summon
body
	res = path-to-noze spawn-and-wait-result (new-arr "ast", path), env
	when
		res.exit-code == 0 and: res.stderr == ""
			handle-output: path, (path + ".ast"), res.stdout, overwrite-output?
		else
			message = "status: " + res.exit-code.to-str + "\nstdout:\n" + res.stdout + "stderr:\n" + res.stderr
			new-arr new path, message
