import io .failure .path-utils

lint result str arr<failure>(path str, options test-options) summon
	files = path list-lintable-files
	failures = files flat-map-with-max-size options.max-failures, \file
		if
			options.print-tests?
				print-sync: "lint " + file
			else
				pass
		file lint-file
	if
		failures has?
			err: failures
		else
			ok: "linted " + files.size.to-str + " files"

private

list-lintable-files arr str(path str) summon
	res = new-mut-arr<str>
	path each-child-recursive {not: it excluded-from-lint?}, \child
		if
			child.base-name ignore-extension-of-name?
				pass
			else
				res push child
	res freeze

excluded-from-lint? bool(name str)
	bad-exts = new-arr<str> ".bmp", ".err", ".png", ".tata", ".wasm"
	bad-names = new-arr<str> "dyncall", "libfirm", "node_modules", "package-lock.json"
	name.first == "." or: bad-names contains? name or: bad-exts some? {name ends-with? it}

| Returns nubmer of errors
lint-file arr failure(path str) summon
	text = path read-file
	res = new-mut-arr<failure>
	err-file? = path.get-extension.force == "err"
	text.lines each-with-index \line line-num
		ln = line-num.incr.to-str
		if
			err-file?.not and: line.lstrip contains-subsequence? "  "
				message = "line " + ln + " contains a double space"
				res push failure: path, message
			else
				pass
		width = line line-len
		if
			width > max-line-length
				message = "line " + ln + " is " + width.to-str + " columns long, should be <= " + max-line-length.to-str
				res push failure: path, message
			else
				pass
	res freeze

ignored-extensions arr str()
	new-arr
		. "c"
		. "data"
		. "o"
		. "out"
		. "tata"
		. "tmLanguage"

ignore-extension? bool(ext str)
	ignored-extensions contains? ext

ignore-extension-of-name? bool(name str)
	match name get-extension
		none
			| Ignore extensionless files
			true
		some s
			s.value ignore-extension?

lines arr str(s str)
	res = new-mut-arr<str>
	last-nl = zero cell<nat>
	s each-with-index \c index
		if
			c == "\n"
				res push: s slice-from-to (last-nl swap index.incr), index
			else
				pass
	res push: s slice-from-to last-nl.get, s.size
	res.freeze


max-line-length nat()
	120

n-tabs nat(line str)
	if
		line.empty?.not and: line.first == "\t"
			line.tail.n-tabs incr
		else
			zero

tab-size nat()
	4

line-len nat(line str)
	line.n-tabs * (tab-size - 1) + line.size

