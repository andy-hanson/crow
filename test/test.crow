#!/usr/bin/env crow

import
	bootstrap: void
	collection.arr-util: first
	command-line-parser: parse-cmd-line-args
	io.environ: get-environ
	io.file: current-directory
	io.print: print
	path: child-path, parent-path
	.failure: first-failures, print-failures, test-options
	.lint: lint
	.crow-tests: run-crow-tests

main fut nat(args arr str) summon
	option-names = ["print-tests", "overwrite-output", "max-failures"]
	options = args parse-cmd-line-args<test-options> option-names, \values
		print-tests-strs = values[0]
		overwrite-output-strs = values[1]
		max-failures-strs = values[2]
		print-tests? = print-tests-strs has?
		overwrite-output? = match overwrite-output-strs
		as none
			false
		as some s
			# TODO: handle it..
			assert: s.value empty?
			true
		max-failures = match max-failures-strs
		as none
			100
		as some s
			strs = s.value
			assert: strs.size == 1
			strs.first.parse-nat.force
		test-options: print-tests?, overwrite-output?, max-failures
	resolved: match options
	as none
		print-help
		1
	as some s
		s.value do-test

.print-help void() summon
	print: "test -- runs tests"
	print: "options:"
	print: "\t--print-tests: print every test run"
	print: "\t--max-failures: stop after this many failures. Defaults to 10."

.do-test nat(options test-options) summon
	test-path = current-directory ~ "/test"
	crow-path = test-path parent-path
	crow-exe = crow-path child-path "bin" child-path "crow"
	env = get-environ

	crow-failures = test-path child-path "parse-errors" run-crow-tests crow-exe, env, options first-failures \
		test-path child-path "compile-errors" run-crow-tests crow-exe, env, options first-failures \
			test-path child-path "runnable" run-crow-tests crow-exe, env, options
	all-failures = crow-failures first-failures {crow-path lint options}
	all-failures print-failures options
