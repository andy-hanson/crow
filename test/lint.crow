import
	crow/col/set: in, new, set
	crow/io/file: each-child-recursive, read-file
	crow/io/path: base-name, get-extension
	crow/io/print: out, print
	./failure: failure, flat-map-with-max-size, max-failures, new, print-tests, test-options

lint result<str, list failure>(path str, options test-options) summon
	files = path list-lintable-files
	failures = files flat-map-with-max-size options.max-failures, file =>
		if options print-tests
			out print "lint {file}"
		file lint-file
	if failures is-empty
		"linted {files size} files" ok
	else
		failures err

.list-lintable-files list str(path str) summon
	res str mut[] = ()
	path each-child-recursive (child => !child.excluded-from-lint), child =>
		unless child.base-name should-ignore-extension-of-name
			res ~= child
	res move-to-list

.excluded-from-lint bool(name str)
	if name starts-with "." || name in exclude-names
		true
	elif ext ?= name get-extension
		ext in exclude-extensions

.exclude-extensions set str()
	"bmp", "dll", "err", "exe", "ilk", "lib", "mdb", "obj", "ogg", "pdb", "png", "svg", "wasm", "wav", "webp", "woff2"
.exclude-names set str()
	# TODO: gl.crow needs a multi-line call syntax
	"bin", "COPYING.txt", "dyncall", "gl.crow", "node_modules", "package-lock.json", "site"

# Returns number of errors
.lint-file list failure(path str) summon
	text = path read-file
	res failure mut[] = ()
	ext = path.get-extension!
	allow-double-space = ext == "err" || ext == "sublime-syntax"
	# TODO: for line, line-num in text.lines
	lines = text lines
	for line-num : 0 .. lines.size
		line = lines[line-num]
		ln = line-num + 1 to-str
		space-space = " " ~~ " "
		if !allow-double-space && line contains-substr space-space
			message = "line {ln} contains a double space"
			res ~= (path, message)
		width = line line-len
		if width > max-line-length
			message = "line {ln} is {width} columns long, should be <= {max-line-length}"
			res ~= (path, message)
	res move-to-list

.ignored-extensions list str()
	"c", "data", "json", "o", "out", "repr", "tmLanguage"

.should-ignore-extension bool(ext str)
	ext in ignored-extensions

.should-ignore-extension-of-name bool(name str)
	if ext ?= name get-extension
		ext should-ignore-extension
	else
		# Ignore extensionless files
		true

.max-line-length nat()
	120

.n-tabs nat(line str)
	if rest ?= line try-remove-start "\t"
		rest.n-tabs + 1

.tab-size nat()
	4

.line-len nat(line str)
	line.n-tabs * (tab-size - 1) + line.char8s.size
