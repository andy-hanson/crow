import
	crow/cell: *, cell, new, swap
	crow/col/arr-util: each-with-index, exists, in
	crow/col/mut-arr: ~=, move-to-arr!, mut-arr, new
	crow/io/file: each-child-recursive, read-file
	crow/io/path: base-name, get-extension
	crow/io/print: print
	crow/str-util: ends-with, has-substr, starts-with, try-remove-start
	./failure: failure, flat-map-with-max-size, max-failures, new, print-tests, test-options

lint result<str, failure[]>(path str, options test-options) summon
	files = path list-lintable-files
	failures = files flat-map-with-max-size options.max-failures, file =>
		if options print-tests
			print: "lint {file}"
		file lint-file
	if failures is-empty
		ok: "linted {files size} files"
	else
		err: failures

.list-lintable-files str[](path str) summon
	res str mut[] = ()
	path each-child-recursive (child => !child.excluded-from-lint), child =>
		if !child.base-name.should-ignore-extension-of-name
			res ~= child
	res move-to-arr!

.excluded-from-lint bool(name str)
	exclude-exts str[] = ".bmp", ".err", ".html", ".mdb", ".png", ".repr", ".svg", ".ttf", ".wasm", ".webp", ".xz"
	exclude-names str[] = "dyncall", "node_modules", "package-lock.json"
	name starts-with "." || name in exclude-names || exclude-exts exists ext => name ends-with ext

# Returns numner of errors
.lint-file failure[](path str) summon
	text = path read-file
	res failure mut[] = ()
	ext = path.get-extension force
	allow-double-space = ext == "err" || ext == "sublime-syntax"
	text.lines each-with-index (line, line-num) =>
		ln = line-num + 1 to-str
		space-space = " " ~ " "
		if !allow-double-space && line has-substr space-space
			message = "line {ln} contains a double space"
			res ~= (path, message)
		width = line line-len
		if width > max-line-length
			message = "line {ln} is {width} columns long, should be <= {max-line-length}"
			res ~= (path, message)
	res move-to-arr!

.ignored-extensions str[]()
	"c", "data", "o", "out", "repr", "tmLanguage"

.should-ignore-extension bool(ext str)
	ext in ignored-extensions

.should-ignore-extension-of-name bool(name str)
	if ext ?= name get-extension
		ext should-ignore-extension
	else
		# Ignore extensionless files
		true

# TODO: move to str-util, make a generic 'split' function
.lines str[](s str)
	res str mut[] = ()
	last-nl cell nat = 0,
	s.chars each-with-index (c, index) =>
		if c == "\n"
			nl = last-nl swap index + 1
			res ~= s.chars[nl .. index] str
	res ~= s.chars[*last-nl .. s.size-bytes] str
	res move-to-arr!

.max-line-length nat()
	120

.n-tabs nat(line str)
	if rest ?= line try-remove-start "\t"
		rest.n-tabs + 1

.tab-size nat()
	4

.line-len nat(line str)
	line.n-tabs * (tab-size - 1) + line.size-bytes
