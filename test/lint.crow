import
	crow/io/file: each-child-recursive, read-file
	crow/io/path: base-name, get-extension
	crow/io/print: out, print
	./failure: failure, flat-map-with-max-size, max-failures, new, print-tests, test-options, test-result

lint test-result(path string, options test-options) summon
	files = path list-lintable-files
	failures = files flat-map-with-max-size options.max-failures, file =>
		if options print-tests
			out print "lint {file}"
		file lint-file
	if failures is-empty
		"linted {files size} files" ok
	else
		failures err

.list-lintable-files string[](path string) summon
	res string mut[] = ()
	path each-child-recursive (child => !child.excluded-from-lint), child =>
		unless child.base-name should-ignore-extension-of-name
			res ~= child
	res move-to-list

.excluded-from-lint bool(name string)
	if name starts-with "." || name in exclude-names
		true
	elif ext ?= name get-extension
		ext in exclude-extensions

.exclude-extensions string set()
	"bmp", "dll", "err", "exe", "ilk", "lib", "mdb", "obj", "ogg", "pdb", "png", "svg", "wasm", "wav", "woff2"
.exclude-names string set()
	# TODO: gl.crow needs a multi-line call syntax
	"bin", "COPYING.txt", "crow.sublime-syntax", "dyncall", "gl.crow", "node_modules", "package-lock.json", "site"

# Returns number of errors
.lint-file failure[](path string) summon
	text = path read-file
	res failure mut[] = ()
	ext = path.get-extension!
	allow-double-space = ext == "err" || ext == "sublime-syntax"
	for line-num, line : text lines
		ln = line-num + 1 to-string
		space-space = " " ~~ " "
		if !allow-double-space && line contains-substr space-space
			message = "line {ln} contains a double space"
			res ~= (path, message)
		width = line line-len
		if width > max-line-length
			message = "line {ln} is {width} columns long, should be <= {max-line-length}"
			res ~= (path, message)
	res move-to-list

.ignored-extensions string[]()
	"c", "data", "json", "o", "out", "repr", "tmLanguage"

.should-ignore-extension bool(ext string)
	ext in ignored-extensions

.should-ignore-extension-of-name bool(name string)
	if ext ?= name get-extension
		ext should-ignore-extension
	else
		# Ignore extensionless files
		true

.max-line-length nat()
	120

.n-tabs nat(line string)
	if rest ?= line try-remove-start "\t"
		rest.n-tabs + 1

.tab-size nat()
	4

.line-len nat(line string)
	line.n-tabs * (tab-size - 1) + line.char8s.size
