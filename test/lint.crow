import
	crow.cell: cell, swap, subscript
	crow.col.arr-util: contains?, each-with-index, exists?
	crow.col.mut-list: ~=, move-to-arr!, mut-list
	crow.io.file: each-child-recursive, read-file
	crow.io.path: base-name, get-extension
	crow.io.print: print
	crow.str-util: ends-with?, has-substr?, starts-with?, try-remove-start
	.failure: failure, flat-map-with-max-size, max-failures, print-tests?, test-options

lint result<str, arr failure>(path str, options test-options) summon
	files = path list-lintable-files
	failures = files flat-map-with-max-size options.max-failures, file =>
		if options.print-tests?
			print: "lint {file}"
		file lint-file
	if failures has?
		err: failures
	else
		ok: "linted {files size} files"

.list-lintable-files arr str(path str) summon
	res = mut-list<str>
	path each-child-recursive (child => child.excluded-from-lint? not), child =>
		if child.base-name.ignore-extension-of-name? not
			res ~= child
	res move-to-arr!

.excluded-from-lint? bool(name str)
	exclude-exts = [".bmp", ".err", ".html", ".mdb", ".png", ".repr", ".svg", ".ttf", ".wasm", ".webp", ".xz"]
	exclude-names = ["documentation", "dyncall", "libfirm", "node_modules", "package-lock.json"]
	name starts-with? "." or: exclude-names contains? name or: exclude-exts exists? ext => name ends-with? ext

# Returns numner of errors
.lint-file arr failure(path str) summon
	text = path read-file
	res = mut-list<failure>
	ext = path.get-extension force
	allow-double-space? = ext == "err" or: ext == "sublime-syntax"
	text.lines each-with-index (line, line-num) =>
		ln = line-num + 1 to-str
		space-space = " " ~ " "
		if allow-double-space?.not and: line has-substr? space-space
			message = "line {ln} contains a double space"
			res ~=: failure: path, message
		width = line line-len
		if width > max-line-length
			message = "line {ln} is {width} columns long, should be <= {max-line-length}"
			res ~=: failure: path, message
	res move-to-arr!

.ignored-extensions arr str()
	["c", "data", "o", "out", "repr", "tmLanguage"]

.ignore-extension? bool(ext str)
	ignored-extensions contains? ext

.ignore-extension-of-name? bool(name str)
	if ext ?= name get-extension
		ext ignore-extension?
	else
		# Ignore extensionless files
		true

# TODO: move to str-util, make a generic 'split' function
.lines arr str(s str)
	res = mut-list<str>
	last-nl = 0 cell<nat>
	s.chars each-with-index (c, index) =>
		if c == "\n"
			nl = last-nl swap index + 1
			res ~= s.chars[nl -> index] str
	res ~= s.chars[last-nl[] -> s.size-bytes] str
	res move-to-arr!

.max-line-length nat()
	120

.n-tabs nat(line str)
	if rest ?= line try-remove-start "\t"
		rest.n-tabs + 1
	else
		0

.tab-size nat()
	4

.line-len nat(line str)
	line.n-tabs * (tab-size - 1) + line.size-bytes
