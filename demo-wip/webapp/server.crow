import
	crow/col/arr-util: index-of
	crow/col/dict: new, subscript
	crow/col/fix-arr: fill-fix-arr, fix-arr, map-to-arr-with-index, set-subscript
	crow/io/file: read-file
	crow/io/net/http-server: http-handler, start-http-server
	crow/io/net/http-types:
		headers, http-bad-query, http-forbidden, http-not-found, http-ok, http-request,
		http-response, method, request-body-str, url
	crow/io/print: err, print
	crow/json: json, new, to-str
	crow/reader: parse-nat
	crow/str-util: split-by, try-split-once
	./db:
		add-post, close-real-db, content, cookie-expire-seconds, db, list-posts, list-users,
		login, post, post-info, open-real-db, parse-user-cookie, register-user, to-str, user,
		user-info, user-name

main fut nat(args str[]) unsafe summon
	if args.size == 1
		port = args[0]
		db = open-real-db
		handler http-handler = req =>
			(req handle-request db),
		_ = port start-http-server handler
		# TODO: do this after we've stopped the server
		if false
			db close-real-db
		()
	else
		err print "Usage: server PORT"
		1,

.handle-request http-response(req http-request, db db) summon
	su = req.url split-url
	path = su path
	query = su query
	match req method
	as http-get
		if path == "/"
			"demo-wip/webapp/index.html".read-file http-ok
		elif path == "/index.js"
			"demo-wip/webapp/index.js".read-file http-ok
		elif path == "/posts"
			(("posts", db.list-posts.json),)::json.to-str http-ok
		elif path == "/user-name"
			user = (query parse-query ("user",))[0].parse-nat!
			db user-name user http-ok
		elif path == "/users"
			(("users", db.list-users.json),)::json.to-str http-ok
		else
			http-not-found
	as http-post
		if path == "/register"
			args = req.request-body-str parse-query ("user-name", "password")
			user-name = args[0]
			password = args[1]
			match db register-user user-name, password
			as register-user-success user-id
				# We need to get: user-name and password
				"\{\"userId\":{user-id}}" http-ok
			as user-already-exists
				"User already exists" http-bad-query
		elif path == "/login"
			args = req.request-body-str parse-query ("user-name", "password")
			user-name = args[0]
			password = args[1]
			match db login user-name, password
			as ok cookie
				"Logged in as {user-name}" http-ok (("Set-Cookie", "login={cookie}; Max-Age={cookie-expire-seconds}"),)
			as err e
				match e
				as empty-name-or-password
					"Invalid user or password" http-bad-query
				as user-does-not-exist
					"User does not exist" http-not-found
				as wrong-password
					"Wrong password" http-forbidden
		elif path == "/post"
			if cookie ?= req.headers["Cookie"]
				user-cookie = (cookie parse-cookies ("login",))[0].parse-user-cookie!
				post = db add-post user-cookie, req.request-body-str
				"\{\"postId\":{post}}" http-ok
			else
				"No login cookie" http-forbidden
		else
			http-not-found
	#descr = req describe-request
	#200, "OK", dict, "Your request:\n{descr}"

url record
	path str
	query str

split-url url(url-str str)
	if split ?= url-str try-split-once "?"
		split.a, split.b
	else
		url-str, ""

# Extra cookies are ignored
parse-cookies str[](s str, cookie-names str[])
	res fix-arr str? = cookie-names.size fill-fix-arr ()
	for part : s split-by "; "
		if pair ?= part try-split-once "="
			if index ?= cookie-names index-of pair.a
				res[index] := pair.b,
	res map-to-arr-with-index (x, index) =>
		x ?? throw "Did not get cookie value for {cookie-names[index]}"

parse-query str[](s str, arg-names str[])
	res fix-arr str? = arg-names.size fill-fix-arr ()
	for part : s split-by "&"
		if pair ?= part try-split-once "="
			if index ?= arg-names index-of pair.a
				res[index] := pair.b,
			else
				throw "Unexpected key {pair a}"
		else
			throw "Invalid part: {part}"
	res map-to-arr-with-index (x, index) =>
		x ?? throw "Did not get argument for {arg-names[index]}"

.json json(a user-info)
	("user", a.user.json), ("userName", a.user-name.json)

.json json(a post-info)
	("post", a.post.json), ("user", a.user.json), ("userName", a.user-name.json), ("content", a.content.json)
