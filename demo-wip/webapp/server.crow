import
	crow/col/arr-util: each, index-of
	crow/col/dict: new, subscript
	crow/col/fix-arr: fill-fix-arr, fix-arr, map-to-arr-with-index, set-subscript
	crow/io/file: read-file
	crow/io/net/http-server: http-handler, start-http-server
	crow/io/net/http-types:
		headers, http-bad-query, http-forbidden, http-not-found, http-ok, http-request,
		http-response, method, request-body-str, url
	crow/io/print: print
	crow/str-util: split-by, try-split-once
	crow/writer: ~=, backup, move-to-str, new, writer
	./db:
		add-post, close-real-db, content, cookie-expire-seconds, db, list-posts, list-users,
		login, post, post-info, open-real-db, parse-user-cookie, register-user, to-str, user,
		user-info, user-name

main fut nat(args str[]) unsafe summon
	if args.size == 1
		port = args[0]
		db = open-real-db
		handler http-handler = req =>
			(req handle-request db),
		server = port start-http-server handler
		# TODO: do this after we've stopped the server
		if false
			db close-real-db
		drop: server
		()
	else
		print: "Usage: server PORT"
		1,

.handle-request http-response(req http-request, db db) summon
	su = req.url split-url
	path = su path
	query = su query
	match req method
	as http-get
		if path == "/"
			"demo-wip/webapp/index.html".read-file http-ok
		elif path == "/index.js"
			"demo-wip/webapp/index.js".read-file http-ok
		elif path == "/posts"
			db.list-posts.format-posts-as-json http-ok
		elif path == "/user-name"
			user = (query parse-query ("user",))[0].parse-nat!
			db user-name user http-ok
		elif path == "/users"
			db.list-users.format-users-as-json http-ok
		else
			http-not-found
	as http-post
		if path == "/register"
			args = req.request-body-str parse-query ("user-name", "password")
			user-name = args[0]
			password = args[1]
			match db register-user user-name, password
			as register-user-success user-id
				# We need to get: user-name and password
				"\{\"userId\":{user-id}}" http-ok
			as user-already-exists
				"User already exists" http-bad-query
		elif path == "/login"
			args = req.request-body-str parse-query ("user-name", "password")
			user-name = args[0]
			password = args[1]
			match db login user-name, password
			as ok cookie
				"Logged in as {user-name}" http-ok (("Set-Cookie", "login={cookie}; Max-Age={cookie-expire-seconds}"),)
			as err e
				match e
				as empty-name-or-password
					"Invalid user or password" http-bad-query
				as user-does-not-exist
					"User does not exist" http-not-found
		elif path == "/post"
			if cookie ?= req.headers["Cookie"]
				user-cookie = (cookie parse-cookies ("login",))[0].parse-user-cookie!
				post = db add-post user-cookie, req.request-body-str
				"\{\"postId\":{post}}" http-ok
			else
				"No login cookie" http-forbidden
		else
			http-not-found
	#descr = req describe-request
	#200, "OK", dict, "Your request:\n{descr}"

url record
	path str
	query str

split-url url(url-str str)
	if split ?= url-str try-split-once "?"
		split.a, split.b
	else
		url-str, ""

# Extra cookies are ignored
parse-cookies str[](s str, cookie-names str[])
	res fix-arr str? = cookie-names.size fill-fix-arr ()
	s split-by "; " each part =>
		if pair ?= part try-split-once "="
			if index ?= cookie-names index-of pair.a
				res[index] := pair.b,
	res map-to-arr-with-index (x, index) =>
		x or-throw () => "Did not get cookie value for {cookie-names[index]}"

parse-query str[](s str, arg-names str[])
	res fix-arr str? = arg-names.size fill-fix-arr ()
	s split-by "&" each part =>
		if pair ?= part try-split-once "="
			if index ?= arg-names index-of pair.a
				res[index] := pair.b,
			else
				throw: "Unexpected key {pair a}"
		else
			throw: "Invalid part: {part}"
	res map-to-arr-with-index (x, index) =>
		x or-throw () => "Did not get argument for {arg-names[index]}"

.format-users-as-json str(users user-info[])
	res writer = ()
	res ~= "\{\"users\":["
	users each user =>
		res ~= "\{\"user\":"
		res ~= user user
		res ~= ",\"userName\":\""
		res ~= user user-name
		res ~= "\"},"
	unless users is-empty
		res backup
	res ~= "]}"
	res move-to-str

.format-posts-as-json str(posts post-info[])
	res writer = ()
	res ~= "\{\"posts\":["
	posts each post =>
		# TODO: module for proper json formatting
		res ~= "\{\"post\":"
		res ~= post post
		res ~= ",\"user\":"
		res ~= post user
		res ~= ",\"userName\":\""
		res ~= post user-name
		res ~= "\",\"content\":\""
		res ~= post content
		res ~= "\"},"
	unless posts is-empty
		res backup
	res ~= "]}"
	res move-to-str
