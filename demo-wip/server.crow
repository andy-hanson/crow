import
	crow/io/net/http-server: http-handler, http-server, with-block
	crow/io/net/http-types: request-body, headers, http-request, method, new, to-string, url
	crow/io/print: err, out, print
	crow/io/stdin: read-stdin

main nat^(args string[]) summon
	if args.size == 1
		port = args[0]
		handler http-handler = req =>
			descr = req describe-request
			out print descr
			(200, "OK", (), "Your request:\n{descr}".to-bytes),
		with _ : port http-server handler
			out print "Press enter to stop"
			_ <- read-stdin
			()
	else
		err print "Usage: server PORT"
		1,

-describe-request string(a http-request)
	h = "\n" join for k, v : a headers
		"{k}: {v}"
	"{a method} {a url}\n{h}\n\n{a.request-body as-string}"
