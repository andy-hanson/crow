import
	crow/str-util: join
	crow/col/dict: dict, map-to-arr
	crow/io/net.http-server: http-handler, start-http-server
	crow/io/net.http-types: body, headers, http-request, http-response, method, to-str, url
	crow/io/print: print

main fut nat(args arr str) unsafe summon
	if args.size != 1
		print: "Usage: server PORT"
		1 resolved
	else
		port = args[0]
		handler = as<http-handler>: req =>
			descr = req describe-request
			print: descr
			resolved: http-response: 200, "OK", dict, "Your request:\n{descr}"
		server = port start-http-server handler
		drop: server
		0 resolved

.describe-request str(a http-request)
	h = a.headers map-to-arr (k, v) => "{k}: {v}"
	"{a method} {a url}\n{h join "\n"}\n\n{a body}"
