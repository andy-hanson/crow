import
	crow/private/alloc: mem-copy
	sndfile/headers:
		channels, frames, new, samplerate, sf_close, sf_count_t, sf_error, SF_INFO, SFM_READ,
		sf_open, sf_open_virtual, sf_read_float, SF_VIRTUAL_IO, SNDFILE
	sndfile/util: check-sndfile-error
	stdio: SEEK
	./sound: new, sound

read-sound-file sound(path string) trusted summon
	info cell SF_INFO = (),
	f = path.to-c-string sf_open SFM_READ, info.as-mut-pointer
	f common *info

sound-from-file-content sound(content array nat8) trusted
	info cell SF_INFO = (),
	user-data = content, content.begin-pointer
	virtual SF_VIRTUAL_IO = &get_filelen, &seek, &read, &write, &tell
	f = virtual sf_open_virtual SFM_READ, info.as-mut-pointer, user-data.as-any-mut-pointer
	f common *info

.common sound(f SNDFILE, info SF_INFO) unsafe
	f.sf_error check-sndfile-error
	n-channels = info.channels to-nat64
	sample-rate = info.samplerate to-nat64
	samples float32 mut[] = info.frames * n-channels uninitialized-mut-list
	n-read = f sf_read_float samples.begin-pointer, samples.size
	f.sf_close check-sndfile-error
	assert n-read == samples.size
	(n-channels, sample-rate), samples.move-to-array

.get_filelen sf_count_t(user-data-pointer any-mut-pointer) noctx unsafe
	a user-data = user-data-pointer as-ref
	a.content size

.seek sf_count_t(offset sf_count_t, whence SEEK, user-data-pointer any-mut-pointer) noctx unsafe
	a user-data = user-data-pointer as-ref
	start = match whence
	as SEEK_SET
		a.content begin-pointer
	as SEEK_CUR
		a cur
	as SEEK_END
		a.content end-pointer
	pos = start + offset
	assert a.content.begin-pointer <= pos && pos <= a.content.end-pointer
	a.cur := pos
	pos - a.content.begin-pointer

.read sf_count_t(pointer nat8 mut*, count sf_count_t, user-data-pointer any-mut-pointer) noctx unsafe
	a user-data = user-data-pointer as-ref
	n-to-read = count min (a.content.end-pointer - a.cur)
	pointer mem-copy a.cur, n-to-read
	a.cur := a.cur + n-to-read
	n-to-read

.write sf_count_t(_ nat8*, _ sf_count_t, _ any-mut-pointer) noctx unsafe
	unreachable

.tell sf_count_t(user-data-pointer any-mut-pointer) noctx unsafe
	a user-data = user-data-pointer as-ref
	a.cur - a.content.begin-pointer

.user-data record by-ref mut
	content array nat8
	cur mut nat8*
