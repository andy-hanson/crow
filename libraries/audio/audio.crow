no-std
import
	crow/bool: bool, false, true
	crow/fun-util: subscript
	crow/fut: fut, subscript
	crow/misc: void
	./sound: sound, sound-format

audio record sendable
	format sound-format
	clear fun void()
	play fun playing-sound(sound)
	play-loop fun playing-sound(sound)
	shut-down ref void()

clear void(a audio)
	cb fun void() = a clear
	cb[]

play playing-sound(a audio, sound sound)
	a.play[sound]

play-loop playing-sound(a audio, sound sound)
	a.play-loop[sound]

shut-down fut void(a audio)
	cb ref void() = a shut-down
	cb[]

playing-sound record sendable
	reset-position fun void()
	# NOTE: This doesn't reset the sound's position
	set-playing fun void(bool)

# Also known as 'pause'
pause void(a playing-sound)
	a.set-playing[false]

stop void(a playing-sound)
	a.set-playing[false]
	a.reset-position[]

###
WARN: This doesn't reset the sound's position.
If it's already over, this has no effect.
###
resume void(a playing-sound)
	a.set-playing[true]

restart void(a playing-sound)
	a.reset-position[]
	a.set-playing[true]
