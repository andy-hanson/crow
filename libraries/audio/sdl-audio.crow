import
	crow/c-types: c-int
	crow/private/alloc: unmanaged-alloc, unmanaged-free
	crow/private/thread-utils: acquire, release
	SDL2/SDL: SDL_Init, SDL_INIT_AUDIO, SDL_Quit
	SDL2/SDL_audio:
		AUDIO_F32, new, SDL_AudioSpec, SDL_CloseAudioDevice, SDL_OpenAudioDevice,
		SDL_PauseAudioDevice
	./audio: audio, new
	./audio-queue: audio-queue, clear, lock, new, play, play-loop, pull
	./sound: n-channels, new, sample-rate, sound-format

+start-sdl-audio audio() summon
	trusted
		err = SDL_INIT_AUDIO SDL_Init
		assert err == 0

		samples nat16 = 4096
		format sound-format = 2, 48000
		queue audio-queue = format,
		write-ctx mut write-ctx mut* = (queue,) unmanaged-alloc

		sr int32 = format.sample-rate to
		nc nat8 = format.n-channels to
		aspec SDL_AudioSpec = sr, AUDIO_F32, nc, 0, samples, 0, 0, &audio-cb, write-ctx.pointer-cast
		desired SDL_AudioSpec = aspec
		obtained mut SDL_AudioSpec = ()
		device = null SDL_OpenAudioDevice 0, &desired, &obtained, 0
		forbid device == 0

		device SDL_PauseAudioDevice 0

		format: format
		clear: () => queue clear
		play: x => queue play x
		play-loop: x => queue play-loop x
		shut-down: shared () =>
			queue.lock acquire
			assert write-ctx != null
			device SDL_CloseAudioDevice
			SDL_Quit
			write-ctx unmanaged-free
			write-ctx := null
			queue.lock release
			()

-write-ctx record(queue audio-queue) by-val, mut

-audio-cb void(userdata any-mut-pointer, stream nat8 mut*, len c-int) bare, unsafe
	queue = (userdata.pointer-cast::write-ctx mut*)->queue
	floats float32 mut-array = stream[0 .. len.to] cast-mut-array
	queue pull floats
