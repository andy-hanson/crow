import
	..bootstrap: c-str, data, deref, incr, not, null, null?, ptr, str, void
	..collection.arr: +, arr-from-begin-end
	..collection.dict: add, dict, each, key-value-pair, move-to-dict, mut-dict, new-mut-dict
	..collection.mut-list: move-to-arr, new-mut-list, push
	..str-utils: find-char-in-cstr, find-cstr-end, to-c-str
	..posix.unistd: environ

environ alias
	dict<str, str>

get-environ environ() summon trusted
	res = new-mut-dict<str, str>
	environ get-environ-recur res
	res move-to-dict

get-environ-recur void(env ptr c-str, res mut-dict<str, str>) unsafe
	if env.deref.null? not
		res add env.deref.parse-environ-entry
		env.incr get-environ-recur res

parse-environ-entry key-value-pair<str, str>(entry c-str) unsafe
	key-end = entry find-char-in-cstr "="
	key = entry arr-from-begin-end key-end
	value-begin = key-end incr
	value-end = value-begin find-cstr-end
	value = value-begin arr-from-begin-end value-end
	| TODO: should be able to infer type arguments
	key-value-pair<str, str>: key, value

convert-environ ptr c-str(environ environ)
	res = new-mut-list<c-str>
	environ each \key, value
		res push: key + "=" + value to-c-str
	res push null
	res.move-to-arr data
