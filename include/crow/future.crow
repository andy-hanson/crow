no-std
import
	./bool: bool
	./col/collection:
		begin, build, concatenate, flatten, for-loop, iterate, next, size, subscript, with-block,
		with-index
	./col/list: ~~, +, ~=, begin, build, is-empty, iterate, list, new, next, size
	./col/mut-list: ~=, hint-size, move-to, mut-list, new
	./exception: catch, exception
	./fun-util: subscript
	./misc: break-or-continue, shared, loop-break, loop-continue, new, void
	./number: ==, -, nat64
	./option: new, option
	./pointer: *, mut-pointer
	./private/bootstrap: lk, new, rejected, resolved
	./private/future-low-level:
		add-task, add-task-no-exclusion, get-result, forward-to, reject, resolve, unresolved
	./private/runtime:
		cur-exclusion, enqueue-task, enqueue-task-no-exclusion, exception-handler, get-global-ctx
	./private/task-queue: task
	./private/thread-utils: init, lock, uninit
	./result: force, result
export
	./private/bootstrap: future

-lock[t] lock mut*(a t^) unsafe
	&a.lk

###
A future already resolved to void.
Writing `<- delay` will cause the code below to run in a new task.
This can be useful for breaking up big tasks.

`delay` from `crow/io/time` is useful for delaying for linger times.
###
+delay void^()
	()

# Converts a value to a future resolved with that value.
+new[t] t^(value t)
	res t^ = trusted uninit, value.resolved
	trusted res.lock init
	res

# Future resolved to `()`.
+new[t] t^() t new
	(),

# Future resolved with an exception.
+rejected[t] t^(err exception)
	res t^ = trusted uninit, err.rejected
	trusted res.lock init
	res

###
Future that completes when every one in the list completes.

If one of `futures` has exception, this resolves to the leftmost exception (discarding others),
regardless of which exception happened first.
###
+wait-all void^(futures void^[])
	<- futures wait-all-complete
	for x : futures
		trusted x.get-result!
	()

###
Waits for every one of `futures` to complete, then returns a list all of their results.

If one of `futures` has exception, this resolves to the leftmost exception (discarding others),
regardless of which exception happened first.
###
+wait-all[t] t[]^(futures t^[]) t shared
	<- futures wait-all-complete
	(for x : futures; trusted x.get-result!),

###
Returns a future that is resolved once all inputs are resolved.
WARN: This resolves successfully even if an input is rejected.
###
-wait-all-complete[t] void^(futures t^[]) t shared
	unless futures is-empty
		res void^ = trusted unresolved
		count mut nat64 = futures.size
		task task = trusted cur-exclusion, () =>
			count -:= 1
			if count == 0
				res resolve ()
		for x : futures
			trusted x add-task task
		res

###
Future resolved to `ok` if `a` succeeds and `err` if `a` fails with an exception.
###
+future-catch[t] (t, exception) result^(a t^) t shared
	res (t, exception) result^ = trusted unresolved
	trusted a add-task-no-exclusion () =>
		res resolve a.get-result
	res

+future-finally record(cleanup void^ mut()) mut, nominal
+with-block[t] t^(a future-finally, f t^ mut()) t shared
	fut = f[]
	res t^ = trusted unresolved
	trusted fut add-task cur-exclusion, () =>
		result = fut get-result
		cleanup-fut void^ = a cleanup
		cleanup-fut add-task-no-exclusion () =>
			match result
			as ok value
				match cleanup-fut get-result
				as ok _
					res resolve value
				as error e
					res reject e
			as error e
				res reject e
	res

###
Calls `f` once `a` is resolved.
Returns a future for the result of `f`.

If `a` is resolved with an exception, this never calls `f`,
and the result of `then` will have the same exception as `a`.
###
+then[out, in] out^(a in^, action out^ mut(param in)) out shared, in shared
	res out^ = trusted unresolved
	trusted a add-task cur-exclusion, () =>
		match a get-result
		as ok value
			match (() => action[value]) catch
			as ok fut
				res forward-to fut
			as error e
				res reject e
		as error e
			res reject e
	res

+|[t, u] (t, u)^(a t^, b u^) t shared, u shared
	x <- a
	y <- b
	(x, y),

# Concatenates the values inside the futures.
+~~[col] col^(a col^, b col^) col shared, col concatenate
	x <- a
	y <- b
	x ~~ y,

# This is called by the runtime for 'shared () => ...' lambdas.
shared-of-mut-lambda[r, p] r^ shared(param p)(a r^ mut(param p)) r shared, p shared
	trusted
		exclusion = cur-exclusion
		wrapper (r, p) force-lambda-shared = a,
		param =>
			res r^ = unresolved
			get-global-ctx enqueue-task exclusion, () =>
				match (() => wrapper.fn[param]) catch
				as ok fut
					res forward-to fut
				as error e
					res reject e
			res
-force-lambda-shared[r, p] record(fn r^ mut(param p)) force-shared

###
If the future resolves successfully, does nothing.
Else, this will call the default exception handler.
###
handle-exceptions[t] void(a t^)
	trusted a add-task-no-exclusion () =>
		match a get-result
		as ok _
			()
		as error e
			get-global-ctx->exception-handler[e]

+fire-and-forget record nominal
+with-block void(_ fire-and-forget, cb void^ mut())
	cb[] handle-exceptions

+loop-break[t] t break-or-continue^(a t)
	a.loop-break,
+loop-continue[t] t break-or-continue^()
	loop-continue,

+parallel record nominal
###
Run `f` in a task with a new exclusion and return a future for its result.

`f` must be a `fun` and not an `act` so that it can have a new exclusion,
which allows it to run in parallel.
###
+with-block[t] t^(_ parallel, f t^ shared())
	res t^ = trusted unresolved
	gctx = trusted get-global-ctx
	trusted gctx enqueue-task-no-exclusion () =>
		match (() => f[]) catch
		as ok fut
			res forward-to fut
		as error e
			res reject e
	res

+sequential[col] record(collection col) nominal

+for-break[in-col, in-iter, in-elem] void^(
	a in-col sequential,
	f void break-or-continue^ mut(elem in-elem),
) (in-iter, in-col) begin, (in-elem, in-iter) next, in-elem shared
	_ void? <- a for-break f

+for-break[out, in-col, in-iter, in-elem] out?^(
	a in-col sequential,
	f out break-or-continue^ mut(elem in-elem),
) (in-iter, in-col) begin, (in-elem, in-iter) next, out shared, in-elem shared
	a.collection.begin for-break-recur f
-for-break-recur[out, in-col, in-iter, in-elem] out?^(
	iter in-iter,
	f out break-or-continue^ mut(elem in-elem),
) (in-iter, in-col) begin, (in-elem, in-iter) next, out shared, in-elem shared
	if x, next ?= iter next
		b-or-c <- f[x]
		match b-or-c
		as loop-break v
			(v,),
		as loop-continue
			next for-break-recur@(out, in-col, in-iter, in-elem) f

+for-loop[in-col, in-iter, in-elem] void^(
	a in-col sequential,
	f void^ mut(elem in-elem),
) (in-iter, in-col) begin, (in-elem, in-iter) next, in-elem shared
	for x : a
		<- f[x]
		continue

+for-loop[out-elem, in-col, in-iter, in-elem] out-elem[]^(
	a in-col sequential,
	f out-elem^ mut(elem in-elem),
) (in-iter, in-col) begin, (in-elem, in-iter) next, in-col size, out-elem shared, in-elem shared
	res out-elem mut[] = ()
	res hint-size a.collection.size
	<- for x : a
		y <- f[x]
		res ~= y
		()
	res.move-to,

+parallel-collection[col] record(collection col)
+parallel[col] col parallel-collection(a col)
	a,
+for-loop[in-col, in-elem] void^(
	a in-col parallel-collection,
	f void^ shared(elem in-elem),
) (in-col, in-elem) iterate, in-col size, in-elem shared
	tasks void^[] = for x : a collection
		with : parallel
			f[x]
	tasks wait-all
+for-loop[out-elem, in-col, in-elem] out-elem[]^(
	a in-col parallel-collection,
	f out-elem^ shared(elem in-elem),
) out-elem shared, in-elem shared, (in-col, in-elem) iterate, in-col size
	tasks out-elem^[] = for x : a collection
		with : parallel
			f[x]
	tasks wait-all

+sequential-concat[col] record(collection col) nominal
+for-loop[out-elem, in-col, in-iter, in-elem] out-elem[]^(
	a in-col sequential-concat,
	f out-elem[]^ mut(elem in-elem),
) (in-iter, in-col) begin, (in-elem, in-iter) next, in-col size, out-elem shared, in-elem shared
	lists out-elem[][] <- a.collection.sequential for-loop f
	lists.flatten,

+parallel-concat[col] record(collection col) nominal
+for-loop[out-elem, in-col, in-elem] out-elem[]^(
	a in-col parallel-concat,
	f out-elem[]^ shared(elem in-elem),
) out-elem shared, in-elem shared, (in-col, in-elem) iterate, in-col size
	lists out-elem[][] <- a.collection.parallel for-loop f
	lists.flatten,

+filter[out-col, in-col, elem, builder] out-col^(
	a in-col parallel-collection,
	f bool^ shared(elem elem),
) (in-col, elem) iterate, in-col size, (out-col, elem, builder) build, elem shared, out-col shared
	bools bool[] <- for x : a; f[x]
	res out-col = with out : a.collection.size build
		# TODO: 'zip'
		for i, x : a.collection with-index
			if bools[i]
				out ~= x
	res,
