no-std
import
	./bool: bool
	./col/array: array, iterate
	./col/collection: build, for-loop, with-block
	./col/list: ~=, build, iterate, list, size
	./json: json, to
	./misc: new
	./string: join, string
	./symbol: symbol, to

###
Spec that is satisfied if 't' is a 'flags' type
###
+flags[t] spec builtin

# Value with no flags set.
+new[t] t() builtin, bare, t flags
# Value with all flags set.
+all[t] t() builtin, bare, t flags

+==[t] bool(a t, b t) builtin, bare, t flags

# Negation: Result contains all flags *not* in 'a'
+ ~[t] t(a t) builtin, bare, t flags
# Union: Result contains all flags in *either* 'a' or 'b'
+ |[t] t(a t, b t) builtin, bare, t flags
# Intersection: Result contains only flags in *both* 'a' and 'b'
+ &[t] t(a t, b t) builtin, bare, t flags

# Returns every name and value in the enum.
+flags-members[t] (symbol, t) array() builtin, bare, t flags

###
True if every flag set in `a` is set in `b`.
###
+is-in[t] bool(a t, b t) bare, t flags
	a & b == a

# Returns the names of every flag set in `a`.
+to[t] symbol[](a t) t flags
	with out : build
		for name, value : flags-members@t
			if value is-in a
				out ~= name

# Flag names separated by `|`, like `"foo | bar"`.
+to[t] string(a t) t flags
	" | " join for x : a.to::symbol[]; x to

+to[t] json(a t) t flags
	a.to::symbol[] to
