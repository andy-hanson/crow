no-std
import
	./bool: bool
	./col/array: array
	./col/list: for-loop, join, list, new, to-list
	./misc: name, named-val, val
	./string: join, literal, string
	./sym: sym, to-string

###
Spec that will be satisfied by generated functions for any `flags` type.
Functions in this spec will be generated when you declare a `flags` type.

(Declaring a `flags` type also generates other functions that aren't needed here.)
###
flags-like<a> spec
	new a()
	all a()
	flags-members array named-val a()
	== bool(a a, b a)
	~ a(a a)
	& a(a a, b a)
	| a(a a, b a)

min-flags-like<a> spec
	== bool(a a, b a)
	& a(a a, b a)

###
True if every flag set in `a` is set in `b`.
###
is-in<a> bool(a a, b a) min-flags-like<a>
	a & b == a

# Returns the names of every flag set in `a`.
to-syms<a> sym[](a a) flags-like<a>
	() join for member : flags-members.to-list
		if member.val is-in a
			member.name,

# Flag names separated by `|`, like `"foo | bar"`.
flags-to-string<a> string(a a) flags-like<a>
	" | " join for x : a to-syms
		x to-string
