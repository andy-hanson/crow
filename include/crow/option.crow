no-std
import
	./bool: bool, false, new, true
	./compare: equal
	./number: nat64, new
	./private/bootstrap: none, some
export
	./private/bootstrap: option

# Empty option.
!new[t] t?() noctx
	none

# Option containing `a`.
!new[t] t?(a t) noctx
	a some

!==[t] bool(a t?, b t?) noctx, t equal
	if va ?= a
		if vb ?= b
			va == vb
	else
		b is-empty

# True iff `a` is an empty option.
!is-empty[t] bool(a t?) noctx
	# TODO: `if _ ?= a`
	if x ?= a
		_ = x
		false
	else
		true

# 1 if `a` has a value, else 0.
!size[t] nat64(a t?) noctx
	unless a is-empty
		1

###
Lazily-evaluated.
If `a` is non-empty, returns it and does not evaluate `b`.
Else, returns `b`.
###
!||[t] t?(a t?, b t?) builtin

###
Lazily-evaluated. 
If `a` is non-empty, returns its value and does not evaluate `b`.
Else, returns `b`.
###
!??[t] t(a t?, default t) builtin

###
Called like 'a!'
If `a` is non-empty, returns its value; else throws.
###
!force[t] t(a t?)
	a ?? throw "tried to force empty option"
