no-std
import
	./bool: !, &&, ||, bool, false, new, true
	./col/mut-list: ~=, move-to-list, mut-list, new
	./fun-util: subscript
	./misc: new, void
	./number: +, *, **, float64, int64, nat64, to-float64, to-int64
	./option: ??, is-empty, new, option
	./string: ==, as-string, char8, literal, mut-iter, mut-string-iter, next, peek, restore, save, string
	./test-util: is

###
Stateful type used for simple parsers that read strictly left-to-right.

Once it gets to the end, `is-empty` will return `true`.
###
reader record mut
	.iter mut mut-string-iter

new reader()
	"",

new reader(a string) trusted
	a.mut-iter,

###
Creates a reader for the string, calls `f`, and returns the result.

Returns an empty option if the reader did not make it to the end of the string.
###
with-reader<a> a?(a string, f act a?(reader))
	reader reader = a,
	res = f[reader]
	if !res.is-empty && reader.is-empty
		res

# `true` iff the reader has reached the end of the string.
is-empty bool(a reader) trusted
	a.iter.peek is-empty

###
Gets the next character without advancing.
Throws if the reader has reached the end.
###
peek char8(a reader) trusted
	a.iter.peek ?? throw "reader empty"

###
Gets the next character and advances.
Throws if the reader had already reached the end before calling `next`.
###
next char8(a reader) trusted
	a.iter.next ?? throw "reader empty"

###
Iff the next character is `c`, advances and returns `true`.
Else, this does nothing and returns `false`.

If the reader had already reached the end of the string before calling this, returns `false`.
###
try-take-char bool(a reader, c char8)
	if peek ?= a.iter peek
		if peek == c
			_ = a next
			true

###
Like `try-take-char` but for a whole string.

If the next `s.size` characters to be read match `s`, takes them all and returns `true`.
Else, this does nothing and returns `false`.

This will not read only part of `s`; it's all or nothing.
###
try-take-string bool(a reader, s string) trusted
	start = a.iter save
	if a.iter try-take-string-recur s.mut-iter
		true
	else
		a.iter restore start
		false

try-take-string-recur bool(a mut-string-iter, b mut-string-iter)
	if nb ?= b next
		if na ?= a next
			na == nb && a try-take-string-recur b
	else
		true

.nat-and-count-digits record
	.value nat64
	.count-digits nat64

###
Takes a number (a sequence of digits 0-9). Stops at the first non-digit.
Does not take separator characters like `,` or `_`.

Returns an empty option if it could not parse any digits.
###
take-nat nat64?(a reader)
	if x ?= a.take-nat-and-count-digits
		x.value,
.take-nat-and-count-digits nat-and-count-digits?(a reader)
	unless a is-empty
		if first-digit ?= a.peek.char-to-nat64
			_ = a next
			(first-digit take-nat-and-count-digits-recur 1, a),
.take-nat-and-count-digits-recur nat-and-count-digits(acc nat64, count-digits nat64, a reader)
	if a is-empty
		acc, count-digits
	elif v ?= a.peek.char-to-nat64
		_ = a next
		acc * 10 + v take-nat-and-count-digits-recur count-digits + 1, a
	else
		acc, count-digits

# This is like `take-nat`, but the number may start with a `+` or `-` sign.
take-int int64?(a reader) trusted
	start = a.iter save
	sign = if a try-take-sign
		+1
	else
		-1
	if n ?= a take-nat
		n.to-int64 * sign,
	else
		a.iter restore start
		()

# TODO: use 'is-whitespace' function
# Skips whitespace characters: `" "`, `"\n"`, `"t"`.
skip-whitespace void(a reader)
	if a try-take-char " " || a try-take-char "\r" || a try-take-char "\n" || a try-take-char "\t"
		a skip-whitespace

.try-take-sign bool(a reader)
	if a try-take-char "-"
		false
	else
		_ = a try-take-char "+"
		true

###
Takes a floating-point number. This is:
* Optionally: A `+` or `-` sign
* A series of digits (`0` - `9`)
* Optionally: A decimal point (`.`), then more digits.
###
take-float float64?(a reader) trusted
	sign = if a.try-take-sign
		1.0
	else
		-1.0
	if whole-nat ?= a take-nat
		whole-part = whole-nat to-float64
		before-dot = a.iter save
		if a try-take-char "."
			if nd ?= a take-nat-and-count-digits
				# TODO: more accurate methods exist
				sign * (whole-part + nd.value.to-float64 * 0.1 ** nd.count-digits),
			else
				a.iter restore before-dot
				sign * whole-part,
		else
			sign * whole-part,

###
Takes a quoted string.

Like in crow, a quoted string begins and ends with `"`.
It can use `\` to escape characters.
Unlike in crow, `{` is not a special character.

Does nothing and returns an empty option if it did not take an entire quoted string correctly.
###
take-quoted-string string?(a reader)
	start = a.iter save
	if a try-take-char "\""
		res = a take-quoted-string-recur ()
		if res is-empty
			a.iter restore start
		res

.take-quoted-string-recur string?(a reader, res char8 mut[])
	unless a is-empty
		if a try-take-char "\""
			res.move-to-list.as-string,
		elif a try-take-char "\\"
			if esc ?= a.next escape
				res ~= esc
				a take-quoted-string-recur res
		else
			res ~= a next
			a take-quoted-string-recur res

.escape char8?(a char8)
	if a == "n"
		"\n",
	elif a == "r"
		"\r",
	elif a == "t"
		"\t",
	elif a == "\""
		"\"",
	elif a == "\\"
		"\\",
	elif a == "0"
		"\0",

.char-to-nat64 nat64?(c char8)
	if c == "0"
		0,
	elif c == "1"
		1,
	elif c == "2"
		2,
	elif c == "3"
		3,
	elif c == "4"
		4,
	elif c == "5"
		5,
	elif c == "6"
		6,
	elif c == "7"
		7,
	elif c == "8"
		8,
	elif c == "9"
		9,

###
Parse digits as a nat64.
Return an empty option if the string contains any punctuation or whitespace.
###
parse-nat nat64?(a string)
	a with-reader r => r take-nat

###
Same as `a with-reader r => r take-int`.
###
parse-int int64?(a string)
	a with-reader r => r take-int

###
Same as `a with-reader r => r take-float`.
###
parse-float float64?(a string)
	a with-reader r => r take-float

test
	"a" is "a"
