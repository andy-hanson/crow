###
Functions for comparison.

If you want a type to be comparable, just define `==` and `<=>`.
Use the signatures defined in the `compare` spec.

Other functions like `!=` are defined here, derived from those.
###

no-std
import
	./private/bool-low-level: !, bool, false, true

###
Result of comparison.

If `a <=> b` is `equal`, then any function on `a` should return ths same result as a on `b`.
###
comparison enum
	less
	equal
	greater

###
Spec that only defines `==` but note `<=>`, since sometimes that's all that's needed.
###
equal<a> spec
	== bool(a a, b a)

###
When defined for mutable values, this should compare their content without regard to mutablility;
so it may consider values equal that may not be equal in the future.
###
compare<a> spec
	== bool(a a, b a)
	# This should return `equal` iff `==` would return `true`.
	<=> comparison(a a, b a)

# True iff `!(a == b)`.
!= <a> bool(a a, b a) noctx equal<a>
	!(a == b)

# True iff `(a <=> b) == less`.
< <a> bool(a a, b a) noctx compare<a>
	match a <=> b
	as less
		true
	as equal
		false
	as greater
		false

# True iff `(a <=> b) != greater`.
<= <a> bool(a a, b a) noctx compare<a>
	!(b < a)

# True iff `(a <=> b) == greater`.
> <a> bool(a a, b a) noctx compare<a>
	b < a

# True iff `(a <=> b) != less`.
>= <a> bool(a a, b a) noctx compare<a>
	!(a < b)

###
Returns the lesser argument.

Returns `b` if they are equal.
###
min<a> a(a a, b a) noctx compare<a>
	a < b ? a : b

###
Returns the greater argument.

Returns `b` if they are equal.
###
max<a> a(a a, b a) noctx compare<a>
	a > b ? a : b

###
Clamps a value to an inclusive range.
Returns the closest value to `a` that is >= min and <= max.

This means: If `a` is too small, return `min`. If `a` is too large, return `max`.
###
clamp<a> a(a a, min a, max a) noctx compare<a>
	a < min ? min : a > max ? max : a
