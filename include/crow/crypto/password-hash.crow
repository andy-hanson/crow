no-std
import
	sodium:
		crypto_pwhash_memlimit_moderate, crypto_pwhash_memlimit_sensitive,
		crypto_pwhash_opslimit_moderate, crypto_pwhash_opslimit_sensitive, crypto_pwhash_str,
		crypto_pwhash_strbytes, crypto_pwhash_str_verify
	../bool: bool
	../col/arr: size
	../col/fix-arr: begin-ptr, cast-immutable, fix-arr, uninitialized-fix-arr
	../col/private/arr-low-level: begin-ptr
	../col/list: list, size, to-array, to-list
	../number: ==, nat8
	../ptr: mut-ptr, ptr-cast
	../str: char8, char8s, str
	./private/sodium: ensure-sodium-initialized

###
These correspond to options from libsodium.
Use `quality-moderate` for most cases.
`quality-sensitive` is for non-interactive use cases.
###
password-hash-quality enum
	quality-moderate
	quality-sensitive

# Get the hash of a password.
password-hash list nat8(password str, quality password-hash-quality) trusted
	ensure-sodium-initialized
	strbytes = crypto_pwhash_strbytes
	assert strbytes == 128
	res fix-arr nat8 = strbytes uninitialized-fix-arr
	ops-limit = match quality
	as quality-moderate
		crypto_pwhash_opslimit_moderate
	as quality-sensitive
		crypto_pwhash_opslimit_sensitive
	mem-limit = match quality
	as quality-moderate
		crypto_pwhash_memlimit_moderate
	as quality-sensitive
		crypto_pwhash_memlimit_sensitive
	res-bytes char8 mut* = res.begin-ptr ptr-cast
	chars = password.char8s to-array
	err = res-bytes crypto_pwhash_str chars.begin-ptr, chars.size, ops-limit, mem-limit
	assert err == 0
	res.cast-immutable to-list

# With very high probabiliy, this will only be `true` if `hash` was the result of hashing `password`.
password-matches-hash bool(password str, hash list nat8) trusted
	assert hash.size == crypto_pwhash_strbytes
	chars = password.char8s to-array
	hash-array = hash to-array
	(hash-array.begin-ptr.ptr-cast crypto_pwhash_str_verify chars.begin-ptr, chars.size) == 0
