no-std
import
	setjmp: jmp_buf, __jmp_buf_tag, longjmp, new, setjmp, _setjmp
	../bool: bool
	../col/private/arr-low-level: new
	../compare: !=
	../fun-util: subscript
	../misc: new, void
	../ptr: ==, as-ref, by-val, null, ptr-to
	../version: is-interpreted, is-windows
	./backtrace: get-backtrace
	./bootstrap:
		c-str, err, exception-ctx-ptr, get-ctx, hard-assert, hard-unreachable, new, ok, result,
		thread-local-stuff
	./number-low-level: ==, int32, nat64
export
	./bootstrap: exception, message

exception-ctx record mut
	jmp-buf-ptr mut jmp_buf
	thrown-exception mut exception

exception-ctx by-val exception-ctx() noctx unsafe
	# Nothing on the exception-ctx initially
	null, ("", ((),))

get-exception-ctx exception-ctx() unsafe
	get-ctx.thread-local-stuff.exception-ctx-ptr as-ref

catch-with-exception-ctx<a> a(ec exception-ctx, try act a(), catcher act a(exception)) unsafe
	old-thrown-exception = ec thrown-exception
	old-jmp-buf = ec jmp-buf-ptr
	store __jmp_buf_tag = ()
	setjmp-result = if is-interpreted
		store.ptr-to setjmp
	elif is-windows
		store.ptr-to _setjmp
	else
		store.ptr-to setjmp
	if setjmp-result == 0
		ec.jmp-buf-ptr := store ptr-to
		# Get here after setting the jump.
		# If it throws, we'll go to the above branch.
		res = try[]
		ec.jmp-buf-ptr := old-jmp-buf
		ec.thrown-exception := old-thrown-exception
		res
	else
		# This is what we threw in 'throw'
		hard-assert: setjmp-result == number-to-throw
		# Get here if we throw inside of `try`
		thrown-exception = ec thrown-exception
		# NOTE: changing thrown-exception back probably isn't really necessary
		ec.jmp-buf-ptr := old-jmp-buf
		ec.thrown-exception := old-thrown-exception
		catcher[thrown-exception]

throw-c-str<a> a(message c-str) trusted
	message throw-c-str 2

throw-c-str<a> a(message c-str, skip nat64) trusted
	(message, skip get-backtrace) rethrow

###
Throw an existing exception object.

This is useful for rethrowing an exception that was already caught.
Throwing an exception does not modify its backtrace.
###
rethrow<a> a(e exception) trusted
	exn-ctx = get-exception-ctx
	hard-assert: exn-ctx.jmp-buf-ptr != null
	exn-ctx.thrown-exception := e
	exn-ctx.jmp-buf-ptr longjmp number-to-throw
	hard-unreachable

.number-to-throw int32()
	7

# Throw iff the condition is `false`.
assert void(condition bool)
	unless condition
		"assert failed" throw-c-str

###
Iff the condition is `false`, throw `message`.

`message` is eagerly evaluated, so it should be a string literal;
use `unless` and `throw` for a computed message.
###
assert-c-str void(message c-str, condition bool)
	unless condition
		message throw-c-str

# Throw iff the condition is `true`.
forbid void(condition bool)
	if condition
		"forbid failed" throw-c-str

###
Iff the condition is `true`, throw `message`.

The message is eagerly evaluated, so it should be a string literal;
use `if` and `throw` for a computed message.
###
forbid-c-str void(message c-str, condition bool)
	if condition
		message throw-c-str

###
Run `try` and return `ok` containing its return value,
or `err` containing an exception if it threw one.
###
catch<a> result<a, exception>(try act a())
	(() => try[] ok) catch e => e err

###
Run `try` and return what it returns, or return the result of `catcher` if `try` threw an exception.
(An exception thrown by `catcher` will not be caught.)
###
catch<a> a(try act a(), catcher act a(exception)) trusted
	get-exception-ctx catch-with-exception-ctx try, catcher
