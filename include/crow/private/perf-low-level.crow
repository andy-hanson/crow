no-std
import
	../col/array: array, size
	../col/mut-array:
		copy-from, fill, mut-array, free-unmanaged-mut-array, index-of, set-subscript, size,
		subscript, unmanaged-uninitialized-mut-array
	../fun-util: subscript
	../misc: void
	../number: ==, <=>, +, nat64
	../option: option
	../pointer: *, mut-pointer, set-deref
	../range: .., for-loop
	./symbol-low-level: ==, symbol

###
Enables performance measurement.
Only names in `names` will be measured; others will be ignored.

If measurement was already enabled, this sets all measurements to 0 (and may change measured names)
###
perf-enable-low-level void(measure-names symbol array) summon
	trusted
		*measure-names-var free-unmanaged-mut-array
		*measure-values-var free-unmanaged-mut-array
		*measure-names-var := measure-names.size unmanaged-uninitialized-mut-array
		*measure-names-var copy-from measure-names
		*measure-values-var := measure-names.size unmanaged-uninitialized-mut-array
		*measure-values-var fill (0, 0)

.measure-names-var symbol mut-array mut*() thread-local
.measure-values-var perf-measure-value mut-array mut*() thread-local

get-measure-index nat64?(measure-name symbol) noctx
	trusted *measure-names-var index-of measure-name

add-measure-value-at-index void(index nat64, nsec nat64) noctx
	values = trusted *measure-values-var
	cur = values[index]
	values[index] := cur.count + 1, cur.total-nsec + nsec

perf-measure-value record by-val
	count nat64
	total-nsec nat64

each-measure void(f act void(symbol, perf-measure-value))
	trusted
		measure-names = *measure-names-var
		measure-values = *measure-values-var
		for i : 0 .. measure-names.size
			f[measure-names[i], measure-values[i]]
