no-std
import
	../bool: &&, bool
	../col/array: array, begin, next
	../io/private/time-low-level: timestamp
	../misc: new, void
	../number: ==, <=>, nat64
	../option: option
	../pointer: as-const, const-pointer, mut-pointer
	../tuple: new
	./alloc: gc
	./bare-priority-queue:
		add-gc-roots, bare-priority-queue, dequeue-if-less-or-equal, destroy, enqueue, is-empty,
		new, peek-key
	./exclusion-queue:
		add-gc-roots, dequeue, destroy, enqueue, exclusion-queue, init, release-exclusion,
		slow-is-empty, uninit
export
	./bootstrap: action, exclusion, task

# WARN: `task-queue` is not thread-safe; acquire `tasks-lock` on `global-ctx` first.

task-queue record by-val, mut
	-non-timed mut void mut() exclusion-queue
	-timed mut (nat64, task) bare-priority-queue

# Use for asserts only
slow-is-empty bool(a task-queue*) bare, unsafe
	a.non-timed.slow-is-empty && a.timed.is-empty

uninit task-queue() bare, unsafe
	uninit, ()

init void(a task-queue mut*) bare, unsafe
	a.non-timed init

destroy void(a task-queue mut*) bare, unsafe
	a.non-timed destroy
	a.timed destroy

add-gc-roots void(a task-queue*, gc gc mut*) bare, unsafe
	a.non-timed add-gc-roots gc
	a.timed add-gc-roots gc

pop-task-result union mut
	task task
	# Indicates that there are no tasks in the queue. (There may be exclusions in use though.)
	# If any timed tasks exists, this has the time of the first one.
	no-task timestamp?

# Caller should call 'release-exclusion' after running the task
pop-task pop-task-result(a task-queue mut*, cur-time timestamp) bare, unsafe
	a move-timed-tasks cur-time
	if exclusion, action ?= a.non-timed dequeue
		(exclusion, action) task
	else
		a.timed.as-const.peek-key no-task

# Move timed tasks to the non-timed queue if they are ready
-move-timed-tasks void(a task-queue mut*, cur-time timestamp) bare, unsafe
	loop
		if t ?= a.timed dequeue-if-less-or-equal cur-time
			a.non-timed enqueue t.exclusion, t.action
			continue
		else
			break

release-exclusion void(a task-queue mut*, exclusion exclusion) bare, unsafe
	a.non-timed release-exclusion exclusion

enqueue-timed-task void(a task-queue mut*, time nat64, task task) bare, unsafe
	a.timed enqueue (time, task)

enqueue-task void(a task-queue mut*, task task) bare, unsafe
	a.non-timed enqueue task.exclusion, task.action

enqueue-tasks void(a task-queue mut*, tasks task array) bare, unsafe
	cur mut = tasks begin
	loop
		if task, next ?= cur next
			a enqueue-task task
			cur := next
			continue
		else
			break
