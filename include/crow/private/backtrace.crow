no-std
import
	system/win32:
		CONTEXT, CONTEXT_FULL, GetCurrentProcess, GetCurrentThread, HANDLE,
		IMAGE_FILE_MACHINE_AMD64, new, RtlCaptureContext
	system/windows/DbgHelp:
		AddrPC, FileName, IMAGEHLP_LINE64, IMAGEHLP_SYMBOL64, LineNumber, Name, new, Offset,
		STACKFRAME64, StackWalk64, SymCleanup, SymGetLineFromAddr64, SymGetSymFromAddr64,
		SymFunctionTableAccess64, SymInitialize, SYMOPT_LOAD_LINES, SymSetOptions
	../bool: ||, true
	../col/array: as-array, new
	../compare: <=
	../misc: new, void
	../option: new, option
	../private/number-low-level: ==, <=>, nat8, nat32, nat64, unsafe-to, wrap-add, wrap-sub
	../pointer: ==, <=>, +, *, as-const, mut-pointer, null, pointer-cast, set-deref, size-of
	../version: is-interpreted, is-windows
	./alloc: gc-ctx, mem-copy, try-alloc-uninitialized
	./bootstrap: hard-assert
	./c-string-util: char8, c-string, c-string-size-incl-nul
	./libunwind:
		new, unw_context_t, unw_cursor_t, unw_getcontext, unw_get_proc_name, unw_init_local,
		unw_step, unw_word_t
	./range-low-level: ..
export
	./bootstrap:
		backtrace, backtrace-entry, column-number, entries, file-path, function-name, line-number,
		new

-new backtrace() bare
	(),

# Taking 'gc' explicitly to avoid accidental uses
get-backtrace backtrace(gc gc-ctx, skip nat64) bare, unsafe
	max-entries nat64 = 16
	opt-entries backtrace-entry mut*? = gc try-alloc-uninitialized max-entries
	if entries ?= opt-entries
		if is-interpreted
			end = entries interpreter-backtrace max-entries, skip
			gc copy-strings entries, end
			(entries .. end).as-array,
		elif is-windows
			gc get-native-backtrace-windows entries, max-entries, (skip wrap-add 2)
		else
			gc get-native-backtrace-libunwind entries, max-entries, (skip wrap-add 1)

###
Implemented by interpreter only.
Returns the pointer to after the last entry written.
Result entries are temporary, must copy!
###
-interpreter-backtrace backtrace-entry mut*(
	out backtrace-entry mut*,
	max-entries nat64,
	skip nat64,
) builtin, bare, unsafe

-copy-strings void(gc gc-ctx, begin backtrace-entry mut*, end backtrace-entry mut*) bare, unsafe
	cur mut = begin
	until cur == end
		*cur := gc copy-c-strings *cur
		cur +:= 1

-copy-c-strings backtrace-entry(gc gc-ctx, a backtrace-entry) bare, unsafe
	(gc copy-c-string-or-empty a.function-name), (gc copy-c-string-or-empty a.file-path),
		a.line-number, a.column-number

-get-native-backtrace-windows backtrace(
	gc gc-ctx,
	entries backtrace-entry mut*,
	max-entries nat64,
	skip nat64,
) bare, unsafe
	if vars ?= gc try-alloc-windows-backtrace-vars
		gc get-windows-backtrace-with-vars entries, max-entries, (skip wrap-add 1), vars

-get-windows-backtrace-with-vars backtrace(
	gc gc-ctx,
	entries backtrace-entry mut*,
	max-entries nat64,
	skip nat64,
	vars windows-backtrace-vars,
) bare, unsafe
	process = GetCurrentProcess
	thread = GetCurrentThread

	ok0 = process SymInitialize null, true
	ok0 hard-assert

	# For some reason this has to be on the stack. Failed when in 'windows-backtrace-vars'
	context mut CONTEXT = CONTEXT_FULL,
	&context RtlCaptureContext
	frame mut STACKFRAME64 = &context,

	_ = SYMOPT_LOAD_LINES SymSetOptions
	max-end = entries + max-entries
	end = gc windows-walk-backtrace entries, max-end, skip, process, thread, &context, &frame, vars
	ok1 = process SymCleanup
	ok1 hard-assert
	(entries .. end).as-array,

-windows-backtrace-vars record by-val, mut
	offset nat64 mut*
	displacement nat32 mut*
	symbol IMAGEHLP_SYMBOL64 mut*
	line IMAGEHLP_LINE64 mut*

-try-alloc-windows-backtrace-vars windows-backtrace-vars?(gc gc-ctx) bare, unsafe
	if offset ?= gc.try-alloc-uninitialized :: nat64 mut*?
		if displacement ?= gc.try-alloc-uninitialized :: nat32 mut*?
			symbol-max-name-length nat64 = 255
			symbol-size = size-of@IMAGEHLP_SYMBOL64 wrap-add symbol-max-name-length wrap-add 1
			if symbol-buffer ?= (gc try-alloc-uninitialized symbol-size) :: nat8 mut*?
				symbol IMAGEHLP_SYMBOL64 mut* = symbol-buffer pointer-cast
				*symbol := symbol-max-name-length.unsafe-to,
				if line ?= gc.try-alloc-uninitialized :: IMAGEHLP_LINE64 mut*?
					*line := ()
					(offset, displacement, symbol, line),

-windows-walk-backtrace backtrace-entry mut*(
	gc gc-ctx,
	out backtrace-entry mut*,
	out-end backtrace-entry mut*,
	skip nat64,
	process HANDLE,
	thread HANDLE,
	context CONTEXT mut*,
	frame STACKFRAME64 mut*,
	vars windows-backtrace-vars,
) bare, unsafe
	if out == out-end
		out
	elif IMAGE_FILE_MACHINE_AMD64 StackWalk64 process, thread, frame,
			context.as-const.pointer-cast, null, &SymFunctionTableAccess64, null, null
		if skip == 0
			*out := gc windows-backtrace-entry process, frame, vars
			gc windows-walk-backtrace out + 1, out-end, 0, process, thread, context, frame, vars
		else
			gc windows-walk-backtrace out, out-end, (skip wrap-sub 1),
				process, thread, context, frame, vars
	else
		out

-windows-backtrace-entry backtrace-entry(
	gc gc-ctx,
	process HANDLE,
	frame STACKFRAME64 mut*,
	vars windows-backtrace-vars,
) bare, unsafe
	name c-string = if process SymGetSymFromAddr64 frame->AddrPC.Offset, vars.offset, vars.symbol
		gc copy-c-string-or-empty &vars.symbol->Name
	else
		""
	if process SymGetLineFromAddr64 frame->AddrPC.Offset, vars.displacement, vars.line
		name, (gc copy-c-string-or-empty vars.line->FileName), vars.line->LineNumber, 0
	else
		name, "", 0, 0

-copy-c-string-or-empty c-string(gc gc-ctx, a c-string) bare, unsafe
	size = a c-string-size-incl-nul
	if out ?= (gc try-alloc-uninitialized size) :: char8 mut*?
		out mem-copy a, size
		out as-const
	else
		""

-get-native-backtrace-libunwind backtrace(
	gc gc-ctx,
	entries backtrace-entry mut*,
	max-entries nat64,
	skip nat64,
) bare, unsafe
	cursor mut unw_cursor_t = ()
	context mut unw_context_t = ()
	err0 = &context unw_getcontext
	err0 == 0 hard-assert
	err1 = &cursor unw_init_local &context
	err1 == 0 hard-assert
	# omit 'get-native-backtrace' in addition to other skipped
	&cursor unwind-skip (skip wrap-add 1)
	end = gc unwind entries, entries + max-entries, &cursor
	(entries .. end).as-array,

-unwind-skip void(cursor unw_cursor_t mut*, skip nat64) bare, unsafe
	unless skip == 0
		_ = cursor unw_step
		cursor unwind-skip (skip wrap-sub 1)

# Returns one past the last entry written to
-unwind backtrace-entry mut*(
	gc gc-ctx,
	out-begin backtrace-entry mut*,
	out-end backtrace-entry mut*,
	cursor unw_cursor_t mut*,
) bare, unsafe
	out-cur mut = out-begin
	loop
		if out-cur == out-end || cursor.unw_step <= 0
			break out-cur
		else
			offset mut unw_word_t = 0
			buf-size nat64 = 256
			buf-try char8 mut*? = gc try-alloc-uninitialized buf-size
			if buf ?= buf-try
				err = cursor unw_get_proc_name buf, buf-size, &offset
				*out-cur := if err == 0
					# TODO: get filename and line number too
					buf.as-const, "", 0, 0
				else
					"", "", 0, 0
				out-cur +:= 1
				continue
			else
				*out-cur := "<<out of memory for backtrace>>", "", 0, 0
				break out-cur + 1
