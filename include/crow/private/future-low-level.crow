no-std
import
	../col/mut-list: ~=, ~~=, iterate, new, move-to, mut-list, prepend=
	../misc: new, void
	../pointer: mut-pointer
	../result: err, ok, result
	./bootstrap:
		empty, exception, forwarded, future, future-state, hard-unreachable, lk, new, one-task,
		rejected, resolved, set-state, state, tasks
	./runtime: enqueue-task, enqueue-tasks, get-global-ctx, new-exclusion
	./task-queue: exclusion, task
	./thread-utils: acquire, init, lock, release, uninit

unresolved[t] t^() unsafe
	res t^ = uninit, empty
	res.lock init
	res

-lock[t] lock mut*(a t^) noctx, unsafe
	&a.lk

resolve[t] void(a t^, value t) unsafe
	a resolve-or-reject value.resolved

reject[t] void(a t^, err exception) unsafe
	a resolve-or-reject err.rejected

-resolve-or-reject[t] void(a t^, new-state t future-state) unsafe
	a.lock acquire
	old-state = a state
	a.state := new-state
	a.lock release
	match old-state
	as empty
		()
	as one-task task
		get-global-ctx enqueue-task task
	as tasks tasks
		get-global-ctx enqueue-tasks tasks.move-to
	as forwarded _
		hard-unreachable
	as resolved _
		hard-unreachable
	as rejected _
		hard-unreachable

-or-forwarded[t, u] union shared
	direct t
	forwarded u^

# 'a' should be the result of 'unresolved-will-forward'; have it forward to 'target'
forward-to[t] void(a t^, target t^) unsafe
	a.lock acquire
	old-state = a state
	a.state := target forwarded
	a.lock release

	match old-state
	as empty
		()
	as one-task task
		target add-task task
	as tasks tasks
		target add-tasks tasks
	as forwarded _
		hard-unreachable
	as resolved _
		hard-unreachable
	as rejected _
		hard-unreachable

add-task-no-exclusion[t] void(a t^, action act void()) unsafe
	a add-task get-global-ctx.new-exclusion, action

add-task[t] void(a t^, exclusion exclusion, action act void()) unsafe
	a add-task (exclusion, action)

add-task[t] void(a t^, task task) unsafe
	a.lock acquire
	choice t add-tasks-choice = match a state
	as empty
		a.state := task one-task
		added
	as one-task existing-task
		a.state := (existing-task, task) tasks
		added
	as tasks existing-tasks
		existing-tasks ~= task
		added
	as forwarded to
		to forward
	as resolved _
		tasks-are-ready
	as rejected _
		tasks-are-ready
	a.lock release

	match choice
	as added
		()
	as forward to
		to add-task task
	as tasks-are-ready
		get-global-ctx enqueue-task task

-add-tasks[t] void(a t^, tasks task mut[]) unsafe
	a.lock acquire
	choice t add-tasks-choice = match a state
	as empty
		a.state := tasks tasks
		added
	as one-task existing-task
		tasks prepend= existing-task
		a.state := tasks tasks
		added
	as tasks existing-tasks
		existing-tasks ~~= tasks
		added
	as forwarded to
		to forward
	as resolved _
		tasks-are-ready
	as rejected _
		tasks-are-ready
	a.lock release

	match choice
	as added
		()
	as forward to
		to add-tasks tasks
	as tasks-are-ready
		get-global-ctx enqueue-tasks tasks.move-to

-add-tasks-choice[t] union shared
	added
	forward t^
	tasks-are-ready

# Assuming the future is resolved, get the result.
get-result[t] (t, exception) result(a t^) noctx, unsafe
	a.lock acquire
	res ((t, exception) result, t) or-forwarded = match a state
	as empty
		hard-unreachable
	as one-task _
		hard-unreachable
	as tasks _
		hard-unreachable
	as forwarded forwarded-to
		forwarded-to forwarded
	as resolved v
		v.ok direct
	as rejected e
		e.err direct
	a.lock release

	match res
	as direct v
		v
	as forwarded to
		to get-result
