no-std
import
	../bool: bool
	../col/array: iterate, size
	../col/collection: for-loop
	../col/dict: ~=, build, get-or-add
	../col/list: iterate, size
	../compare: comparison
	../misc: void
	../number: +
	../pointer: ==, +, as-const, mut-pointer, set-deref
	../string: ==, <=>, char8, char8s, c-string, hash-mix, literal, string
	../tuple: a, b, new
	./alloc: unmanaged-alloc-elements
	./bootstrap: new-symbol, static-symbols
	./runtime: get-global-ctx, global-ctx, set-symbols-table, symbols-lock, symbols-table
	./thread-utils: with-lock
export
	# TODO: don't export to-c-string
	../private/bootstrap: symbol, to-c-string

!== bool(a symbol, b symbol) noctx
	# (this is pointer equality)
	trusted a.to-c-string == b.to-c-string

# Same as `a.to-string <=> b.to-string`.
!<=> comparison(a symbol, b symbol)
	a.to-string <=> b.to-string

!to-string string(a symbol) noctx
	trusted a.to-c-string literal

init-symbol-table void(a global-ctx)
	a.symbols-table := for x : static-symbols
		x.to-string, x

# TODO: name 'to-symbol'
###
Interns a string.
This is O(log n), where 'n' is the number of unique symbols that exist.
###
!string-to-symbol symbol(a string)
	gctx = trusted get-global-ctx
	trusted &gctx.symbols-lock with-lock () =>
		match gctx.symbols-table get-or-add a, () => a.to-malloced-c-string new-symbol
		as got x
			x
		as added pair
			gctx.symbols-table := pair a
			pair b

.to-malloced-c-string c-string(a string) unsafe
	res char8 mut* = a.char8s.size + 1 unmanaged-alloc-elements
	cur mut = res
	for x : a char8s
		*cur := x
		cur := cur + 1
	assert cur == res + a.char8s.size
	*cur := "\0"
	res as-const
