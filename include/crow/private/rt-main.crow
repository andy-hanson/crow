no-std
import
	../cell: as-cell
	../col/arr: arr, for-loop
	../col/arr-util: tail
	../col/private/arr-low-level: subscript
	../fun-util: fun-ptr2, subscript
	../fut: delay, fut, handle-exceptions, then-void
	../misc: new, void
	../number: ==, <=>, +, -, int32, nat64, to-int32, to-int64, to-nat64
	../ptr:
		+, any-mut-ptr, as-any-mut-ptr, as-const, as-ref, by-val, const-ptr, mut-ptr, null,
		ref-of-val, set-deref, subscript
	../range: ..
	../str: c-str, str
	../str-util: to-str
	./alloc: unmanaged-alloc-elements, unmanaged-free
	./bootstrap: call-with-ctx, ctx, get-ctx, new, thread-local-stuff
	./fut-low-level: get-result
	./log-low-level: log-ctx
	./perf-low-level: perf-ctx
	./runtime:
		any-unhandled-exceptions, free, get-global-ctx, global-ctx, init, new-ctx, print-lock,
		thread-function, uninit
	./sym-low-level: init-symbol-table
	./thread-utils: count-processors, create-one-thread, join-one-thread, thread-handle

.main-ptr alias
	fun-ptr2<fut nat64, ctx, str[]>

# Treated specially by the compiler
rt-main int32(argc int32, argv c-str*, main-ptr main-ptr) trusted noctx summon
	do-init-constants
	n-threads = count-processors
	gctx-by-val by-val global-ctx = n-threads uninit
	gctx = gctx-by-val ref-of-val
	gctx init
	main-fut = gctx add-main-task n-threads - 1, argc, argv, main-ptr
	n-threads run-threads gctx
	res = gctx exit-code-from-main-fut main-fut
	gctx free
	res

.exit-code-from-main-fut int32(gctx global-ctx, main-fut fut nat64) unsafe noctx
	match main-fut get-result
	as ok v
		# TODO: handle nat64 too big for int32
		if gctx any-unhandled-exceptions
			1
		else
			v.to-int64 to-int32
	as err _
		# 'handle-exceptions' should have logged the error
		1

# TODO:KILL (using this to narrow down scope of error)
.do-init-constants void() unsafe noctx summon
	init-constants

init-constants void() unsafe noctx summon builtin

.add-main-task fut nat64(
	gctx global-ctx,
	thread-id nat64,
	argc int32,
	argv c-str*,
	main-ptr main-ptr,
) trusted noctx
	# Before we start the engine, add a task.
	# Create a ctx manually so we can call functions that need it when creating the task
	log-ctx = log-ctx
	perf-ctx = perf-ctx
	log-ctx-ptr = log-ctx.ref-of-val as-any-mut-ptr
	perf-ptr = perf-ctx.ref-of-val as-any-mut-ptr
	print-lock = gctx.print-lock.ref-of-val
	tls by-val thread-local-stuff = thread-id, print-lock, log-ctx-ptr, perf-ptr
	ctx-by-val = gctx new-ctx tls.ref-of-val, 0
	ctx = ctx-by-val ref-of-val

	add act fut nat64(c-str[], main-ptr) = (all-args, main-ptr-2) =>
		all-args add-first-task main-ptr-2
	all-args = argv[0 .. argc.to-int64.to-nat64]
	add call-with-ctx ctx, all-args, main-ptr

.thread-args record mut by-ref
	thread-id nat64
	gctx global-ctx

# TODO: if mut-bag-node is in unmanaged memory, don't need ctx for this
# We want main-fut to be available immediately, not after having run some user code.
# So we create main-fut now, and forward the result of 'main' to that.
.add-first-task fut nat64(all-args c-str[], main-ptr fun-ptr2<fut nat64, ctx, str[]>) unsafe
	# 'then' is useful here -- it handles any errors in 'main' and gets us main-fut immediately.
	# TODO: 'then' overload for fut<void> taking lambda with 0 params
	res = delay then-void () =>
		get-global-ctx init-symbol-table
		# First argument is the path to this executable; omit from crow main
		main-ptr[get-ctx, for arg : all-args tail; arg to-str]
	res handle-exceptions
	res

# Each thread will get the thread ID, and the common arguments.
.run-threads void(n-threads nat64, gctx global-ctx) noctx unsafe summon
	threads thread-handle mut* = n-threads unmanaged-alloc-elements
	thread-args by-val<thread-args> mut* = n-threads unmanaged-alloc-elements

	actual-n-threads = n-threads - 1

	0 start-threads-recur actual-n-threads, threads, thread-args, gctx
	actual-n-threads thread-function gctx
	0 join-threads-recur actual-n-threads, threads.as-const

	threads unmanaged-free
	thread-args unmanaged-free

.start-threads-recur void(
	i nat64,
	n-threads nat64,
	threads thread-handle mut*,
	thread-args-begin by-val<thread-args> mut*,
	gctx global-ctx,
) noctx unsafe summon
	unless i == n-threads
		thread-arg-ptr = thread-args-begin + i
		*thread-arg-ptr := i, gctx
		thread-ptr = threads + i
		thread-ptr.as-cell create-one-thread thread-arg-ptr.as-any-mut-ptr, &thread-fun
		i + 1 start-threads-recur n-threads, threads, thread-args-begin, gctx

.thread-fun any-mut-ptr(args-ptr any-mut-ptr) noctx unsafe summon
	args thread-args = args-ptr as-ref
	args.thread-id thread-function args.gctx
	null

.join-threads-recur void(i nat64, n-threads nat64, threads thread-handle*) noctx unsafe summon
	unless i == n-threads
		threads[i] join-one-thread
		i + 1 join-threads-recur n-threads, threads
