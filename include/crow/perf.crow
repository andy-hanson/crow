###
Functions for measuring performance.

Even non-`summon` code is can measure perf, but only `summon` code can access the measurements.
###
no-std
import
	./col/array: array, iterate, new, size
	./col/collection: for-loop, index-of, join, n-of, set-subscript, subscript
	./col/dict: ~=, build, dict, iterate, size
	./col/list: ~=, build, iterate, list, to-array
	./col/mut-array: ~=, +, begin, build, mut-array, next, set-next
	./exception: finally, with-block
	./fun-util: subscript
	./io/duration: duration, nsec, to-string
	./io/private/time-low-level: get-monotime-nsec
	./misc: void
	./number: +, -, nat64, to-float64
	./option: new, is-empty, option
	./pointer: *, mut-pointer, set-deref
	./string: ~~, literal, new, string, to-string
	./symbol: ==, <=>, hash-mix, symbol, to-string
	./tuple: new, tuple2

+perf-result record
	total duration
	measures measure-total[symbol]
+to-string string(a perf-result)
	measures = ", " join for name, total : a measures
		"{name}: {total}"
	"total: {a total}, {measures}"

# Count and total duration of all measurements with the same name.
+measure-total record
	# Number of times a perf measure happend with this name.
	+count nat64
	# Total time spent in all measures with this name.
	+total-duration duration
+to-string string(a measure-total)
	"{a count} Ã— {a total-duration}"

# Use 'nsec' instead of 'duration' to avoid spending time converting to floats
-measure-accum record
	count nat64
	total-nsec nat64
-new measure-accum()
	0, 0
-+ measure-accum(a measure-accum, b measure-accum)
	a.count + b.count, a.total-nsec + b.total-nsec

+measure-performance record
	measure-names symbol array
+measure-performance measure-performance()
	(),
+measure-performance measure-performance(measure-names symbol[])
	measure-names.to-array,

# Enable performance, run the callback, then return its result and the measured performance.
+with-block[t] (t, perf-result)(a measure-performance, f act t()) summon
	assert (trusted local-perf->is-empty) : "Already measuring performance"
	with : (() => trusted clear-perf) finally
		measure-values measure-accum mut-array = a.measure-names.size n-of ()
		trusted
			*local-perf := (a.measure-names, measure-values),
		start = perf-now
		f-res = f[]
		end = perf-now
		perf-res perf-result = (end - start).to-float64.nsec, for i, name : a measure-names
			value = measure-values[i]
			name, (value.count, value.total-nsec.to-float64.nsec)
		f-res, perf-res

+perf-measure record
	measure-name symbol
+perf-measure perf-measure(measure-name symbol)
	measure-name,

###
Runs `f` and returns its result.
If the performance measure `measure-name` is enabled, this contribues to its performance measurement.
###
+with-block[t] t(a perf-measure, f act t())
	if perf ?= trusted *local-perf
		if index ?= perf.measure-names index-of a.measure-name
			start = perf-now
			res = f[]
			nsec = perf-now - start
			perf.measure-accums[index] := perf.measure-accums[index] + (1, nsec)
			res
		else
			f[]
	else
		f[]

-local-perf perf? mut*() thread-local
-perf record mut
	measure-names symbol array
	measure-accums measure-accum mut-array
-clear-perf void() unsafe
	*local-perf := ()

-perf-now nat64() noctx
	get-monotime-nsec
