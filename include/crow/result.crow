no-std
import
	./bool: bool, false, true
	./compare: equal
	./exception: exception, rethrow
	./fun-util: subscript
export
	./private/bootstrap: err, ok, result

==[t, u] bool(a (t, u) result, b (t, u) result) t equal, u equal
	match a
	as ok oa
		match b
		as ok ob
			oa == ob
		as err _
			false
	as err ea
		match b
		as ok _
			false
		as err eb
			ea == eb

then[out, in, err] (out, err) result(a (in, err) result, f act (out, err) result(in))
	match a
	as ok v
		f[v]
	as err e
		e err

![t] t(a (t, exception) result)
	match a
	as ok v
		v
	as err e
		e rethrow

is-ok[t, u] bool(a (t, u) result)
	match a
	as ok _
		true
	as err _
		false
