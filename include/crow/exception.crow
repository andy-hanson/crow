no-std
import
	./bool: bool
	./col/arr: for-loop
	./fun-util: subscript
	./misc: new, void
	./str: ~~, is-empty, literal, str
	./str-conv: finish, interp, to-str, with-str, with-value
	./writer: ~~=, move-to-str, new, writer
	./private/backtrace: backtrace, backtrace-entry, entries, file-path, function-name, line-number
	./private/exception-low-level: throw-c-str
	./private/number-low-level: ==
	./str-util: join, to-c-str, to-str
export
	./private/exception-low-level: assert, catch, exception, forbid, message, throw

assert void(message str, condition bool)
	unless condition
		message throw

forbid void(message str, condition bool)
	if condition
		message throw

###
Create amd throw an exception with the given message.

This gets the current backtrace to use in the exception.
###
throw<a> a(message str) trusted
	message.to-c-str throw-c-str 2

###
Fail unconditionally.
This function is used to mark part of an `if` or `match` that should not be possible.
###
unreachable<a> a()
	"should be unreachable" throw

region handling

###
Runs `try`, and then runs `after`, even if `try` threw an exception.

If there was an exception, it will be rethrown after `after` finishes.
If `after` throws an exception, the original exception will be lost.
###
finally<a> a(after act void(), try act a())
	res = after do-if-error try
	after[]
	res

###
Rus `try`, and if it throws an exception, runs `on-error`.

This is like `finally`, but does not run in the successful case.
###
do-if-error<a> a(on-error act void(), try act a())
	try catch err =>
		on-error[]
		err throw

# Outputs the exception's message and backtrace to a string.
to-str str(a exception) unsafe
	res writer = ()
	message = a.message to-str
	res ~~= (message.is-empty ? "<<empty message>>" : message)
	for x : a.backtrace entries
		res ~~= "\n\tat "
		res ~~= x.to-str
	res move-to-str

to-str str(a backtrace)
	"" join for x : a entries
		"\n\tat " ~~ x.to-str

to-str str(a backtrace-entry) trusted
	name = a.function-name to-str
	path = a.file-path to-str
	if name is-empty
		"<<unknown>>"
	elif path is-empty
		name
	elif a.line-number == 0
		"{name} ({path})"
	else
		"{name} ({path}:{a line-number})"
