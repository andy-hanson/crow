###
More string functions are in `crow/str-conv` and `crow/str-util`.
Also, `crow/reader` is useful for parsing strings, and `crow-writer` is useful for generating strings.
###

no-std
import
	./bool: bool
	./compare: comparison
	./col/arr: arr
	./col/private/arr-low-level: arr-from-begin-end
	./col/seq: ~, ~~, is-empty, seq, seq-compare, seq-equal, to-seq
	./private/number-low-level: ==, <=>
	./private/c-str-util: find-c-str-end
export
	./private/bootstrap: char, c-str, to-char, to-nat8

###
String type.

Strings are optimized for fast concatenation and splitting.
They don't support efficient access by index, so use iterators instead.
###
str record
	by-val
	.inner seq char

char8s seq char(a str)
	a inner

str str(a seq char)
	a,

###
Treat an array of characters as a string.
This is O(1).
###
str str(a char[]) noctx
	a.to-seq,

literal str(a c-str) noctx trusted
	a arr-from-begin-end a.find-c-str-end str

###
Concatenate two strings.
This is O(n).

If you need to concatenate many strings, consider using the `crow/writer` module.
###
~~ str(a str, b str)
	a.inner ~~ b.inner,

~ str(a char, b str)
	a ~ b.inner,

~ str(a str, b char)
	a.inner ~ b,

# Same as `""`.
new str() noctx
	""

# Same as `a == ""`.
is-empty bool(a str)
	a.inner is-empty

== bool(a str, b str) trusted
	a.inner seq-equal b.inner
== bool(a char, b char) noctx
	a.to-nat8 == b.to-nat8

<=> comparison(a str, b str) trusted
	a.inner seq-compare b.inner
<=> comparison(a char, b char) noctx
	a.to-nat8 <=> b.to-nat8
