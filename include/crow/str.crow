###
More string functions are in `crow/str-conv` and `crow/str-util`.
Also, `crow/reader` is useful for parsing strings, and `crow-writer` is useful for generating strings.
###

no-std
import
	./bool: bool
	./compare: comparison
	./col/arr: arr
	./col/private/arr-low-level: arr-from-begin-end
	./col/seq: ~, ~~, for-loop, is-empty, seq, seq-compare, seq-equal, to-seq
	./hash: hasher, hash-mix
	./misc: void
	./private/number-low-level: ==, <=>, to-nat64
	./private/c-str-util: find-c-str-end
export
	./private/bootstrap: char8, c-str, to-char8, to-nat8

###
String type.

Strings are optimized for fast concatenation and splitting.
They don't support efficient access by index, so use iterators instead.
###
str record by-val
	.inner seq char8

char8s seq char8(a str)
	a inner

###
Treat a seq of UTF-8 character codes as a string.
This is O(1).
###
str str(a seq char8)
	a,

###
Treat an array of UTF-8 character codes as a string.
This is O(1).
###
str str(a char8[]) noctx
	a.to-seq,

literal str(a c-str) noctx trusted
	a arr-from-begin-end a.find-c-str-end str

###
Concatenate two strings.
This is O(n).

If you need to concatenate many strings, consider using the `crow/writer` module.
###
~~ str(a str, b str)
	a.inner ~~ b.inner,

~ str(a char8, b str)
	a ~ b.inner,

~ str(a str, b char8)
	a.inner ~ b,

# Same as `""`.
new str() noctx
	""

# Same as `a == ""`.
is-empty bool(a str)
	a.inner is-empty

== bool(a str, b str) trusted
	a.inner seq-equal b.inner
== bool(a char8, b char8) noctx
	a.to-nat8 == b.to-nat8

<=> comparison(a str, b str) trusted
	a.inner seq-compare b.inner
<=> comparison(a char8, b char8) noctx
	a.to-nat8 <=> b.to-nat8

hash-mix void(hasher hasher, a str)
	for c : a char8s
		hasher hash-mix c.to-nat8.to-nat64
