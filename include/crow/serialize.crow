###
Functions for converting values to/from byte arrays.

For JSON serialization, see the `crow/json` module.
###

no-std
import
	./bits: &, |, <<, >>
	./col/list: ==, for-loop, list, new, size, drop-while, subscript
	./compare: <, <=
	./json: json
	./number: ==, <=>, int64, max-int64, nat8, nat32, nat64, to-nat8, to-int64, to-nat32, to-nat64
	./range: ..
	./string: as-string, string
	./test-util: is
	./private/bootstrap: todo

###
A type is serializable if it can convert to and from a byte array.

Implementations should ensure that `a.to-bytes.from-bytes == a`.
###
serialize<a> spec
	to-bytes nat8[](a a)
	from-bytes a(a nat8[])

# Identity function to ensure that byte lists are trivially serializable.
to-bytes nat8[](a nat8[])
	a
from-bytes nat8[](a nat8[])
	a

from-bytes string(a nat8[]) trusted
	a as-string

###
Convert a number to a nat8[] simply by taking its component bytes in big-endian order.
This strips leading 0 bytes.
###
to-bytes nat8[](a nat64)
	a.to-8-bytes drop-while byte => byte == 0
###
Unlike `a to-bytes`, this always returns an 8-byte array, including any leading 0 bytes.
###
to-8-bytes nat8[](a nat64)
	b0 = a >> 0x00 wrap-to-nat8
	b1 = a >> 0x08 wrap-to-nat8
	b2 = a >> 0x10 wrap-to-nat8
	b3 = a >> 0x18 wrap-to-nat8
	b4 = a >> 0x20 wrap-to-nat8
	b5 = a >> 0x28 wrap-to-nat8
	b6 = a >> 0x30 wrap-to-nat8
	b7 = a >> 0x38 wrap-to-nat8
	b7, b6, b5, b4, b3, b2, b1, b0
# This works for the result of either `to-bytes` or `to-8-bytes`.
from-bytes nat64(a nat8[])
	assert a.size <= 8
	res mut = 0
	for x : a
		res := res << 8 | x.to-nat64
	res
test
	n = 0x0123456789abcdef
	bytes = n to-bytes
	bytes is (0x01, 0x23, 0x45, 0x67, 0x89, 0xab, 0xcd, 0xef)
	bytes.from-bytes :: nat64 is n
	0x500.to-8-bytes is (0, 0, 0, 0, 0, 0, 5, 0)
	0x500.to-bytes is (5, 0)

# TODO:MOVE
.wrap-to-nat8 nat8(a nat64) noctx trusted
	# TODO: gccjit has a bug if I inline 'masked'
	masked = a & 0xff
	masked to-nat8

to-bytes nat8[](a nat32)
	a.to-nat64 to-bytes
to-4-bytes nat8[](a nat32)
	a.to-nat64.to-8-bytes[4 .. 8]
from-bytes nat32(a nat8[])
	a.from-bytes :: nat64 to-nat32

to-bytes nat8[](a int64) no-doc
	a to-8-bytes
to-8-bytes nat8[](a int64) no-doc
	if a < 0
		todo
	else
		a.to-nat64 to-8-bytes
from-bytes int64(a nat8[])
	n nat64 = a from-bytes
	if n < max-int64.to-nat64
		n to-int64
	else
		todo
