no-std
import
	system/time: clock_gettime, CLOCK_MONOTONIC, new, timespec, tv_nsec, tv_sec
	system/win32: GetTickCount
	../../number: ==, +, *, /, %, int64, to
	../../private/bootstrap: nat64, todo

# Monotime in nanoseconds.
timestamp alias
	nat64

###
Get the value from a monotonic clock.
TODO: This should be 'summon', but I didn't mark it that way so 'perf' can use it.
###
get-monotime-nsec timestamp() bare, trusted, native extern
	if extern windows
		GetTickCount.to msec-to-nsec
	elif extern posix
		time mut timespec = ()
		err = CLOCK_MONOTONIC clock_gettime &time
		if err == 0
			time.tv_sec.to * 1000000000 + time.tv_nsec.to
		else
			todo
	else
		todo

-msec-to-nsec nat64(a nat64) bare
	a * 1000000

nsec-to-msec nat64(a nat64) bare
	a / 1000000

to-timespec timespec(a timestamp) bare, unsafe
	second nat64 = 1000000000
	seconds int64 = a / second to
	ns int64 = a % second to
	seconds, ns
