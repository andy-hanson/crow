no-std
import
	system/stdio: stderr, stdout
	system/unistd: write
	system/win32:
		DWORD, GetLastError, GetStdHandle, HANDLE, STD_ERROR_HANDLE, STD_OUTPUT_HANDLE,
		WriteFile
	../col/array: array, begin-pointer, size
	../c-types: fd-t
	../exception: exception, not-implemented
	../js: as-t, call-property, js-global
	../misc: new, void
	../number: ==, nat8, nat32, to
	../pointer: null, size-of
	../string: char8, interpolate, string, to, to-bytes
	../private/bootstrap: todo
	./win32-util: show-win32-error

+print-error record(describe string) exception variant-member

+pipe enum(out, err)

# Print to stdout or stderr, including a newline.
+print void(pipe pipe, message string) summon, trusted
	if extern native
		pipe print-no-newline "{message}\n"
	elif extern js
		fn string = match pipe
		as out
			"log"
		as err
			"error"
		"console".js-global call-property fn, message as-t
	else
		throw not-implemented

# Print to stdout or stderr, without appending a newline.
+print-no-newline void(pipe pipe, message string) summon, native extern
	if trusted extern windows
		handle = match pipe
		as out
			STD_OUTPUT_HANDLE
		as err
			STD_ERROR_HANDLE
		trusted handle.GetStdHandle write-windows message
	elif trusted extern posix
		output = match pipe
		as out
			stdout
		as err
			stderr
		trusted output write-posix message
	else
		throw not-implemented

-write-windows void(console HANDLE, a string) summon, unsafe, (native, windows) extern
	bytes nat8 array = a to-bytes
	written mut DWORD = 0
	size32 nat32 = bytes.size to
	ok = console WriteFile bytes.begin-pointer, size32, &written, null
	assert ok : "{GetLastError show-win32-error}".print-error
	unless written == size32
		# TODO: maybe retry
		todo

-write-posix void(fd fd-t, a string) summon, unsafe, (native, posix) extern
	assert size-of@char8 == size-of@nat8
	bytes nat8 array = a to-bytes
	res = fd write bytes.begin-pointer, bytes.size
	unless res == bytes.size.to
		# TODO: Maybe retry
		todo

# Clear the terminal.
+clear-screen void() summon, native extern
	out print-no-newline "\x1bc"
