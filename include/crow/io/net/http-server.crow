no-std
import
	errno: EAGAIN, errno
	sys/socket: as-fd, SOCKET
	unistd: write
	../../bool: bool, false, true
	../../col/array: array, begin-pointer, is-empty, size, subscript
	../../col/dict: for-loop, in, to-dict
	../../col/list: ~~, is-empty, rtail, size, subscript, to-array
	../../col/mut-dict: mut-dict, new, try-remove, set-subscript, subscript
	../../col/mut-list: ~=, ~~=, index-of, move-to-list, mut-list, new, pop-n-from-start, size
	../../compare: <=, >
	../../exception: to-string
	../../future: delay, future, future-catch, future-finally, new, subscript, then, with-block
	../../hash: hash-mix
	../../log: error, log, warn
	../../misc: is-sendable, new, void
	../../number: ==, <=>, +, nat8, nat64, to-int64, to-nat64
	../../option: ??, new, option
	../../parse: nat, parse
	../../range: ..
	../../string:
		==, <=>, ~~, as-string, contains-substr, hash-mix, is-empty, join, literal, split-by, string,
		to-bytes, to-c-string, to-nat8, to-string, try-remove-end
	../../tuple: new, pair
	../../private/bootstrap: todo
	./http-types:
		headers, http-get, http-internal-server-error, http-method, http-post, http-request,
		http-response, new, response-body, status, status-desc
	./tcp-server: close-client, new, start-tcp-server, stop

# Reference to a running server.
http-server record

# Type of a function that can respond to HTTP requests.
http-handler alias
	ref http-response(http-request)

http-server-options record sendable
	port string
	handler http-handler
http-server http-server-options(port string, handler http-handler)
	port, handler

###
Starts running an HTTP server. Stops it when `cb` returns.
###
with-block<a> a^(a http-server-options, cb ref a(http-server)) is-sendable<a> trusted summon
	client-states http-parse-state mut[SOCKET] = ()

	on-accepted-client ref void(SOCKET) = sock =>
		client-states[sock] := ()
		()
	on-received-chunk ref void(SOCKET, array nat8) = (sock, bytes) =>
		if state ?= client-states[sock]
			closed <- sock handle-chunk state, bytes, a.handler
			if closed
				_ = client-states try-remove sock
				()
			()
		else
			warn log "Ignoring unrecognized client"
			()
	tcp-server = a.port start-tcp-server (on-accepted-client, on-received-chunk)

	# TODO: KILL
	#_ <- a.handler[(http-get, "", (), ())]

	with : tcp-server.stop future-finally
		cb[()]

# Returns true when closed client
.handle-chunk bool^(sock SOCKET, state http-parse-state, chunk array nat8, handler http-handler) summon trusted
	state.unparsed ~~= chunk
	finished-request = if state.content-length == 0
		state handle-http-initial
	else
		assert state.unparsed.size <= state.content-length
		state.unparsed.size == state.content-length

	if finished-request
		request = state finish-request
		handler-response <- handler[request] future-catch
		response = match handler-response
		as ok o
			o
		as err e
			error log e.to-string
			http-internal-server-error
		<- sock write-until-finished response.response-to-array
		sock close-client
		true,
	else
		false,

.write-until-finished void^(sock SOCKET, content array nat8) summon trusted
	unless content is-empty
		wrote = sock.as-fd write content.begin-pointer, content.size
		unless wrote == content.size.to-int64
			if wrote == -1
				err = errno
				assert err == EAGAIN : "can't write to socket, errno {err}"
				<- delay
				sock write-until-finished content
			else
				assert wrote > 0
				<- delay
				sock write-until-finished content[wrote.to-nat64 .. content.size]

.finish-request http-request(state http-parse-state) trusted
	state.method, state.url, state.headers.move-to-list.to-dict, state.unparsed.move-to-list

.response-to-array array nat8(a http-response)
	forbid a.status-desc contains-substr "\n"
	# TODO: headers are case insensitive, maybe forbid any upper-case letters in headers to be consistent
	forbid "Content-Length" in a.headers
	headers = "" join for k, v : a headers
		forbid k contains-substr "\n"
		forbid v contains-substr "\n"
		"\r\n{k}: {v}"
	head = "HTTP/1.1 {a status} \r\n{a status-desc}{headers}"
	if a.response-body is-empty
		"{head}\r\n\r\n".to-bytes to-array
	else
		"{head}\r\nContent-Length:{a.response-body size}\r\n\r\n".to-bytes ~~ a.response-body to-array

# returns true if ready to call handler
.handle-http-initial bool(state http-parse-state)
	loop
		if line ?= state.unparsed try-take-first-line
			if line is-empty
				break state.content-length == 0
			elif state.url is-empty
				# Expect something like "GET / HTTP/1.1"
				parts = line split-by " "
				assert parts.size == 3
				state.method := parts[0] parse-method
				state.url := parts[1]
				assert parts[2] == "HTTP/1.1"
				forbid state.url is-empty
				continue
			else
				parts = line split-by ": "
				assert parts.size == 2
				key = parts[0]
				value = parts[1]
				state.headers ~= (key, value)
				if key == "content-length"
					match nat parse value
					as ok content-length
						state.content-length := content-length
					as err _
						# invalid content-length
						todo
				continue
		else
			break false

.parse-method http-method(name string)
	if name == "GET"
		http-get
	elif name == "POST"
		http-post
	else
		todo

.try-take-first-line string?(a nat8 mut[]) trusted
	if index ?= a index-of "\n".to-nat8
		bytes = a pop-n-from-start index + 1 rtail
		line = bytes as-string
		(line try-remove-end "\r" ?? line),

.http-parse-state record mut
	method mut http-method
	url mut string
	headers (string, string) mut[]
	# This is not set until after the end of headers
	content-length mut nat64
	# TODO:PERF: Use a circular buffer as we often take content off the front
	unparsed nat8 mut[]

.new http-parse-state()
	http-get, "", (), 0, ()
