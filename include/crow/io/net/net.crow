no-std
import
	bits/socket_type: SOCK_STREAM
	netdb:
		addrinfo, ai_addr, ai_addrlen, ai_family, ai_next, ai-passive, ai_protocol, ai_socktype,
		freeaddrinfo, getaddrinfo, new
	sys/socket: AF_INET, AF_UNSPEC, bind, connect, socket
	unistd: close
	../../cell: *, cell, new
	../../compare: !=
	../../exception: assert
	../../extra-types: fd-t
	../../number: ==, nat16, to-int32
	../../opt: !, new, opt
	../../ptr: ==, *, any-const-ptr, as-const, as-ref, const-ptr, mut-ptr, null, ptr-to
	../../str: str
	../../str-conv: finish, interp, to-str, with-str, with-value
	../../str-util: to-c-str
	../../private/bootstrap: todo
	../posix-util: check-posix-error
	../print: print

# Uniform Resource Identifier
uri record
	host str
	port nat16
	path-and-query str
	# Basically just ignored
	after-hash str

# Opens a socket, connects to the URI, and returns the fd.
connect-to-uri fd-t(u uri) trusted summon
	# TODO: null.as-ref is evil
	hints addrinfo = 0, AF_UNSPEC.to-int32, SOCK_STREAM, 0, 0, null::any-const-ptr.as-ref, null, null
	result cell addrinfo mut* = null,
	check-posix-error: u.host.to-c-str getaddrinfo u.port.to-str.to-c-str, hints.ptr-to, result
	op-sock = result->as-const find-and-connect-working-socket
	*result freeaddrinfo
	# TODO: handle failure
	op-sock!

.find-and-connect-working-socket fd-t?(ap addrinfo*) unsafe summon
	if ap != null
		a = *ap
		sock = a.ai_family socket a.ai_socktype, a.ai_protocol
		print: "sock is {sock}"
		if sock == -1
			# Ignore error and try again
			a.ai_next find-and-connect-working-socket
		else
			err = sock connect a.ai_addr, a.ai_addrlen
			print: "err is {err}"
			if err == -1
				print: "connect() failed: "
				check-posix-error: sock close
				# Ignore error and try again
				a.ai_next find-and-connect-working-socket
			else
				assert: err == 0
				sock,

# Returns an fd bound to the given port.
bind-to-port fd-t(port str) unsafe summon
	# TODO: null.as-ref is evil
	hints addrinfo = ai-passive, AF_INET, SOCK_STREAM, 0, 0, null::any-const-ptr.as-ref, null, null
	result cell addrinfo mut* = null,
	null getaddrinfo port.to-c-str, hints.ptr-to, result check-posix-error
	op-sock = result->as-const find-and-bind-working-socket
	*result freeaddrinfo
	if res ?= op-sock
		res
	else
		print: "Failed to bind to port {port}"
		todo

.find-and-bind-working-socket fd-t?(ap addrinfo*) unsafe summon
	if ap != null
		a = *ap
		listen-fd = a.ai_family socket a.ai_socktype, 0
		if listen-fd != -1
			if (listen-fd bind a.ai_addr, a.ai_addrlen) == 0
				print: "Connected!"
				listen-fd,
			else
				check-posix-error: listen-fd close
				a.ai_next find-and-bind-working-socket
		else
			a.ai_next find-and-bind-working-socket
