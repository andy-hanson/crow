###
Module for low-level networking.
###

no-std
import
	bits/socket_type: SOCK_STREAM
	crow/misc: new, void
	crow/version: is-windows
	netdb:
		addrinfo, ai_addr, ai_addrlen, ai_family, ai_next, ai-passive, ai_protocol, ai_socktype,
		freeaddrinfo, getaddrinfo, new
	sys/socket:
		AF_INET, AF_UNSPEC, as-fd, bind, closesocket, connect, INVALID_SOCKET, socket, SOCKET
	unistd: close
	win32: new, MAKEWORD, WSADATA, WSAStartup
	../../compare: !=
	../../number: ==, int32, nat16, to
	../../option: force, new, option
	../../pointer: ==, *, as-const, const-pointer, mut-pointer, null
	../../string: ~~, literal, string, to
	../../private/bootstrap: todo
	../posix-util: check-posix-error
	../print: err, out, print

# Parsed Uniform Resource Identifier
uri record
	host string
	port nat16
	path-and-query string
	# Basically just ignored
	after-hash string

# Opens a socket, connects to the URI, and returns the fd.
connect-to-uri SOCKET(u uri) summon, unsafe
	if is-windows
		wsa-init

	hints addrinfo = 0, AF_UNSPEC.to, SOCK_STREAM, 0, 0, null, null, null
	result mut addrinfo mut* = null
	u.host.to getaddrinfo "{u port}".to, &hints, &result check-getaddrinfo-error
	op-sock = result.as-const find-and-connect-working-socket
	result freeaddrinfo
	# TODO: handle failure
	op-sock!

-wsa-init void() summon, unsafe
	wsa-data mut WSADATA = () 
	err = 2 MAKEWORD 2 WSAStartup &wsa-data
	out print "initialized WSA, err is {err}"
	if err != 0
		todo

-find-and-connect-working-socket SOCKET?(ap addrinfo*) summon, unsafe
	unless ap == null
		a = *ap
		sock = a.ai_family socket a.ai_socktype, a.ai_protocol
		if sock == INVALID_SOCKET
			# Ignore error and try again
			a.ai_next find-and-connect-working-socket
		else
			error = sock connect a.ai_addr, a.ai_addrlen
			if error == -1
				err print "connect() failed: "
				sock close-socket
				# Ignore error and try again
				a.ai_next find-and-connect-working-socket
			else
				assert error == 0
				sock,

# Returns a file descriptor bound to the given port.
bind-to-port SOCKET(port nat16) summon, unsafe
	if is-windows
		wsa-init

	hints addrinfo = ai-passive, AF_INET, SOCK_STREAM, 0, 0, null, null, null
	result mut addrinfo mut* = null
	null getaddrinfo "{port}", &hints, &result check-getaddrinfo-error
	op-sock = result.as-const find-and-bind-working-socket
	result freeaddrinfo
	if res ?= op-sock
		res
	else
		err print "Failed to bind to port {port}"
		todo

close-socket void(sock SOCKET) summon, unsafe
	if is-windows
		err = sock closesocket
		if err != 0
			todo
	else
		sock.as-fd.close check-posix-error

-find-and-bind-working-socket SOCKET?(ap addrinfo*) summon, unsafe
	unless ap == null
		a = *ap
		listen-fd = a.ai_family socket a.ai_socktype, 0
		if listen-fd == INVALID_SOCKET
			a.ai_next find-and-bind-working-socket
		else
			if (listen-fd bind a.ai_addr, a.ai_addrlen) == 0
				listen-fd,
			else
				listen-fd close-socket
				a.ai_next find-and-bind-working-socket

-check-getaddrinfo-error void(err int32) summon, unsafe
	if err != 0
		# TODO: convert to appropriate enum
		throw "getaddrinfo error: {err}"
