no-std
import
	unistd: __environ
	../col/arr: begin-ptr, arr-from-begin-end
	../col/dict: dict, each
	../col/mut-dict: move-to-dict!, mut-dict, new, set-subscript
	../col/mut-arr: ~=, move-to-arr!, mut-arr, new
	../compare: !=
	../hash: hash-mix
	../misc: ->, arrow, from, to, typed, void
	../private/bootstrap: todo
	../ptr: ==, +, *, const-ptr, null
	../str: ==, <=>, ~, c-str, str
	../str-util: find-char-in-cstr, find-cstr-end, to-c-str

# Represents all environment variables.
environ alias
	str[str]

# Get the environment variables for this process.
get-environ environ() summon trusted
	res str mut[str] = ()
	__environ get-environ-recur res
	res move-to-dict!

.get-environ-recur void(env c-str*, res str mut[str]) unsafe
	if *env != null
		entry = (*env).parse-environ-entry
		res[entry from] := entry to
		env + 1 get-environ-recur res

.parse-environ-entry arrow<str, str>(entry c-str) unsafe
	if key-end ?= entry find-char-in-cstr "="
		key = entry arr-from-begin-end key-end str
		value-begin = key-end + 1
		value-end = value-begin find-cstr-end
		value = value-begin arr-from-begin-end value-end str
		key -> value
	else
		todo

# Get an environment string from environment variables.
convert-environ c-str*(environ environ) unsafe
	res c-str mut[] = ()
	environ each (key, value) =>
		res ~= key ~ "=" ~ value to-c-str
	# TODO: 'typed' should be unnecessary
	res ~= null typed@<c-str>
	res.move-to-arr! begin-ptr
