###
Functions for operating on paths.
There is technically no I/O here; for functions like reading from a path, see `crow/io/file`.
###

no-std
import
	../bool: bool
	../opt: new, opt, opt-equal
	../str: ==, literal, new, str
	../tuple: a, b
	../str: ~~, to-str, try-split-once-right
	../test-util: is

###
Parent of a path. This means removing the last path component.
`""` if the path has no parent.
###
parent-path str(a str)
	if x ?= a try-split-once-right "/"
		x a

# Adds a new path component onto the end.
child-path str(a str, child_name str)
	# TODO: ensure child-name doesn't contain "../"
	"{a}/{child_name}"

###
Gets the extension from a file name or path.
Returns an empty option if there is no extension.
###
get-extension str?(name str)
	if x ?= name try-split-once-right "."
		x.b,
test
	"birds/crow.txt".get-extension is ("txt",)

.== bool(a str?, b str?)
	a opt-equal b

###
Removes the extension from a file name or path.
This returns the path without the extension.

For example: `"birds/crow.txt" remove-extension` is `"birds/crow"`.
###
remove-extension str(name str)
	if x ?= name try-split-once-right "."
		x a
	else
		name
test
	"birds/crow.txt".remove-extension is "birds/crow"

###
Adds an extension to a file name or path.
If an extension already exists, adds a second one, as in `foo.png.txt`.
###
add-extension str(name str, ext str)
	"{name}.{ext}"
test
	"birds/crow.txt" add-extension "exe" is "birds/crow.txt.exe"

# Removes the extension (if it exists) and adds a new one.
change-extension str(name str, ext str)
	name.remove-extension add-extension ext
test
	"birds/crow.txt" change-extension "exe" is "birds/crow.exe"

# Gets just the file name without any directory names in from of it.
base-name str(path str)
	if x ?= path try-split-once-right "/"
		x b
	else
		path
test
	"birds/crow.txt".base-name is "crow.txt"
