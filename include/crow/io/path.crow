no-std
import
	../col/arr: subscript
	../col/arr-util: last, r-index-of, rtail
	../number: ==, <=>, +, -, nat64
	../opt: new, opt
	../range: ..
	../str: ==, char, chars, new, size-bytes, str
	../str-conv: finish, interp, to-str, with-str, with-value

###
Gets the parent of a path.
Returns "" if the path has no parent.
###
parent-path str(a str)
	if index ?= a.chars r-index-of "/"
		a.chars[0 .. index] str

# Gets a child of a path.
child-path str(a str, child_name str)
	# TODO: ensure child-name doesn't contain "../"
	"{a}/{child_name}"

# TODO:MOVE
.last-index-of nat64?(a str, c char)
	if last-char ?= a.chars last
		if last-char == c
			a.size-bytes - 1,
		else
			a.chars.rtail.str last-index-of c

# Gets the extension from a file name or path.
# Returns none if there is no extension.
get-extension str?(name str)
	if index ?= name last-index-of "."
		name.chars[index + 1 .. name.size-bytes].str,

# Removes the extension from a file name or path.
remove-extension str(name str)
	if index ?= name last-index-of "."
		name.chars[0 .. index] str
	else
		name

###
Adds an extension to a file name or path.
If an extension already exists, adds a second one, as in `foo.png.txt`.
###
add-extension str(name str, ext str)
	"{name}.{ext}"

# Removes the extension (if it exists) and adds a new one.
change-extension str(name str, ext str)
	name.remove-extension add-extension ext

# Gets just the file name without any directory names in from of it.
base-name str(path str)
	if index ?= path last-index-of "/"
		path.chars[index + 1 .. path.size-bytes] str
	else
		path
