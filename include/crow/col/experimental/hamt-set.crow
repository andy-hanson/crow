no-std
import
	../../bool: !, bool
	../../compare: compare
	../../hash: hash-mix
	../../misc: is-data, void
	../../number: nat64
	../../opt: is-empty, none, opt, some
	../../tuple: new
	../arr: arr
	../arr-util: map
	../dict:
		dict, each-key, fold-keys, is-empty, new, new-dict, size, subscript, to-dict, try-add,
		try-remove

hamt-set<a> record
	.inner void[a]

new<a> hamt-set a() is-data<a> compare<a> hash-mix<a>
	(),

hamt-set<a> hamt-set a(a a[]) is-data<a> compare<a> hash-mix<a>
	(a map (x => x, void) to-dict),

is-empty<a> bool(a hamt-set a) is-data<a> compare<a> hash-mix<a>
	a.inner is-empty

size<a> nat64(a hamt-set a) is-data<a> compare<a> hash-mix<a>
	a.inner size

in<a> bool(value a, a hamt-set a) is-data<a> compare<a> hash-mix<a>
	!a.inner[value].is-empty

~ <a> hamt-set a(a hamt-set a, value a) is-data<a> compare<a> hash-mix<a>
	if res ?= a try-add value
		res
	else
		a

try-add<a> hamt-set<a>?(a hamt-set a, value a) is-data<a> compare<a> hash-mix<a>
	match a.inner try-add value, () => void
	as already-has-key _
		none
	as added res
		(res,) some

- <a> hamt-set a(a hamt-set a, value a) is-data<a> compare<a> hash-mix<a>
	if res ?= a try-remove value
		res
	else
		a

try-remove<a> hamt-set<a>?(a hamt-set a, value a) is-data<a> compare<a> hash-mix<a>
	if res ?= a.inner try-remove value
		(res.new-dict,) some

region iteration

each<a> void(a hamt-set a, f act void(a)) is-data<a> compare<a> hash-mix<a>
	a.inner each-key f

fold<a, b> a(acc a, a hamt-set b, f act a(a, b)) is-data<b> compare<b> hash-mix<b>
	acc fold-keys a.inner, f
