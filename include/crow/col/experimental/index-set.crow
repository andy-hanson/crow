no-std
import
	../../bits:
		&, <<, >>, add-bit, has-bit, count-ones, fold-one-bits, high-n-bits, low-n-bits, sub-bit
	../../bool: &&, ==, bool, false, true
	../../compare: <, <=, >, >=, min
	../../exception: unreachable
	../../fun-util: subscript
	../../log: info, log
	../../misc: new, void
	../../number: ==, <=>, +, -, *, /, %, max-nat64, nat64, new
	../../opt: ??, is-empty, new, opt
	../../ptr: ==, +, *, const-ptr, set-deref
	../../private/number-low-level: wrap-sub
	../../range: .., for-loop
	../../str: ==, literal, str
	../../str-conv: finish, interp, to-hex, to-str, with-str, with-value
	../../str-util: join
	../../test-util: is
	../array: array, fold, fold-with-index, new, subscript
	../mut-array: begin-ptr, cast-immutable, end-ptr, mut-array, uninitialized-mut-array
	../list: ==, fold, list, new, size, to-list
	../private/array-low-level: begin-ptr, end-ptr, subscript

index-set record by-ref
	# This is one more than the highest number that would currently fit in the tree.
	capacity nat64
	root node

.node union
	inner inner-node
	leaf leaf-node
	empty
# TODO: could save space by having separate types for inner node of inner nodes and inner node of leaf-node?
.inner-node record by-ref
	a node
	b node
	c node
	d node
.leaf-node record by-ref
	a nat64
	b nat64
	c nat64
	d nat64

.values array nat64(a leaf-node) trusted
	((&a.a)::nat64*)[0 .. 4]
.bits-in-leaf-node nat64()
	# 64 * 4
	256

new index-set(...a array nat64)
	a.to-list to-index-set

to-index-set index-set(a nat64[])
	(0, empty) fold a, (acc, x) => acc ~ x

is-empty bool(a index-set)
	a.capacity == 0
.is-empty bool(a node)
	match a
	as inner _
		false
	as leaf _
		false
	as empty
		true

# O(n).
size nat64(a index-set)
	0 size-recur a.root
.size-recur nat64(acc nat64, a node)
	match a
	as inner i
		acc size-recur i.a size-recur i.b size-recur i.c size-recur i.d
	as leaf l
		acc + l.a.count-ones + l.b.count-ones + l.c.count-ones + l.d.count-ones
	as empty
		acc

to-list nat64[](a index-set)
	for x : a
		x

in bool(index nat64, a index-set) trusted
	index < a.capacity && index in a.root, a.capacity
# n = capacity of this node
.in bool(index nat64, a node, n nat64) unsafe
	assert index < n
	match a
	as inner i
		assert n > bits-in-leaf-node
		half = n / 2
		quarter = half / 2
		if index < half
			if index < quarter
				index in i.a, quarter
			else
				index - quarter in i.b, quarter
		else
			index2 = index - half
			if index2 < quarter
				index2 in i.c, quarter
			else
				index2 - quarter in i.d, quarter
	as leaf l
		assert n == bits-in-leaf-node
		l.values[index word-index] has-bit index.bit-index
	as empty
		false

~ index-set(a index-set, index nat64)
	a try-add index ?? a

.quadruple-capacity index-set(a index-set)
	if a.capacity == 0
		bits-in-leaf-node, empty-leaf-node.leaf
	else
		a.capacity * 4, (a.root, empty, empty, empty).inner

.empty-inner-node inner-node()
	empty, empty, empty, empty
.empty-leaf-node leaf-node()
	0, 0, 0, 0

try-add index-set?(a index-set, index nat64) trusted
	if index >= a.capacity
		a.quadruple-capacity try-add index
	elif res ?= a.root try-add index, a.capacity
		(a.capacity, res),
.try-add node?(a node, index nat64, n nat64) unsafe
	assert index < n
	match a
	as inner i
		assert n > bits-in-leaf-node
		half = n / 2
		quarter = half / 2
		if index < half
			if index < quarter
				if res ?= i.a try-add index, quarter
					(i with-a res).inner,
			else
				if res ?= i.b try-add index - quarter, quarter
					(i with-b res).inner,
		else
			index2 = index - half
			if index2 < quarter
				if res ?= i.c try-add index2, quarter
					(i with-c res).inner,
			else
				if res ?= i.d try-add index2 - quarter, quarter
					(i with-d res).inner,
	as leaf l
		assert n == bits-in-leaf-node
		word-index = index word-index
		bit-index = index bit-index
		word = l.values[word-index]
		new-word = word add-bit bit-index
		unless new-word == word
			(l update-leaf word-index, new-word).leaf,
	as empty
		new-node = if n == bits-in-leaf-node
			empty-leaf-node leaf
		else
			empty-inner-node inner
		new-node try-add index, n

~~ index-set(a index-set, indices array nat64)
	a fold indices, (cur, index) => cur ~ index

- index-set(a index-set, index nat64)
	a try-remove index ?? a

try-remove index-set?(a index-set, index nat64) trusted
	if index < a.capacity
		if removed ?= a.root try-remove index, a.capacity
			(removed simplify-after-remove-or-filter a.capacity),
.try-remove node?(a node, index nat64, n nat64) unsafe
	assert index < n
	match a
	as inner i
		assert n > bits-in-leaf-node
		half = n / 2
		quarter = half / 2
		opt-new-node inner-node? = if index < half
			if index < quarter
				if res ?= i.a try-remove index, quarter
					(i with-a res),
			else
				if res ?= i.b try-remove index - quarter, quarter
					(i with-b res),
		else
			index2 = index - half
			if index2 < quarter
				if res ?= i.c try-remove index2, quarter
					(i with-c res),
			else
				if res ?= i.d try-remove index2 - quarter, quarter
					(i with-d res),
		if new-node ?= opt-new-node
			if new-node is-all-empty
				empty,
			else
				new-node.inner,
	as leaf l
		assert n == bits-in-leaf-node
		word-index = index word-index
		bit-index = index bit-index
		word = l.values[word-index]
		new-word = word sub-bit bit-index
		unless new-word == word
			if new-word == 0
				empty,
			else
				(l update-leaf word-index, new-word).leaf,
	as empty
		()
.simplify-after-remove-or-filter index-set(node node, capacity nat64) unsafe
	match node
	as inner i
		if i.b.is-empty && i.c.is-empty && i.d.is-empty
			forbid i.a is-empty
			i.a simplify-after-remove-or-filter capacity / 4
		else
			capacity, i.inner
	as leaf l
		assert capacity == bits-in-leaf-node
		forbid l.a == 0 && l.b == 0 && l.c == 0 && l.d == 0
		capacity, l.leaf
	as empty
		0, empty

.is-all-empty bool(a inner-node)
	a.a.is-empty && a.b.is-empty && a.c.is-empty && a.d.is-empty

# TODO:MOVE
.with-a inner-node(a inner-node, new-a node)
	new-a, a.b, a.c, a.d
.with-b inner-node(a inner-node, new-b node)
	a.a, new-b, a.c, a.d
.with-c inner-node(a inner-node, new-c node)
	a.a, a.b, new-c, a.d
.with-d inner-node(a inner-node, new-d node)
	a.a, a.b, a.c, new-d

.update-leaf leaf-node(a leaf-node, word-index nat64, new-word nat64) unsafe
	if word-index == 0
		new-word, a.b, a.c, a.d
	elif word-index == 1
		a.a, new-word, a.c, a.d
	elif word-index == 2
		a.a, a.b, new-word, a.d
	elif word-index == 3
		a.a, a.b, a.c, new-word
	else
		unreachable

###
# TODO: ~ index-set(a index-set, b index-set)

intersect index-set(a index-set, b index-set)
	strip-trailing-zeroes: a.values zip-smaller b.values, (x, y) =>
		x & y

- index-set(a index-set, b index-set)
	strip-trailing-zeroes: a.values zip-larger b.values, (x, y) =>
		(x ?? 0) & ~(y ?? 0)

.strip-trailing-zeroes index-set(a array nat64)
	index-set: void, a.strip-trailing-zeroes
.strip-trailing-zeroes array nat64(a array nat64)
	if last ?= a last
		if last == 0
			a.rtail strip-trailing-zeroes
		else
			a
###

# hi exclusive
# TODO: use 'range' type
count-range nat64(a index-set, low nat64, high nat64) trusted
	if low <= high
		a.root count-range a.capacity, low, high - low
.count-range nat64(a node, n nat64, low nat64, len nat64) unsafe
	if low <= n
		match a
		as inner i
			assert n > bits-in-leaf-node
			half = n / 2
			quarter = half / 2
			three-quarters = quarter + half
			# If low < quarter, wrap-sub will give an impossibly high value and the recursive call will return 0.
			ca = i.a count-range quarter, low, len
			cb = i.b count-range quarter, (low wrap-sub quarter), len
			cc = i.c count-range quarter, (low wrap-sub half), len
			cd = i.d count-range quarter, (low wrap-sub three-quarters), len
			ca + cb + cc + cd
		as leaf l
			assert n == bits-in-leaf-node
			high = low + len min (bits-in-leaf-node - 1)
			low-word-index = low word-index
			high-word-index = high word-index
			low-bit-index = low bit-index
			high-bit-index = high bit-index
			values = l values
			if low-word-index == high-word-index
				values[low-word-index] count-range-in-nat64 low-bit-index, high-bit-index
			else
				first-word = values[low-word-index] count-gte-in-nat64 low-bit-index
				mid-words = values[(low-word-index + 1) .. high-word-index] count-ones-in-array
				last-word = values[high-word-index] count-less-in-nat64 high-bit-index
				first-word + mid-words + last-word
		as empty
			0

count-less nat64(a index-set, n nat64)
	a count-range 0, n

count-greater-or-equal nat64(a index-set, n nat64)
	a count-range n, max-nat64

.count-range-in-nat64 nat64(a nat64, lo-bit-index nat64, hi-bit-index nat64)
	a >> lo-bit-index count-less-in-nat64 (hi-bit-index - lo-bit-index)

.count-less-in-nat64 nat64(a nat64, bit-index nat64)
	a << (64 - bit-index) count-ones

.count-gte-in-nat64 nat64(a nat64, bit-index nat64)
	a >> bit-index count-ones

.count-ones-in-array nat64(a array nat64) trusted
	0 count-ones-in-array-recur a.begin-ptr, a.end-ptr
.count-ones-in-array-recur nat64(acc nat64, cur nat64*, end nat64*) unsafe
	if cur == end
		acc
	else
		acc + cur->count-ones count-ones-in-array-recur cur + 1, end

# Gets the set with only values < limit
filter-less index-set(a index-set, limit nat64) trusted
	if limit < a.capacity
		filtered = a.root filter-less-recur a.capacity, limit
		filtered simplify-after-remove-or-filter a.capacity
	else
		a
.filter-less-recur node(a node, n nat64, limit nat64) unsafe
	match a
	as inner i
		assert n > bits-in-leaf-node
		half = n / 2
		quarter = half / 2
		new-a = i.a filter-less-recur quarter, limit, 0
		new-b = i.b filter-less-recur quarter, limit, 0x40
		new-c = i.c filter-less-recur quarter, limit, 0x80
		new-d = i.c filter-less-recur quarter, limit, 0xc0
		new-node = new-a, new-b, new-c, new-d
		if new-node is-all-empty
			empty
		else
			new-node inner
	as leaf l
		assert n == bits-in-leaf-node
		new-a = l.a filter-less-word limit, 0
		new-b = l.b filter-less-word limit, 0x40
		new-c = l.c filter-less-word limit, 0x80
		new-d = l.d filter-less-word limit, 0xc0
		(new-a, new-b, new-c, new-d) leaf
	as empty
		empty
.filter-less-recur node(a node, n nat64, limit nat64, offset nat64) unsafe
	if limit < offset
		empty
	elif limit > offset + n
		a
	else
		a filter-less-recur n, limit - offset

.filter-less-word nat64(a nat64, limit nat64, offset nat64)
	if limit >= offset
		if limit > offset + 64
			a
		else
			a & (limit - offset low-n-bits)

.word-index nat64(a nat64) trusted
	a / 64
.bit-index nat64(a nat64) trusted
	a % 64

to-str str(a index-set)
	", " join for x : a; "{x}"

region iteration

for-loop void(a index-set, f act void(nat64))
	() fold a, (_, x) => f[x]

fold<a> a(acc a, a index-set, f act a(a, nat64)) trusted
	acc fold a.root, 0, a.capacity, f
# offset: how much to add to indices in this node
.fold<a> a(acc a, node node, offset nat64, n nat64, f act a(a, nat64)) unsafe
	match node
	as inner i
		half = n / 2
		quarter = half / 2
		three-quarters = half + quarter
		acc-a = acc fold i.a, offset, quarter, f
		acc-b = acc-a fold i.b, offset + quarter, quarter, f
		acc-c = acc-b fold i.c, offset + half, quarter, f
		acc-c fold i.d, offset + three-quarters, quarter, f
	as leaf l
		acc fold-with-index l.values, (cur, value, word-index) =>
			bit-index-offset = offset + word-index * 64
			cur fold-one-bits value, (inner-acc, bit-index) =>
				f[inner-acc, bit-index-offset + bit-index]
	as empty
		acc

###
TODO
filter index-set(a index-set, f act bool(nat64))
	strip-trailing-zeroes: a.values map-with-index (value, nat64-index) =>
		bit-index-offset = nat64-index + 64
		value filter-bits bit-index =>
			f[bit-index-offset + bit-index]
###

for-loop index-set(a index-set, f act nat64(nat64))
	a for-loop f to-index-set

for-loop<a> a[](a index-set, f act a(nat64)) trusted
	res mut-array a = a.size uninitialized-mut-array
	end = res.begin-ptr fold a, (cur, x) =>
		*cur := f[x]
		cur + 1
	assert end == res.end-ptr
	res.cast-immutable to-list

.dbg str(a index-set)
	a.root dbg
.dbg str(a node)
	match a
	as inner i
		"({i.a dbg} {i.b dbg} {i.c dbg} {i.d dbg})"
	as leaf l
		"[{l.a to-hex} {l.b to-hex} {l.c to-hex} {l.d to-hex}]"
	as empty
		"_"

.verify void(a index-set, expected str, expected-list nat64[])
	actual = a.dbg
	unless actual == expected
		info log "a.dbg is"
		info log a.dbg
		info log "but expected"
		info log expected
		a.dbg is expected
	actual is expected
	r = a to-list
	r is expected-list
	_ = 0 fold a, (prev, cur) =>
		for x : prev .. cur
			x in a is false
		cur in a is true
		cur + 1
	a.size is r.size

test
	a index-set = ()
	a verify "_", ()
	3 in a is false

	b = a ~ 8
	b verify "[100 0 0 0]", (8,)

	b count-less 8 is 0
	b count-greater-or-equal 8 is 1
	b count-range 4, 8 is 0
	b count-range 8, 9 is 1

	c = b ~~ (4, 9)
	c verify "[310 0 0 0]", (4, 8, 9)

	# TODO: 'is none'
	(c try-remove 7).is-empty is true
	(c try-add 8).is-empty is true

	d = c - 8
	d verify "[210 0 0 0]", (4, 9)

	e = c filter-less 9
	e verify "[110 0 0 0]", (4, 8)

	folded = 1 fold d, (cur, x) => cur + x
	folded is 14

	# TODO
	# filtered = c filter it => it != 8
	# filtered verify "[x210]", (4, 9)

	mapped index-set = for x : c
		10 - x
	mapped verify "[46 0 0 0]", (1, 2, 6)

test
	a index-set = 100,
	a verify "[0 1000000000 0 0]", (100,)
	b = a ~ 1000
	b verify "([0 1000000000 0 0] _ _ [0 0 0 10000000000])", (100, 1000)
	c = b ~ 5000
	cv = "((([0 1000000000 0 0] _ _ [0 0 0 10000000000]) _ _ _) ((_ _ _ [0 0 100 0]) _ _ _) _ _)"
	c verify cv, (100, 1000, 5000)
	d = c - 1000
	d verify "((([0 1000000000 0 0] _ _ _) _ _ _) ((_ _ _ [0 0 100 0]) _ _ _) _ _)", (100, 5000)
	e = c - 5000
	e verify "([0 1000000000 0 0] _ _ [0 0 0 10000000000])", (100, 1000)

# TODO: these belong in bits.crow

test
	0.high-n-bits is 0
	1.high-n-bits is 0b1000000000000000000000000000000000000000000000000000000000000000
	13.high-n-bits is 0b1111111111111000000000000000000000000000000000000000000000000000
	63.high-n-bits is 0b1111111111111111111111111111111111111111111111111111111111111110
	64.high-n-bits is 0b1111111111111111111111111111111111111111111111111111111111111111

test
	0.low-n-bits is 0
	1.low-n-bits is 1
	13.low-n-bits is 0b1111111111111
	63.low-n-bits is 0b111111111111111111111111111111111111111111111111111111111111111
	64.low-n-bits is 0b1111111111111111111111111111111111111111111111111111111111111111
