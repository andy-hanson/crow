no-std
import
	../../bool: bool, new, not, true
	../../fun-util: subscript
	../../misc: key, new, void
	../../number: nat64
	../../option: is-empty
	../../tuple: new
	../collection: build-options, for-loop
	../list: ~=, build, iterate, list, size
	./mut-sorted-dict:
		~=, build, clear, in, is-empty, iterate, mut-sorted-dict, new, remove-key, set-filter,
		set-map, set-subscript, size, subscript
	./sorted-set: sorted-set, to-sorted-set

###
Mutable sorted set of unique values.
This is like a `set` but mutable, or like a `mut-sorted-dict` with no values.
Unlike `mut-list`, values are in sorted order, not insertion order,
and it never stores two of the same value.
###
mut-sorted-set[t] record mut
	.inner (t, void) mut-sorted-dict

# New empty `mut-sorted-set`.
new[t] t mut-sorted-set() t key
	(),

to-mut-sorted-set[t] t mut-sorted-set(a t[]) t key
	inner (t, void) mut-sorted-dict = for x : a; x, ()
	inner,

# True iff the set has no elements.
is-empty[t] bool(a t mut-sorted-set) t key
	a.inner is-empty

# Removes all elements.
clear[t] void(a t mut-sorted-set) t key
	a.inner clear

# Number of values in the set.
size[t] nat64(a t mut-sorted-set) t key
	a.inner size

# True iff the value is in the set.
subscript[t] bool(a t mut-sorted-set, value t) t key
	value in a

in[t] bool(value t, a t mut-sorted-set) t key
	!a.inner[value].is-empty

###
Adds a value to the set.
Does nothing if the value is already in the set.
###
~=[t] void(a t mut-sorted-set, value t) t key
	a.inner[value] := ()

# Adds multiple values to the set.
~~=[t] void(a t mut-sorted-set, values t[]) t key
	for value : values
		a ~= value

try-add[t] bool(a t mut-sorted-set, value t) t key
	# TODO:PERF do it without two lookups
	unless value in a.inner
		a.inner[value] := ()
		true

###
Removes a value from the set.
If the value was present, removes it and returns `true`.
If the value was not present, does nothing and returns `false`.
###
remove[t] bool(a t mut-sorted-set, value t) t key
	!(a.inner remove-key value is-empty)

# Convert to a list, clearing the original `mut-sorted-set` in the process.
move-to-list[t] t[](a t mut-sorted-set) t key
	res t[] = for x : a; x
	a clear
	res

# Convert to a `sorted-set`, clearning the original `mut-sorted-set` in the process.
move-to-sorted-set[t] t sorted-set(a t mut-sorted-set) t key
	# TODO:PERF avoid a copy: return a 'mut-list' which the sorted-set can move into to itself
	a.move-to-list to-sorted-set

set-filter[t] void(a t mut-sorted-set, f fun bool(t)) t key
	a.inner set-filter (k, _) => f[k]

set-map[t] void(a t mut-sorted-set, f fun t(t)) t key
	a.inner set-map (k, _) => f[k], ()

region spec impls (see collection.crow)

iterate[t] bool(a t mut-sorted-set, f act bool(t)) t key
	a.inner iterate (k, _) => f[k]

subregion build

build[t] t mut-sorted-set(_ build-options, f act void(t mut-sorted-set)) t key
	res t mut-sorted-set = ()
	f[res]
	res
