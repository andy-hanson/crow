no-std
import
	../../bool: !, &&, ==, bool, true
	../../compare: <=, compare, equal
	../../fun-util: subscript
	../../hash: hash, hash-mix
	../../json: json
	../../misc: is-data, new, void
	../../number: ==, <=>, +, -, /, %, nat64
	../../option: ==, is-empty, new, option
	../../pointer: ==, <=>, +, -, *, const-pointer, mut-pointer, swap
	../../range: ..
	../../string: ==, literal, string
	../../test-util: is
	../../tuple: from, new, pair, to
	../array:
		~=, array, array-cast, as-array, begin-pointer, end-pointer, build, exists, for-loop,
		is-empty, new, size, subscript, with-block
	../mut-array:
		~=, begin-pointer, build, cast-immutable, end-pointer, fill, fill-mut-array, mut-array,
		mut-array-builder, for-loop, size, temp-as-array, to-mut-array, set-subscript, subscript,
		with-block
	../list: ==, for-loop, list, new, to-array, to-list

###
Immutable dictionary that is not efficient to update.
However, this is more compact than `dict` and has faster lookups.
###
frozen-dict<k, v> record by-val
	.pairs array (k, v)
	.bucket-starts array (k, v)*

new<k, v> frozen-dict<k, v>(...a array (k, v)) is-data<k> compare<k> hash-mix<k>
	a.to-list to-frozen-dict

to-frozen-dict<k, v> frozen-dict<k, v>(a (k, v)[]) trusted is-data<k> compare<k> hash-mix<k>
	a.to-array.to-mut-array as-frozen-dict

# This is O(1).
to-array<k, v> array<(k, v)>(a frozen-dict<k, v>) noctx
	a pairs

# This is O(1).
to-list<k, v> (k, v)[](a frozen-dict<k, v>) noctx
	a.to-array to-list

# TODO:INTERNAL
begin-pointer<k, v> (k, v)*(a frozen-dict<k, v>) unsafe
	a.pairs begin-pointer

end-pointer<k, v> (k, v)*(a frozen-dict<k, v>) unsafe
	a.pairs end-pointer

is-empty<k, v> bool(a frozen-dict<k, v>)
	a.pairs is-empty

size<k, v> nat64(a frozen-dict<k, v>)
	a.pairs size

subscript<k, v> v?(a frozen-dict<k, v>, key k) trusted is-data<k> compare<k> hash-mix<k>
	if pointer ?= a find-pointer key
		pointer->to,

in<k, v> bool(key k, a frozen-dict<k, v>) trusted is-data<k> compare<k> hash-mix<k>
	!(a find-pointer key is-empty)

# TODO:INTERNAL
try-get-unordered-index<k, v> nat64?(a frozen-dict<k, v>, key k) trusted is-data<k> compare<k> hash-mix<k>
	if pointer ?= a find-pointer key
		pointer - a.pairs.begin-pointer,

.find-pointer<k, v> (k, v)*?(a frozen-dict<k, v>, key k) unsafe is-data<k> compare<k> hash-mix<k>
	bucket = if a.bucket-starts is-empty
		a pairs
	else
		bucket-index = key.hash % a.bucket-starts.size
		bucket-start = a.bucket-starts[bucket-index]
		bucket-end = if bucket-index == a.bucket-starts.size - 1
			a.pairs end-pointer
		else
			a.bucket-starts[bucket-index + 1]
		bucket-start .. bucket-end as-array

	bucket.begin-pointer find-pointer bucket.end-pointer, key

find-index<k, v> nat64?(pairs array (k, v), key k) unsafe is-data<k> compare<k> hash-mix<k>
	if pointer ?= pairs.begin-pointer find-pointer pairs.end-pointer, key
		pointer - pairs.begin-pointer,

find-pointer<k, v> (k, v)*?(cur (k, v)*, end (k, v)*, key k) unsafe is-data<k> compare<k> hash-mix<k>
	if cur == end
		()
	elif cur->from == key
		cur,
	else
		cur + 1 find-pointer end, key

.as-frozen-dict<k, v> frozen-dict<k, v>(a mut-array (k, v)) unsafe is-data<k> compare<k> hash-mix<k>
	a.mut-array-to-frozen-dict-promise-no-duplicates remove-duplicates

mut-array-to-frozen-dict-promise-no-duplicates<k, v> frozen-dict<k, v>(
	a mut-array (k, v),
) no-doc unsafe is-data<k> compare<k> hash-mix<k>
	if a.size <= 4
		a.cast-immutable, ()
	else
		n-buckets = a.size / 2
		bucket-counts = a get-bucket-counts n-buckets
		bucket-starts = a.begin-pointer running-sum bucket-counts.temp-as-array
		bucket-counts fill 0
		cur mut = a begin-pointer
		until cur == a.end-pointer
			bucket-index = cur->from get-bucket-index bucket-counts.size
			bucket-count = bucket-counts[bucket-index]
			bucket-start = bucket-starts[bucket-index]
			bucket-cur-end = bucket-start + bucket-count
			if bucket-start <= cur && cur <= bucket-cur-end
				if cur == bucket-cur-end
					bucket-counts[bucket-index] := bucket-count + 1
				cur := cur + 1
			else
				bucket-counts[bucket-index] := bucket-count + 1
				cur swap bucket-cur-end
		a.cast-immutable, bucket-starts.array-cast

.remove-duplicates<k, v> frozen-dict<k, v>(a frozen-dict<k, v>) unsafe is-data<k> compare<k> hash-mix<k>
	if a has-duplicates
		# Write out pairs from each bucket to a mut-array, omitting a pair if its key appears later in the same bucket.
		# Then call mut-array-to-frozen-dict-promise-no-duplicates 
		xs mut-array (k, v) = with out : a.size build
			out write-without-duplicates a
		xs mut-array-to-frozen-dict-promise-no-duplicates
	else
		a

.write-without-duplicates<k, v> void(out mut-array-builder (k, v), a frozen-dict<k, v>) trusted equal<k>
	if a.bucket-starts is-empty
		out write-bucket-without-duplicates a.pairs
	else
		for bucket-start, i : a bucket-starts
			bucket-end = if i == a.bucket-starts.size - 1
				a.pairs.end-pointer
			else
				a.bucket-starts[i + 1]
			out write-bucket-without-duplicates (bucket-start .. bucket-end).as-array

.write-bucket-without-duplicates<k, v> void(out mut-array-builder (k, v), pairs array (k, v)) equal<k>
	for pair, i : pairs
		unless pair.from key-in-range pairs[i + 1 .. pairs.size]
			out ~= pairs[i]

.has-duplicates<k, v> bool(a frozen-dict<k, v>) trusted equal<k>
	if a.bucket-starts is-empty
		a.pairs bucket-has-duplicates
	else
		a.bucket-starts exists (bucket-start, i) =>
			bucket-end = if i == a.bucket-starts.size - 1
				a.pairs.end-pointer
			else
				a.bucket-starts[i + 1]
			(bucket-start .. bucket-end).as-array bucket-has-duplicates

.bucket-has-duplicates<k, v> bool(pairs array (k, v)) unsafe equal<k>
	pairs exists (pair, i) =>
		pair.from key-in-range pairs[i + 1 .. pairs.size]

.key-in-range<k, v> bool(key k, pairs array (k, v)) equal<k>
	pairs exists pair => key == pair.from

.get-bucket-counts<k, v> mut-array nat64(a mut-array (k, v), n-buckets nat64) unsafe hash-mix<k>
	res = n-buckets fill-mut-array 0
	for pair : a
		bucket-index = pair.from get-bucket-index n-buckets
		res[bucket-index] := res[bucket-index] + 1
	res

.get-bucket-index<k> nat64(key k, n-buckets nat64) hash-mix<k>
	key.hash % n-buckets

.running-sum<k, v> array<(k, v) mut*>(initial (k, v) mut*, offsets array nat64) trusted
	with out : offsets.size build
		acc mut = initial
		for offset : offsets
			out ~= acc
			acc := acc + offset

region builder

build-frozen-dict record
	.size-hint nat64
build build-frozen-dict()
	0,
build build-frozen-dict(size-hint nat64)
	size-hint,

frozen-dict-builder<k, v> record mut
	.inner mut-array-builder (k, v)

with-block<k, v> frozen-dict<k, v>(
	a build-frozen-dict,
	f act void(frozen-dict-builder<k, v>),
) trusted is-data<k> compare<k> hash-mix<k>
	(with out : a.size-hint build; f[(out,)]) as-frozen-dict

~= <k, v> void(a frozen-dict-builder<k, v>, pair (k, v))
	a.inner ~= pair

region iteration

for-loop<k, v> void(a frozen-dict<k, v>, f act void(k, v))
	for pair : a pairs
		f[pair from, pair to]

for-loop<out, k, v> out[](a frozen-dict<k, v>, f act out(k, v))
	for pair : a.pairs to-list
		f[pair from, pair to]

.verify void(a frozen-dict<nat64, string>, keys-order nat64[], bucket-starts nat64[]) unsafe
	(for pair : a.pairs to-list; pair from)::nat64[] is keys-order
	(for pointer : a.bucket-starts to-list; pointer - a.pairs.begin-pointer)::nat64[] is bucket-starts

test
	a frozen-dict<nat64, string> = ()
	a.is-empty is true
	a.size is 0
	a verify (), ()
	a[0] is ()
	
	b frozen-dict<nat64, string> = (3, "3"), (1, "1"), (5, "5"), (2, "2")
	b verify (3, 1, 5, 2), ()
	b[0] is ()
	b[1] is ("1",)
	b[2] is ("2",)
	b[3] is ("3",)
	b[4] is ()
	b[5] is ("5",)
	b[6] is ()

	c frozen-dict<nat64, string> = (3, "3"), (1, "1"), (5, "5"), (2, "2"), (8, "8")
	c verify (2, 8, 3, 5, 1), (0, 2)
	c[0] is ()
	c[1] is ("1",)
	c[2] is ("2",)
	c[3] is ("3",)
	c[4] is ()
	c[5] is ("5",)
	c[6] is ()
	c[7] is ()
	c[8] is ("8",)
	c[9] is ()

test
	a frozen-dict<nat64, string> = (1, "1"), (1, "2")
	a.size is 1
	a verify (1,), ()
	a[1] is ("2",)
