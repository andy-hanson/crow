no-std
import
	../bool: ==, bool, false, new, not, true
	../compare: <=, >, compare, comparison, equal, greater, less
	../fun-util: subscript
	../misc: break-or-continue, loop-break, loop-continue, new, void
	../number: ==, <=>, +, -, nat64
	../option: ??, force, is-empty, new, option
	../private/bootstrap: exception, string
	../private/range-low-level: .., iterate, range, size, subtract-to-nat
	../tuple: a, b, new
export
	./private/build

+index-too-big record(actual nat64, maximum nat64) by-val, exception variant-member
+describe string(_ index-too-big)
	"The index is bigger than the collection's size"

# Exception when attempting to skip too far forward or backward
+iterator-out-of-bounds record exception variant-member
+describe string(_ iterator-out-of-bounds)
	"Iterator out of bounds"
# For a 'slice', the first iterator should be to the right of the second iterator.
+slice-right-to-left record exception variant-member
+describe string(_ slice-right-to-left)
	"Iterators for slice were in the wrong order"

# Some iterator operations like 'slice' require two iterators to be for the same collection.
+iterator-not-comparable record exception variant-member
+describe string(_ iterator-not-comparable)
	"Iterators are for different collections and can't be compared."

+is-empty[t] spec
	is-empty bool(a t)
+size[t] spec
	size nat64(a t)

+iterate[col, elem] spec
	iterate bool(a col, f bool mut(elem elem))

+begin[iter, col] spec
	begin iter(a col)
+end[iter, col] spec
	end iter(a col)
+begin-end[iter, col] spec (iter, col) begin, (iter, col) end
+collection[col, iter] spec
	collection col(a iter)

+prev[elem, iter] spec
	prev (iter, elem)?(a iter)
+next[elem, iter] spec
	next (elem, iter)?(a iter)
+prev-next[elem, iter] spec (elem, iter) prev, (elem, iter) next

+set-prev[iter, elem] spec
	set-prev void(a iter, value elem)
+set-next[iter, elem] spec
	set-next void(a iter, value elem)
+set-prev-next[iter, elem] spec (iter, elem) set-prev, (iter, elem) set-next

+slice[col, iter] spec
	slice col(begin iter, end iter)
+slice-out-of-order record exception variant-member
+describe string(_ slice-out-of-order)
	"Tried to slice from a later iterator to an earlier one."

+skip-prev[iter] spec
	- iter(a iter, n nat64)
+skip-next[iter] spec
	+ iter(a iter, n nat64)
+skip-prev-next[iter] spec iter skip-prev, iter skip-next

is-at-begin[iter, elem] bool(a iter) (elem, iter) prev, bare
	a.prev is-empty
is-at-end[iter, elem] bool(a iter) (elem, iter) next, bare
	a.next is-empty

# Represents an index relative to the size of a collection
+index-relative-to-end record(subtract nat64) by-val

+end index-relative-to-end() bare
	0,

+- index-relative-to-end(a index-relative-to-end, n nat64) bare
	a.subtract + n,

+at-index[col, elem] spec
	subscript elem(a col, index nat64)

+subscript[col, elem] elem(a col, index index-relative-to-end) bare, (col, elem) at-index, col size
	a[a.size - index.subtract]

+set-at-index[col, elem] spec
	set-subscript void(a col, index nat64, value elem)

+set-subscript[col, iter, elem] void(
	a col,
	index index-relative-to-end,
	value elem,
) bare, (col, elem) set-at-index, col size
	a[a.size - index.subtract] := value

+fill[col, iter, elem] void(
	a col,
	value elem,
) (iter, col) begin, (elem, iter) next, (iter, elem) set-next, bare
	iter mut = a begin
	while _, next ?= iter next
		iter set-next value
		iter := next

+map=[col, iter, elem] void(
	a col,
	f elem mut(elem elem),
) (iter, col) begin, (elem, iter) next, (iter, elem) set-next
	iter mut = a begin
	while x, next ?= iter next
		iter set-next f[x]
		iter := next

+range-relative-to-end record(low nat64, high index-relative-to-end) by-val

+.. range-relative-to-end(low nat64, high index-relative-to-end) bare
	low, high

+to-range nat64 range(a range-relative-to-end, size nat64) bare
	assert a.low <= size : a.low index-too-big size
	assert a.high.subtract <= size : a.high.subtract index-too-big size
	high = size - a.high.subtract
	assert a.low <= high
	a.low .. high

+subscript-range[col] spec
	subscript col(a col, range nat64 range)

+subscript[col] col(a col, indices range-relative-to-end) col size, col subscript-range, bare
	a[indices to-range a.size]

+slice-left[col, iter] col(a iter) (col, iter) collection, (iter, col) begin, (col, iter) slice
	a.collection.begin slice a
+slice-right[col, iter] col(a iter) (col, iter) collection, (iter, col) end, (col, iter) slice
	a slice a.collection.end

+set-reverse[col, iter, elem] void(
	a col,
) (iter, col) begin-end, (elem, iter) prev-next, iter subtract-to-nat, (iter, elem) set-prev-next
	lo mut = a begin
	hi mut = a end
	while hi - lo > 1
		pair0 = lo.next!
		pair1 = hi.prev!
		lo set-next pair1.b
		hi set-prev pair0.a
		lo := pair0 b
		hi := pair1 a

swap-at[col, elem] void(a col, i nat64, j nat64) (col, elem) at-index, (col, elem) set-at-index
	temp = a[i]
	a[i] := a[j]
	a[j] := temp

region 'for' loops

# Call 'f' for each element in the collection and return nothing.
+for-loop[col, elem] void(a col, f void mut(elem elem)) (col, elem) iterate
	res = a iterate x =>
		f[x]
		false
	forbid res

# Call 'f' for each element in the input collection
# and return an output collection of the results of 'f'.
+for-loop[out-col, out-elem, out-builder, in-col, in-elem] out-col(
	in in-col,
	f out-elem mut(elem in-elem),
) (out-col, out-elem, out-builder) build, (in-col, in-elem) iterate, in-col size
	(in.size,) build builder =>
		for x : in
			builder ~= f[x]

###
Call `f` for each element in the collection, but stop and return the result of the first `break`.
If there is no `break`, return `f-else` instead.
###
+for-break[out, col, elem] out(
	a col,
	f-loop out break-or-continue mut(elem elem),
	f-else out mut(),
) (col, elem) iterate
	a for-break f-loop ?? f-else[]

+for-break[col, elem] void(a col, f-loop void break-or-continue mut(elem elem)) (col, elem) iterate
	a for-break f-loop ?? ()

###
Call `f` for each element in the collection, but stop and return the result of the first `break`.
If there is no `break`, return an empty option instead.
###
+for-break[out, col, elem] out?(
	a col,
	f-loop out break-or-continue mut(elem elem),
) (col, elem) iterate
	res mut out? = ()
	broke = a iterate x =>
		match f-loop[x]
		as loop-break value
			res := value,
			true
		as loop-continue
			false
	assert broke == !res.is-empty
	res

###
Call `f` for each element in the input collection,
and return an output collection of the elements for which `f` was `true`.
###
+filter[out-col, elem, out-builder, in-col] out-col(
	in in-col,
	f bool mut(elem elem),
) (out-col, elem, out-builder) build, (in-col, elem) iterate
	with out : build
		for x : in
			if f[x]
				out ~= x

+reduce[col, elem] elem?(a col, f elem mut(acc elem, next elem)) (col, elem) iterate
	res mut elem? = ()
	for x : a
		res := if cur ?= res
			f[cur, x],
		else
			x,
	res

sequence-equal[col, iter, elem] bool(
	a col,
	b col,
) col size, (iter, col) begin, (elem, iter) next, elem equal, bare
	guard a.size == b.size
	ai mut = a begin
	bi mut = b begin
	loop
		if na ?= ai next
			nb = bi.next!
			if na.a == nb.a
				ai := na b
				bi := nb b
				continue
			else
				break false
		else
			assert bi is-at-end
			break true

sequence-compare[col, iter, elem] comparison(
	a col,
	b col,
) (iter, col) begin, (elem, iter) next, elem compare, bare
	ai mut = a begin
	bi mut = b begin
	loop
		if na ?= ai next
			if nb ?= bi next
				cmp = na.a <=> nb.a
				match cmp
				as less
					break less
				as equal
					ai := na b
					bi := nb b
					continue
				as greater
					break greater
			else
				break greater
		else
			break bi.is-at-end ? equal : less

###
True if `value` is an element of `a`.
For sets, prefer `in`.
###
+contains[col, elem] bool(a col, value elem) (col, elem) iterate, elem equal
	a iterate x => x == value

+starts-with[col, iter, elem] bool(a col, b col) (iter, col) begin, (elem, iter) next, elem equal
	!(a.begin iter-starts-with b).is-empty

+ends-with[col, iter, elem] bool(a col, b col) (iter, col) end, (elem, iter) prev, elem equal
	!(a.end iter-ends-with b).is-empty

+try-remove-start[col, iter, elem] col?(
	a col,
	start col,
) (iter, col) begin-end, (elem, iter) next, (col, iter) slice, elem equal
	if after ?= a.begin iter-starts-with start
		(after slice a.end),
+try-remove-end[col, iter, elem] col?(
	a col,
	end col,
) (iter, col) begin-end, (elem, iter) prev, (col, iter) slice, elem equal
	if before ?= a.end iter-ends-with end
		(a.begin slice before),

# Removes all occurrences of 'b' from the front of 'a'.
+strip-left[col, iter, elem] col(
	a col,
	b col,
) (iter, col) begin-end, (elem, iter) next, (col, iter) slice, elem equal
	if x ?= a try-remove-start b
		x strip-left b
	else
		a
# Removes all occurrences of 'b' from the end of 'a'.
+strip-right[col, iter, elem] col(
	a col,
	b col,
) (iter, col) begin-end, (elem, iter) prev, (col, iter) slice, elem equal
	if x ?= a try-remove-end b
		x strip-right b
	else
		a

# Returns iterator after 'start' if 'ai' starts with 'start'
-iter-starts-with[col, iter, elem] iter?(
	a iter,
	start col,
) (iter, col) begin, (elem, iter) next, elem equal
	ai mut = a
	bi mut = start begin
	loop
		guard bp ?= bi next : break ai,
		guard ap ?= ai next : break
		guard ap.a == bp.a : break
		ai := ap b
		bi := bp b
		continue
-iter-ends-with[col, iter, elem] iter?(
	a iter,
	end col,
) (iter, col) end, (elem, iter) prev, elem equal
	ai mut = a
	bi mut = end end
	loop
		guard bp ?= bi prev : break ai,
		guard ap ?= ai prev : break
		guard ap.b == bp.b : break
		ai := ap a
		bi := bp a
		continue

###
`true` iff `b` is contained in `a`.

That means: The elements in `b` appear in `a`, in order, without interruptions.
###
+contains-seq[col, iter, elem] bool(a col, b col) (iter, col) begin, (elem, iter) next, elem equal
	!(a iters-of-seq b).is-empty

###
Finds the first appearance of `b` in `a`,
and returns slices of `a` to the left and right of that.

Returns an empty option iff `b` is not a subsequence of `a`.
###
+try-split-once[col, iter, elem] (col, col)?(
	a col,
	b col,
) (iter, col) begin-end, (elem, iter) next, (col, iter) slice, elem equal
	if l, r ?= a iters-of-seq b
		((a.begin slice l), (r slice a.end)),

+try-split-once-right[col, iter, elem] (col, col)?(
	a col,
	b col,
) (iter, col) begin-end, (elem, iter) prev, (col, iter) slice, elem equal
	if l, r ?= a iters-of-seq-right b
		((a.begin slice l), (r slice a.end)),

-iters-of-seq[col, iter, elem] (iter, iter)?(
	a col,
	b col,
) (iter, col) begin, (elem, iter) next, elem equal
	ai mut = a begin
	loop
		if after ?= ai iter-starts-with b
			break (ai, after),
		elif _, next ?= ai next
			ai := next
			continue
		else
			break

-iters-of-seq-right[col, iter, elem] (iter, iter)?(
	a col,
	b col,
) (iter, col) end, (elem, iter) prev, elem equal
	ai mut = a end
	loop
		if before ?= ai iter-ends-with b
			break (before, ai),
		elif prev, _ ?= ai prev
			ai := prev
			continue
		else
			break

###
Returns the first element for which `f` returns true,
or an empty option if `f` was always `false`.
###
+find[col, elem] elem?(a col, f bool mut(elem elem)) (col, elem) iterate
	for x : a
		if f[x]
			break x

###
Returns the first index `i` for which `f[a[i]]` is true.
Returns an empty option iff `f` returns `false` for every elem of `a`.
###
+find-index[col, elem] nat64?(a col, f bool mut(elem elem)) (col, elem) iterate
	i mut nat64 = 0
	for x : a
		if f[x]
			break i
		else
			i +:= 1
			continue

+find-last-iter[col, iter, elem] iter?(
	a col,
	f bool mut(elem elem),
) (iter, col) end, (elem, iter) prev
	iter mut = a end
	loop
		guard prev, x ?= iter prev : break
		iter := prev
		if f[x]
			break iter,
		else
			continue

+last-iter-of[col, iter, elem] iter?(
	a col,
	value elem,
) (iter, col) end, (elem, iter) prev, elem equal
	a find-last-iter x => x == value

###
If `search-value` is in `a`, return its index.
###
+index-of[col, elem] nat64?(a col, search-value elem) (col, elem) iterate, elem equal
	a find-index x => x == search-value

###
Equivalent to `f[a[0]] || f[a[1]] || ... || f[a[end - 1]]`

Calls `f` on each element of `a` and returns `true` the first time `f` does.
Returns `false` iff `f` returned `false` every time.
###
+exists[col, elem] bool(a col, f bool mut(elem elem)) (col, elem) iterate
	a iterate f

###
Equivalent to `f[a[0]] && f[a[1]] && ... && f[a[end - 1]]`.

Calls `f` on each element of `a` and returns `false` the fist time `f` does.
Returns `true` iff `f` returned `true` every time.
###
+every[col, elem] bool(a col, f bool mut(elem elem)) (col, elem) iterate
	!(a iterate x => !f[x])

###
Drops elements from the left until `f` returns `false`.
If `f` always returns true, this returns an empty collection.
###
+drop-while[col, iter, elem] col(
	a col,
	f bool mut(elem elem),
) (iter, col) begin-end, (elem, iter) next, (col, iter) slice
	iter mut = a begin
	loop
		guard x, next ?= iter next : break
		guard f[x] : break
		iter := next
		continue
	iter slice a.end

# Drops elements from the right that match `f`.
+drop-right-while[col, iter, elem] col(
	a col,
	f bool mut(elem elem),
) (iter, col) begin-end, (elem, iter) prev, (col, iter) slice
	iter mut = a end
	loop
		guard prev, x ?= iter prev : break
		guard f[x] : break
		iter := prev
		continue
	a.begin slice iter

+take-while[col, iter, elem] col(
	a col,
	f bool mut(elem elem),
) (iter, col) begin, (elem, iter) next, (col, iter) slice
	iter mut = a begin
	loop
		guard x, next ?= iter next : break
		guard f[x] : break
		iter := next
		continue
	a.begin slice iter

+take-right-while[col, iter, elem] col(
	a col,
	f bool mut(elem elem),
) (iter, col) end, (elem, iter) prev, (col, iter) slice
	iter mut = a end
	loop
		guard prev, x ?= iter prev : break
		guard f[x] : break
		iter := prev
		continue
	iter slice a.end

+concatenate[t] spec
	~~ t(a t, b t)

+flatten[col, col-col] col(a col-col) (col-col, col) iterate, col concatenate, col new
	res mut col = ()
	for x : a
		res ~~:= x
	res

# Concatenate `n` copies of `a`.
+repeat[col] col(a col, n nat64) col concatenate, col new
	res mut col = ()
	for _ nat64 : 0 .. n
		res ~~:= a
	res

+n-of[col, elem, builder] col(n nat64, value elem) (col, elem, builder) build
	for _ nat64 : 0 .. n
		value

+reverse[col, iter, elem, builder] col(
	a col,
) col size, (col, elem, builder) build, (iter, col) end, (elem, iter) prev
	with out : a.size build
		for x : a reversed
			out ~= x

+reversed[col] record(inner col) nominal
+size[col] nat64(a col reversed) col size
	a.inner size
+for-loop[col, iter, elem] void(
	a col reversed,
	f void mut(elem elem),
) (iter, col) end, (elem, iter) prev
	# TODO: define 'iterate' instead
	iter mut = a.inner end
	while prev, x ?= iter prev
		f[x]
		iter := prev
+for-loop[out-col, out-elem, out-builder, in-col, in-iter, in-elem] out-col(
	a in-col reversed,
	f out-elem mut(elem in-elem),
) (out-col, out-elem, out-builder) build,
		(in-iter, in-col) end,
		(in-elem, in-iter) prev,
		in-col size
	with out : a.size build
		iter mut = a.inner end
		while prev, x ?= iter prev
			out ~= f[x]
			iter := prev

region with-index

+indices[col] nat64 range(a col) col size
	0 .. a.size

+with-index[col] record
	-inner col
+with-index[col] col with-index(a col)
	a,
+for-loop[col, elem] void(a col with-index, f void mut(index nat64, elem elem)) (col, elem) iterate
	i mut nat64 = 0
	for x : a inner
		f[i, x]
		i +:= 1
+for-loop[out-col, out-elem, out-builder, in-col, in-elem] out-col(
	a in-col with-index,
	f out-elem mut(index nat64, elem in-elem),
) (out-col, out-elem, out-builder) build, (in-col, in-elem) iterate, in-col size
	(a.inner.size,) build builder =>
		i mut nat64 = 0
		for x : a inner
			builder ~= f[i, x]
			i +:= 1
+for-break[col, elem] void(
	a col with-index,
	f void break-or-continue mut(index nat64, elem elem),
) (col, elem) iterate
	i mut nat64 = 0
	_ = a.inner iterate x =>
		match f[i, x]
		as loop-break _
			true
		as loop-continue
			i +:= 1
			false
+exists[col, elem] bool(a col with-index, f bool mut(index nat64, elem elem)) (col, elem) iterate
	i mut nat64 = 0
	a.inner iterate x =>
		res = f[i, x]
		i +:= 1
		res
