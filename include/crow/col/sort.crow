no-std
import
	../bool: bool, false, true
	../compare: <, <=, >, compare
	../fun-util: subscript
	../json: to
	../misc: loop-break, loop-continue, new, void
	../number: ==, <=>, +, -, /, nat64
	../option: ==, new, option
	../range: ..
	../string: ==, <=>, char32
	../test-util: is
	../tuple: a, b, new
	./array: ==, array, new, subscript
	./collection:
		.., begin, build, end, for-break, for-loop, iterate, prev, prev-next, set-next, size,
		subscript, swap-at
	./mut-array:
		~=, +, as-slice, begin, build, cast-immutable, iterate, mut-array, mut-slice, new, next,
		prev, set-next, set-subscript, size, slice, to, subscript
	./list: ~=, ==, build, iterate, list, new, size, to
	./mut-list: mut-list, temp-as-mut-array

+is-sorted[col, elem] bool(a col) (col, elem) iterate, elem compare
	prev mut elem? = ()
	for x : a
		if p ?= prev
			if p > x
				break false
			else
				prev := x,
				continue
		else
			prev := x,
			continue
	else
		true

###
Sorts the elements of `a` from least to greatest.

The result satisfies `res[i] <= res[i + 1]`.
###
+sort[col, elem, builder] col(
	a col,
) (col, elem, builder) build, (col, elem) iterate, col size, elem compare
	a sort x => x

###
Same as `sort`, but use `get-key` to get the comparable part of each element.
###
+sort[col, elem, builder, k] col(
	a col,
	get-key k data(elem elem),
) (col, elem, builder) build, (col, elem) iterate, col size, k compare
	res elem mut-array = for x : a; x
	res sort= get-key
	for x : res; x

###
Sort and remove duplicates.
That means `res[i] < res[i + 1]` for each element in the result.
`(2, 1, 2) sort-unique` is `1, 2`.
###
+sort-unique[col, elem, builder] col(
	a col,
) (col, elem, builder) build, (col, elem) iterate, col size, elem compare
	m elem mut-array = for x : a; x
	for x : m set-sort-unique; x

test
	()::nat64[].sort is ()
	(3, 1, 2)::nat64[].sort is (1, 2, 3)

	# Test that it's a stable sort
	arr char32 array mut-array = "2a", "2b", "1a", "1b"
	arr sort= x => x[0]
	arr.to::(char32 array array) is ("1a", "1b", "2a", "2b")
	arr sort= x => x[1]
	arr.to::(char32 array array) is ("1a", "2a", "1b", "2b")

# Sort the elements of `a` from least to greatest.
+set-sort[col, iter, elem] void(
	a col,
) (iter, col) begin, (elem, iter) prev-next, (iter, elem) set-next, elem compare
	a sort= x => x
# Like `set-sort`, but use `get-key` to get the comparable part of each element.
+sort=[col, iter, elem, k] void(
	a col,
	get-key k data(elem elem),
) (iter, col) begin, (elem, iter) prev-next, (iter, elem) set-next, k compare
	# TODO: PERF Try https://github.com/BonzaiThePenguin/WikiSort
	cur mut = a begin
	while pair ?= cur next
		cur move-last-sorted pair.a, get-key
		cur := pair b
-move-last-sorted[iter, elem, k] void(
	init iter,
	value-to-move elem,
	get-key k data(elem elem),
) (elem, iter) prev, (iter, elem) set-next, k compare
	cur mut = init
	key-to-move = get-key[value-to-move]
	loop
		guard prev-pair ?= cur prev : break
		guard get-key[prev-pair b] > key-to-move : break
		cur set-next prev-pair.b
		cur := prev-pair a
		continue
	cur set-next value-to-move

###
Sort `a` in place and remove unique elements.
Returns is a slice of the original `a`, which will be shorter if non-unique elements were removed.
###
-set-sort-unique[t] t mut-slice(a t mut-array) t compare
	a set-sort
	out mut = a begin
	in mut = a begin
	prev mut t? = ()
	while pair ?= in next
		unless prev == (pair.a,)
			out set-next pair.a
			out +:= 1
			prev := pair.a,
		in := pair b
	a.begin slice out

###
Sort `a`, and move elements of `b` in the same permutation as `a`.

This is useful if `a` and `b` are parallel arrays, meaning `a[i]` and `b[i]` belong together.

Every pair `a[i]` and `b[i]`, will move to `res.a[j]` and `res.b[j]` for some `j`.
###
~sort-by-first[t, u] (t[], u[])(a t[], b u[]) t compare
	mut-a t mut-array = a to
	mut-b u mut-array = b to
	mut-a.as-slice set-sort-by-first mut-b.as-slice
	trusted mut-a.cast-immutable.to, mut-b.cast-immutable.to

# Sort `a` in place, and move elements of `b` in the same permutation as `a`.
~set-sort-by-first[t, u] void(a t mut[], b u mut[]) t compare
	trusted a.temp-as-mut-array.as-slice set-sort-by-first b.temp-as-mut-array.as-slice

# Sort `a` in place, and move elements of `b` in the same permutation as `a`.
~set-sort-by-first[t, u] void(a t mut-slice, b u mut-slice) t compare
	assert a.size == b.size
	if a.size > 1
		a swap-at 0, a.size / 2
		pivot = a[0]
		new-pivot-index = (a set-partition-by-first b, pivot, 1, a.size - 1) - 1
		a swap-at 0, new-pivot-index
		b swap-at 0, new-pivot-index
		a[0 .. new-pivot-index] set-sort-by-first b[0 .. new-pivot-index]
		a[new-pivot-index + 1 .. end] set-sort-by-first b[new-pivot-index + 1 .. end]

-set-partition-by-first[t, u] nat64(
	a t mut-slice,
	b u mut-slice,
	pivot t,
	left nat64,
	right nat64,
) t compare
	l mut = left
	r mut = right
	while l <= r
		assert r < a.size
		if a[l] < pivot
			l +:= 1
		else
			a swap-at l, r
			b swap-at l, r
			r -:= 1
	l
