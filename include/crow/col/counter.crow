no-std
import
	../bool: bool
	../hash: hash-mix
	../json: json
	../misc: key
	../number: ==, <=>, +, nat64
	../option: ??
	../test-util: is
	../tuple: new
	./array: array
	./collection: for-loop
	./dict: ~, dict, iterate, subscript
	./list: iterate, list, to-list
	./mut-dict: mut-dict, new, set-subscript, subscript, to-dict

+counter[k] record
	-inner nat64[k]

+new[k] k counter(...a k array) k key
	a.to-list to-counter

+to-counter[k] k counter(values k[]) k key
	res nat64 mut[k] = ()
	for x : values
		res[x] := (res[x] ?? 0) + 1
	res.to-dict,

+to-dict[k] nat64[k](a k counter) k key
	a inner

+subscript[k] nat64(a k counter, value k) k key
	a.inner[value] ?? 0

+~[k] k counter(a k counter, value k) k key
	a.inner ~ (value, a[value] + 1),

region spec impls (see collection.crow)

+iterate[k] bool(a k counter, f act bool(k, nat64)) k key
	a.inner iterate f

test
	a nat64 counter = ()
	a[1] is 0
	b = a ~ 3
	b[3] is 1
	c = b ~ 3
	c[3] is 2
