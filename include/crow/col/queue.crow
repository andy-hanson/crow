no-std
import
	../bool: &&, ==, bool, true
	../compare: !=
	../fun-util: subscript
	../misc: void
	../number: ==, <=>, +, nat64
	../opt: !, is-empty, new, opt
	../ptr: ==, +, -, set-deref
	../test-util: is
	../range: ..
	./array: array, for-loop, is-empty, new, size
	./array-util: first, fold, tail
	./mut-array: begin-ptr, cast-immutable, end-ptr, mut-array, subscript, uninitialized-mut-array
	./list: ==, list, new, to-array, to-list
	./stack: ~, fold, for-loop, is-empty, new, size, stack, to-list

###
Immutable queue.

Supports efficient removal from the left and insertion to the right.
###
queue<a> record
	.left array a
	.right stack a

# Empty queue.
new<a> queue a()
	(), ()

to-queue<a> queue a(a a[])
	a.to-array, ()

###
Copies queue elements to an array.

This is O(n).
###
to-list<a> a[](a queue a) trusted
	res mut-array a = a.size uninitialized-mut-array
	end = res.begin-ptr fold a, (out, x) =>
		*out := x
		out + 1
	assert end == res.end-ptr
	res.cast-immutable to-list

# `true` iff `a.size == 0`.
is-empty<a> bool(a queue a)
	a.left.is-empty && a.right.is-empty

# Number of elements in the queue.
size<a> nat64(a queue a)
	a.left.size + a.right.size

# Adds a new element on the right.
~ <a> queue a(a queue a, value a)
	a.left, a.right ~ value

dequeued<a> record
	value a
	rest queue a

###
Removes and returns an element from the left.
Returns an empty option iff `a` is empty.
###
dequeue<a> dequeued<a>?(a queue a)
	if first ?= a.left first
		(first, (a.left.tail, a.right)),
	else
		right-array = a.right.to-list to-array
		if first ?= right-array first
			(first, (right-array.tail, ())),

region iteration

for-loop<a> void(a queue a, f act void(a))
	a.left for-loop f
	a.right for-loop f

for-loop<out, in> queue out(a queue in, f act out(in)) trusted
	res mut-array out = a.size uninitialized-mut-array
	end = res.begin-ptr fold a, (out, x) =>
		*out := f[x]
		out + 1
	assert end == res.end-ptr
	res.cast-immutable.to-list to-queue

fold<a, b> a(acc a, a queue b, f act a(a, b))
	acc fold a.left, f fold a.right, f

filter<a> queue a(a queue a, f act bool(a)) trusted
	res mut-array a = a.size uninitialized-mut-array
	end = res.begin-ptr fold a, (out, x) =>
		if f[x]
			*out := x
			out + 1
		else
			out
	res[0 .. end - res.begin-ptr].cast-immutable.to-list to-queue

test
	a queue nat64 = ()
	assert a is-empty
	a.to-list is ()
	a.dequeue.is-empty is true

	b = a ~ 1
	b.to-list is (1,)
	bd = b.dequeue!
	bd.value is 1
	assert bd.rest is-empty

	c = b ~ 2
	c.to-list is (1, 2)
	cd = c.dequeue!
	cd.value is 1
	cdr = cd rest

	cdr.to-list is (2,)
	cdd = cdr.dequeue!
	cdd.value is 2
	assert cdd.rest is-empty

	d = c ~ 3
	d.to-list is (1, 2, 3)

	(d filter x => x != 2).to-list is (1, 3)
	(for x : d; x + 10)::queue<nat64>.to-list is (11, 12, 13)
