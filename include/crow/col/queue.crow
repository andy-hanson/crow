no-std
import
	..bool: ==, and, bool, false
	..compare: !=
	..exception: assert
	..fun: subscript
	..misc: ->, void
	..opt: force, has?, opt, some
	..ptr: +, -, ptr-eq?, set-subscript
	..test: is
	..private.number-low-level: ==, nat64, wrap-add
	.arr: ==, arr, empty?, size
	.arr-util: each, first, fold, tail
	.mut-arr: begin-ptr, cast-immutable, end-ptr, subscript, uninitialized-mut-arr
	.stack: ~, arr, each, empty?, fold, size, stack

queue<?a> record
	left arr ?a
	right stack ?a

# Empty queue.
queue queue ?a()
	queue: [], stack

###
Convert an arr to a queue.
This is O(1).
###
queue queue ?a(a arr ?a)
	queue: a, stack

arr arr ?a(a queue ?a) trusted
	res = a.size uninitialized-mut-arr<?a>
	end = res.begin-ptr fold a, (out, x) =>
		out[] := x
		out + 1
	assert: end ptr-eq? res.end-ptr
	res cast-immutable

size nat64(a queue ?a)
	a.left.size wrap-add a.right.size

~ queue ?a(a queue ?a, value ?a)
	queue: a.left, a.right ~ value

empty? bool(a queue ?a)
	a.left.empty? and a.right.empty?

dequeued<?a> record
	value ?a
	rest queue ?a

dequeue opt dequeued ?a(a queue ?a)
	if first ?= a.left first
		some: dequeued: first, queue: a.left.tail, a.right
	else
		right-arr = a.right arr
		if first ?= right-arr first
			some: dequeued: first, queue: right-arr.tail, stack

region iteration

each void(a queue ?a, f act void(?a))
	a.left each f
	a.right each f

fold ?a(acc ?a, a queue ?b, f act ?a(?a, ?b))
	acc fold a.left, f fold a.right, f

filter queue ?a(a queue ?a, f act bool(?a)) trusted
	res = a.size uninitialized-mut-arr<?a>
	end = res.begin-ptr fold a, (out, x) =>
		if f[x]
			out[] := x
			out + 1
		else
			out
	queue: res[0 -> end - res.begin-ptr] cast-immutable

map queue ?out(a queue ?in, f act ?out(?in)) trusted
	res = a.size uninitialized-mut-arr<?out>
	end = res.begin-ptr fold a, (out, x) =>
		out[] := f[x]
		out + 1
	assert: end ptr-eq? res.end-ptr
	queue: res.cast-immutable

test
	a = queue<nat64>
	assert: a empty?
	a.arr is []
	a.dequeue.has? is false

	b = a ~ 1
	b.arr is [1]
	bd = b.dequeue force
	bd.value is 1
	assert: bd.rest empty?

	c = b ~ 2
	c.arr is [1, 2]
	cd = c.dequeue force
	cd.value is 1
	cdr = cd rest

	cdr.arr is [2]
	cdd = cdr.dequeue force
	cdd.value is 2
	assert: cdd.rest empty?

	d = c ~ 3
	d.arr is [1, 2, 3]

	(d filter x => x != 2).arr is [1, 3]
	(d map x => x wrap-add 10).arr is [11, 12, 13]
