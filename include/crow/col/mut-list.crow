no-std
import
	string: memcpy, memmove
	../bool: !, bool
	../compare: <, <=, >, >=, equal
	../fun: subscript
	../misc: ->, drop, void
	../number: +, -, *, nat64
	../opt: opt, some
	../ptr: ==, +, -, as-any-ptr, by-val, ptr, set-subscript, size-of, subscript, zeroed
	../private/alloc:
		alloc-uninitialized, copy-data-from!, set-zero-range, unmanaged-alloc-zeroed-elements
	../private/bootstrap: hard-assert, todo
	../private/exception-low-level: assert
	../private/number-low-level: ==, <=>, wrap-add, wrap-sub
	./arr: arr
	./arr-util: each, each-with-index, exists, find-index, fold, fold-reverse, in, index-of
	./mut-arr:
		begin-ptr, cast-immutable, each, fill-mut-arr, map!, make-mut-arr,
		mut-arr, reverse!, set-zero-elements, size, subscript, temp-as-arr, uninitialized-mut-arr

###
Mutable array type that can change in size.
This is represented as a reference to a `mut-arr` with extra capacity.
Appending (or removing) elements to the end is amortized O(1),
but appending (or removing) from the beginning is always O(N).
###
mut-list<?a> record mut
	# TODO:PRIVATE
	# backing.size is the capacity
	# For the GC's sake, unused parts of backing are filled with 'zeroed'
	backing mut mut-arr ?a
	# TODO:PRIVATE (getter public)
	size mut nat64

###
Pointer to the first element.
Since `mut-list` changes its underlying data occasionally,
it's UB if this is used after the size of `a` changes.
###
begin-ptr ptr ?a(a mut-list ?a) noctx unsafe
	a.backing begin-ptr

end-ptr ptr ?a(a mut-list ?a) noctx unsafe
	a.begin-ptr + a.size

is-at-capacity bool(a mut-list ?a) noctx unsafe
	a.capacity == a.size

capacity nat64(a mut-list ?a) noctx unsafe
	a.backing size

mut-list-by-val by-val mut-list<?a>() noctx trusted
	mut-list<?a>: mut-arr, 0

mut-list-by-val-with-capacity-from-unmanaged-memory by-val mut-list ?a(capacity nat64) noctx unsafe
	backing = mut-arr<?a>: capacity, capacity unmanaged-alloc-zeroed-elements<?a>
	mut-list<?a>: backing, 0

###
mut-list where every element is `value`.
Equivalent to `mut-list: [value, value, ...]`.
###
fill-mut-list mut-list ?a(size nat64, value ?a)
	backing = size fill-mut-arr value
	mut-list: backing, size

# New empty `mut-list`.
mut-list mut-list ?a()
	mut-list: mut-arr, 0

###
Copies an `arr` to a `mut-list`.
Writes to the `mut-list` won't affect the `arr`.
###
mut-list mut-list ?a(a ?a[])
	res = mut-list<?a>
	res ~= a
	res

# TODO: use an 'each' spec to combine these functions
###
Copies a `mut-arr` to a `mut-list`.
Writes to the `mut-list` won't affect the `mut-arr`.
###
mut-list mut-list ?a(a mut-arr ?a)
	res = mut-list<?a>
	res ~= a
	res

empty! void(a mut-list ?a)
	a pop-n! a.size

###
Copies the `mut-list`.
Writes to the original won't affect the copy.
###
copy mut-list ?a(a mut-list ?a) trusted
	a.temp-as-arr mut-list

###
Copies a `mut-list` to an `arr`.
Writes to the `mut-list` won't affect the `arr`.
###
arr ?a[](a mut-list ?a)
	a.copy move-to-arr!

###
Convert to a `dict`, clearning the original `mut-list` in the process.
(This avoids copying but still ensures that the result is immutable.)
###
move-to-arr! ?a[](a mut-list ?a) noctx trusted
	res = arr: a.size, a.begin-ptr
	a.backing := mut-arr
	a.size := 0
	res

swap! void(a mut-list ?a, b mut-list ?a)
	a-backing = a backing
	a-size = a size
	a.backing := b backing
	a.size := b size
	b.backing := a-backing
	b.size := a-size

###
Gets the element at the index.
Throws if `index >= a.size`.
###
subscript ?a(a mut-list ?a, index nat64) trusted
	assert: index < a.size
	a.begin-ptr[index]

first opt ?a(a mut-list ?a)
	if !a.is-empty
		a[0] some

last opt ?a(a mut-list ?a)
	if !a.is-empty
		a[a.size - 1] some

###
Sets the last element.
Throws if the list is empty.
###
set-last void(a mut-list ?a, value ?a)
	a[a.size - 1] := value

###
Converts to an `mut-arr`.
"Temp" because it's UB if the result is used after ths size of `a` changes.
###
temp-as-mut-arr mut-arr ?a(a mut-list ?a) noctx unsafe
	mut-arr: a.size, a.begin-ptr

###
Converts to an `arr`.
"Temp" because it's UB if the result is used after `a` is written to.
###
temp-as-arr ?a[](a mut-list ?a) noctx unsafe
	a.temp-as-mut-arr temp-as-arr

# True iff `a.size == 0`.
is-empty bool(a mut-list ?a) noctx
	a.size == 0

###
Sets the element at the index.
Throws if `index >= a.size`.
###
set-subscript void(a mut-list ?a, index nat64, value ?a) trusted
	assert: index < a.size
	a.begin-ptr[index] := value

###
True iff `a[i] == value` for some `i`.
###
in bool(value ?a, a mut-list ?a) noctx trusted equal<?a>
	value in a.temp-as-arr

copy-range-to-arr ?a[](a mut-list ?a, begin nat64, size nat64) trusted
	assert: begin + size <= a.size
	res = alloc-uninitialized<?a>: size
	src = a.begin-ptr + begin
	drop: memcpy: res.as-any-ptr, src.as-any-ptr, size * size-of<?a>
	arr: size, res

###
Removes the first `n` elements and returns them as an array.
This is O(a.size - n) because of the cost of copying, and all remaining elements must be shifted.
Throws if `n > a.size`.
###
pop-n-from-start! ?a[](a mut-list ?a, n nat64) trusted
	assert: n <= a.size
	res = a copy-range-to-arr 0, n
	dest = a.begin-ptr as-any-ptr
	src = a.begin-ptr + n as-any-ptr
	new-size = a.size - n
	drop: memmove: dest, src, new-size * size-of<?a>
	a.size := new-size
	res

###
Removes the last element and returns it.
Returns `none` if `a` is empty.
###
pop! opt ?a(a mut-list ?a) trusted
	if !a.is-empty
		new-size = a.size wrap-sub 1
		res = a[new-size]
		# Ensure GC doesn't hold on to the memory there
		a[new-size] := zeroed
		a.size := new-size
		res some

###
Pops `n` times, discarding the result.
Throws if `n > a.size`.
###
pop-n! void(a mut-list ?a, n nat64) trusted
	assert: n <= a.size
	new-size = a.size - n
	a.begin-ptr + new-size set-zero-range n
	a.size := new-size

# Push a value onto the end of the list.
~= void(a mut-list ?a, value ?a) trusted
	a incr-capacity!
	a.size < a.capacity assert
	a.begin-ptr[a size] := value
	a.size := a.size + 1

# TODO: Use an 'each' spec..
# Push multiple values onto the end of the list.
~= void(a mut-list ?a, values ?a[])
	values each x => a ~= x
~= void(a mut-list ?a, values mut-arr ?a)
	values each x => a ~= x
~= void(a mut-list ?a, values mut-list ?a)
	values each x => a ~= x

.incr-capacity! void(a mut-list ?a) unsafe
	a ensure-capacity: a.size + 1 round-up-to-power-of-two

push-capacity-must-be-sufficient! void(a mut-list ?a, value ?a) noctx unsafe
	hard-assert: a.size < a.capacity
	a.begin-ptr[a size] := value
	a.size := a.size wrap-add 1

.increase-capacity-to! void(a mut-list ?a, new-capacity nat64) unsafe
	assert: new-capacity > a.capacity
	old-begin = a begin-ptr
	a.backing := new-capacity uninitialized-mut-arr<?a>
	a.begin-ptr copy-data-from! old-begin, a.size
	a.backing[a.size + 1 -> a.backing.size] set-zero-elements

.ensure-capacity void(a mut-list ?a, min-capacity nat64) unsafe
	if a.capacity < min-capacity
		a increase-capacity-to! min-capacity

###
Ensure that the mut-list can grow to a size of `reserved` without reallocating.
Does nothing if the capacity is already sufficient.
This may reserve more space than requested, so calling `reserve` repeatedly for slowly increasing sizes
doesn't reallocate every time, and is no worse than pushing repeatedly.
###
.reserve void(a mut-list ?a, reserved nat64) unsafe
	a ensure-capacity reserved.round-up-to-power-of-two

###
Removes `a[index]`, replaces it with the last value, and returns the old `a[index]`.
If `a[index]` is the last value, simply pops it.
This is more efficient than `a remove-at! index`,
which has to shift all values past `index` to the left.
Throws if `index >= a.size`.
###
remove-unordered-at! ?a(a mut-list ?a, index nat64) trusted
	assert: index < a.size
	a noctx-remove-unordered-at! index

noctx-remove-unordered-at! ?a(a mut-list ?a, index nat64) noctx unsafe
	hard-assert: index < a.size
	res = a.begin-ptr[index]
	new-size = a.size wrap-sub 1
	a.begin-ptr[index] := a.begin-ptr[new-size]
	a.size := new-size
	res

noctx-must-remove-unordered! void(a mut-list ?a, value ?a) noctx unsafe equal<?a>
	a noctx-must-remove-unordered-recur! 0, value
.noctx-must-remove-unordered-recur! void(a mut-list ?a, index nat64, value ?a) noctx unsafe equal<?a>
	if index == a.size
		todo
	elif a.begin-ptr[index] == value
		a noctx-remove-unordered-at! index drop
	else
		a noctx-must-remove-unordered-recur! (index wrap-add 1), value

###
Directly sets the `size` without touching the data.
Unsafe because if this increases the size, it exposes uninitialized data.
If this decreases the size, it doesn't zero out the rest of the data, so the garbage collector will keep it alive.
###
unsafe-set-size! void(a mut-list ?a, new-size nat64) unsafe
	a reserve new-size
	a.size := new-size

###
Pops elements until the size is <= `new-size`.
Does nothing if `a.size <= new-size` already.
###
reduce-size-if-more-than! void(a mut-list ?a, new-size nat64)
	if new-size < a.size
		a.pop! drop
		a reduce-size-if-more-than! new-size

# TODO:MOVE
.round-up-to-power-of-two nat64(n nat64)
	1 round-up-to-power-of-two-recur n
.round-up-to-power-of-two-recur nat64(acc nat64, n nat64)
	if acc >= n
		acc
	else
		acc * 2 round-up-to-power-of-two-recur n

# Returns the first index where `a[i] == value`, or returns `none`.
index-of opt nat64(a mut-list ?a, value ?a) equal<?a> trusted
	a.temp-as-arr index-of value

###
Sets `a[index] := value`, but first shifts all elements from `index` onwards to the right to make room.
Increases the size by 1.
This is O(a.size - index) due to having to move other elements.
Throws if `index > a.size`.
###
insert-at! void(a mut-list ?a, index nat64, value ?a) trusted
	assert: index <= a.size
	a incr-capacity!
	dest = a.begin-ptr + index + 1
	src = a.begin-ptr + index
	n = a.size - index
	drop: memmove: dest.as-any-ptr, src.as-any-ptr, n * size-of<?a>
	a.size := a.size + 1
	a[index] := value
	assert: a.size <= a.capacity

insert-at! void(a mut-list ?a, ptr ptr ?a, value ?a) unsafe
	a insert-at! ptr - a.begin-ptr, value

###
Removes `a[index]`, and shifts all values from `index` onwards to the left to fill the hole.
Returns the old `a[index]`.
Decreases the size by 1.
This is O(a.size - index) due to having to move other elements.
Throws if `index >= a.size`.
###
remove-at! ?a(a mut-list ?a, index nat64) trusted
	assert: index < a.size
	res = a[index]
	dest = a.begin-ptr + index
	src = a.begin-ptr + index + 1
	n = a.size - index - 1
	drop: memmove: dest.as-any-ptr, src.as-any-ptr, n * size-of<?a>
	a.size := a.size - 1
	res

remove-at! ?a(a mut-list ?a, ptr ptr ?a) unsafe
	a remove-at! ptr - a.begin-ptr

reverse! void(a mut-list ?a) trusted
	a.temp-as-mut-arr reverse!

region iteration

# TODO: What if it's mutated while searching ?
find-index opt nat64(a mut-list ?a, f act bool(?a)) trusted
	a.temp-as-arr find-index f

# Calls `f` for each element in `a`.
each void(a mut-list ?a, f act void(?a)) trusted
	a.temp-as-arr each f

each-with-index void(a mut-list ?a, f act void(?a, nat64)) trusted
	a.temp-as-arr each-with-index f

exists bool(a mut-list ?a, f act bool(?a)) trusted
	a.temp-as-arr exists f

fold ?a(acc ?a, a mut-list ?b, f act ?a(?a, ?b)) trusted
	acc fold a.temp-as-arr, f

fold-reverse ?a(acc ?a, a mut-list ?b, f act ?a(?a, ?b)) trusted
	acc fold-reverse a.temp-as-arr, f

filter mut-list ?a(a mut-list ?a, f act bool(?a)) trusted
	res = a copy
	res filter! f
	res

map mut-list ?out(a mut-list ?in, f act ?out(?in)) trusted
	a map-to-mut-arr f as-mut-list

map-to-arr ?out[](a mut-list ?in, f act ?out(?in)) trusted
	a map-to-mut-arr f cast-immutable

map-to-mut-arr mut-arr ?out(a mut-list ?in, f act ?out(?in))
	a.size make-mut-arr i => f[a[i]]

map-opt mut-list ?out(a mut-list ?in, f act opt ?out(?in)) trusted
	a map-opt-to-mut-arr f as-mut-list

# TODO: this works on anything supporting 'fold' .. don't want to repeat for every collection
map-opt-to-mut-arr mut-arr ?out(a mut-list ?in, f act opt ?out(?in)) trusted
	out = a.size uninitialized-mut-arr<?out>
	end = out.begin-ptr fold a, (cur, x) =>
		if fx ?= f[x]
			cur[] := fx
			cur + 1
		else
			cur
	out[0 -> end - out.begin-ptr]

.as-mut-list mut-list ?a(a mut-arr ?a) unsafe
	mut-list: a, a.size

region mutating iteration

filter! void(a mut-list ?a, f act bool(?a)) trusted
	new-end = a.begin-ptr filter-recur! a.begin-ptr, a.end-ptr, f
	new-size = new-end - a.begin-ptr
	a.backing[new-size -> a.size] set-zero-elements
	a.size := new-size
.filter-recur! ptr ?a(out ptr ?a, in ptr ?a, end ptr ?a, f act bool(?a)) unsafe
	if in == end
		out
	else
		new-out = if f[in[]]
			out[] := in[]
			out + 1
		else
			out
		new-out filter-recur! in + 1, end, f

map! void(a mut-list ?a, f act ?a(?a)) trusted
	a.temp-as-mut-arr map! f
