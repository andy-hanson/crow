no-std
import
	../bool: bool
	../compare: !=, <
	../misc: new, void
	../number: ==, <=>, +, *, nat64, new
	../opt: ==, new, opt
	../ptr: ==, <=>, +, -, *, as-const, each-ptr, mut-ptr, null, set-deref, zeroed
	../range: ..
	../str: ==, literal, str
	../str-conv: to-str
	../test-util: is
	../writer: ~=, ~~=, backup, move-to-str, new, writer
	./arr: ==, arr, new, size
	./arr-util: first, last
	./fix-arr:
		begin-ptr, cast-immutable, copy-from, end-ptr, fix-arr, is-empty, new, set-zero-elements,
		size, subscript, uninitialized-fix-arr, zeroed-fix-arr
	./private/arr-low-level: arr-from-begin-end

# Mutable double-ended queue.
mut-deque<a> record mut
	.inner .mut fix-arr a
	# Points to the first element. Null for an empty queue.
	.left .mut a mut*
	# Points to the element after the last. Null for an empty queue.
	# This should never be inner.end-ptr; it wraps around to inner.begin-ptr
	.right .mut a mut*

# New empty `mut-deque`.
new<a> mut-deque a() trusted
	(), null, null

###
Copy elements to an array.

This is O(n).
###
to-arr<a> a[](a mut-deque a) trusted
	a.to-fix-arr cast-immutable

to-fix-arr<a> fix-arr a(a mut-deque a) no-doc trusted
	unless a is-empty
		res fix-arr a = a.size uninitialized-fix-arr
		res copy-from-deque a
		res

# Copy elements to a new deque.
copy<a> mut-deque a(a mut-deque a) trusted
	inner = a to-fix-arr
	inner, inner.begin-ptr, inner.begin-ptr

# `true` iff `a.size == 0`.
is-empty<a> bool(a mut-deque a) trusted
	a.left == null

# Number of elements in the deque.
size<a> nat64(a mut-deque a) trusted
	match a.left <=> a.right
	as less
		a.right - a.left
	as equal
		unless a.left == null
			a.inner size
	as greater
		(a.inner.end-ptr - a.left) + (a.right - a.inner.begin-ptr)

# Push a value to the left.
push<a> void(value a, a mut-deque a) trusted
	a ensure-capacity
	if a.left == a.inner.begin-ptr
		a.left := a.inner end-ptr
	a.left := a.left - 1
	*a.left := value

# Push a value to the right.
push<a> void(a mut-deque a, value a) trusted
	a ensure-capacity
	assert a.right < a.inner.end-ptr
	*a.right := value
	a.right := a.right + 1

###
Returns the leftmost element without removing it.

Returns an empty option iff `a` is empty.
###
peek-left<a> a?(a mut-deque a) trusted
	unless a.left == null
		*a.left,

###
Returns the rightmost element without removing it.

Returns an empty option iff `a` is empty.
###
peek-right<a> a?(a mut-deque a) trusted
	unless a.right == null
		fixed-right = if a.right == a.inner.begin-ptr
			a.inner end-ptr
		else
			a right
		*(fixed-right - 1),

###
Removes and returns the leftmost element.

Returns an empty option iff `a` was empty (before calling `pop-left`).
###
pop-left<a> a?(a mut-deque a) trusted
	unless a.left == null
		res = *a.left
		*a.left := zeroed
		a.left := a.left + 1
		if a.left == a.inner.end-ptr
			a.left := a.inner begin-ptr
		if a.left == a.right
			a.left := null
			a.right := null
			# Still hold on to `a inner`
		res,

###
Removes and returns the rightmost element.

Returns an empty option iff `a` was empty (before calling `pop-right`).
###
pop-right<a> a?(a mut-deque a) trusted
	unless a.right == null
		if a.right == a.inner.begin-ptr
			a.right := a.inner end-ptr
		a.right := a.right - 1
		res = *a.right
		*a.right := zeroed
		if a.right == a.left
			a.left := null
			a.right := null
			# Still hold on to `a inner`
		res,

# This temporarily breaks the invariant that a.left and a.right are null for an empty queue.
.ensure-capacity<a> void(a mut-deque a) unsafe
	if a.left == null
		assert a.right == null
		if a.inner is-empty
			a.inner := 2 zeroed-fix-arr
		a.left := a.inner begin-ptr
		a.right := a.left
	else
		assert a.right != null
		forbid a.inner is-empty
		if a.left == a.right
			old-inner = a inner
			old-capacity = old-inner size
			new-capacity = old-capacity * 2
			new-inner fix-arr a = new-capacity uninitialized-fix-arr
			new-inner[0 .. old-capacity] copy-from-deque a
			new-inner[old-capacity .. new-capacity] set-zero-elements
			a.inner := new-inner
			a.left := a.inner begin-ptr
			a.right := a.left + old-capacity

.copy-from-deque<a> void(dest fix-arr a, a mut-deque a) trusted
	assert dest.size == a.size
	if a.left < a.right
		dest copy-from (a.left.as-const arr-from-begin-end a.right.as-const)
	else
		left-to-end-size = a.inner.end-ptr - a.left
		dest[0 .. left-to-end-size] copy-from (a.left.as-const arr-from-begin-end a.inner.end-ptr.as-const)
		dest[left-to-end-size .. dest.size] copy-from (a.inner.begin-ptr.as-const arr-from-begin-end a.right.as-const)

.dbg str(a mut-deque nat64) trusted
	out writer = ()
	a.inner.begin-ptr.as-const arr-from-begin-end a.inner.end-ptr.as-const each-ptr ptr =>
		if ptr == a.right.as-const
			out ~~= "<-R "
		if ptr == a.left.as-const
			out ~~= "L-> "
		out ~~= *ptr to-str
		out ~= " "
	unless a.inner is-empty
		out backup
	out move-to-str

.verify void(a mut-deque nat64, arr nat64[], expected str)
	a.peek-left is arr.first
	a.peek-right is arr.last
	a.size is arr.size
	a.to-arr is arr
	a.dbg is expected

test
	a mut-deque nat64 = ()
	a.pop-left is ()
	a.pop-right is ()
	a verify (), ""
	1 push a
	a verify (1,), "<-R 0 L-> 1"
	a push 2
	a verify (1, 2), "2 <-R L-> 1"

	a-copy = a copy
	a-copy verify (1, 2), "<-R L-> 1 2"

	a.pop-right is (2,)
	a verify (1,), "<-R 0 L-> 1"

	a.pop-right is (1,)
	assert a.left == null
	assert a.right == null
	a verify (), "0 0"

	a push 1
	a verify (1,), "L-> 1 <-R 0"

	2 push a
	a verify (2, 1), "1 <-R L-> 2"

	3 push a
	a verify (3, 2, 1), "2 1 <-R 0 L-> 3"
