###
More functions on arrays are in the `crow/arr-util` module.
###

no-std
import
	../bool: &&, bool
	../compare: !=, >, >=, compare, comparison, equal, greater, less
	../fun-util: subscript
	../misc: new, void
	../private/alloc: alloc-uninitialized, copy-data-from
	../private/bootstrap: hard-assert, new
	../private/exception-low-level: throw-c-str
	../private/number-low-level: ==, <=>, nat8, nat64, wrap-add, wrap-sub
	../private/range-low-level: low, high, range
	../ptr: *, ==, +, as-const, const-ptr, mut-ptr, set-deref
	./private/arr-low-level: arr-from-begin-end, begin-ptr, end-ptr, unsafe-at
export
	../private/bootstrap: arr, size
	./private/arr-low-level: new

# `true` iff `a.size == 0`.
is-empty<a> bool(a a[]) noctx
	a.size == 0

###
Gets the element at the index.
Throws if `index >= a.size`.
###
subscript<a> a(a a[], index nat64) trusted
	if index >= a.size
		"index out of bounds" throw-c-str
	a unsafe-at index

###
Gets an array elements from 'range.low' (inclusive) to 'range.height' (exclusive).
This works in O(1) without copying any elements.
Throws if the slice would exceed the bounds of the arr.
###
subscript<a> a[](a a[], range range nat64) trusted
	if range.high > a.size
		"slice out of bounds" throw-c-str
	(range.high wrap-sub range.low), a.begin-ptr + range.low

###
Prepend an element to an array.
This is O(n).
###
~ <a> a[](a a, b a[])
	(a,) ~~ b

###
Append an element to an array.
This is O(n).
###
~ <a> a[](a a[], b a)
	a ~~ (b,)

###
Concatenates two arrays.
This is O(n).

If you need concatenate many arrays, use a `mut[]` to accumulate elements, then call `move-to-arr`.
###
~~ <a> a[](a a[], b a[]) trusted
	res-size = a.size wrap-add b.size
	res a mut* = res-size alloc-uninitialized
	res copy-data-from a.begin-ptr, a.size
	res + a.size copy-data-from b.begin-ptr, b.size
	res-size, res.as-const

# TODO: arr comparison should be generic
arr-equal<a> bool(a a[], b a[]) no-doc trusted noctx equal<a>
	a.begin-ptr equal-recur a.end-ptr, b.begin-ptr, b.end-ptr
.equal-recur<a> bool(a a*, a-end a*, b a*, b-end a*) unsafe noctx equal<a>
	if a == a-end
		b == b-end
	else
		b != b-end && *a == *b && a + 1 equal-recur a-end, b + 1, b-end

arr-equal<a> bool(a a[], b a[], eq act bool(a, a)) no-doc trusted
	a.begin-ptr equal-recur a.end-ptr, b.begin-ptr, b.end-ptr, eq
.equal-recur<a> bool(a a*, a-end a*, b a*, b-end a*, eq act bool(a, a)) unsafe
	if a == a-end
		b == b-end
	else
		b != b-end && eq[*a, *b] && a + 1 equal-recur a-end, b + 1, b-end, eq

arr-compare<a> comparison(a a[], b a[]) no-doc trusted noctx compare<a>
	a.begin-ptr compare-recur a.end-ptr, b.begin-ptr, b.end-ptr
.compare-recur<a> comparison(a a*, a-end a*, b a*, b-end a*) unsafe noctx compare<a>
	if a == a-end
		if b == b-end
			equal
		else
			less
	elif b == b-end
		greater
	else
		match *a <=> *b
		as less
			less
		as equal
			a + 1 compare-recur a-end, b + 1, b-end
		as greater
			greater

== bool(a nat8[], b nat8[]) no-doc noctx
	a arr-equal b
<=> comparison(a nat8[], b nat8[]) no-doc noctx
	a arr-compare b

== bool(a nat64[], b nat64[]) no-doc noctx
	a arr-equal b
<=> comparison(a nat64[], b nat64[]) no-doc noctx
	a arr-compare b

region 'for'

###
Equivalent to calling `f[a[0]]`, `f[a[1]]`, etc.

Calls `f` for each element in `a`.
###
for-loop<a> void(a a[], f act void(a)) trusted
	cur mut = a begin-ptr
	end = a end-ptr
	loop
		if cur == end
			break
		else
			f[*cur]
			cur := cur + 1
			continue

###
Equivalent to `f[a[0]], f[a[1]], ...`.
Array containing `f[x]` for each `x` in `a`.
###
for-loop<out, in> out[](a in[], f act out(in)) trusted
	in-cur mut = a begin-ptr
	in-end = a end-ptr
	out-begin out mut* = a.size alloc-uninitialized
	out-cur mut = out-begin
	loop
		if in-cur == in-end
			out-end = out-begin + a.size
			hard-assert: out-cur == out-end
			break out-begin.as-const arr-from-begin-end out-end.as-const
		else
			*out-cur := f[*in-cur]
			out-cur := out-cur + 1
			in-cur := in-cur + 1
			continue
