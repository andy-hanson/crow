no-std
import
	../bool: bool
	../misc: void
	../number: nat64
	../option: new, option
	./list: list
	./mut-list:
		~=, ~~=, copy, for-loop, is-empty, last, mut-list, new, pop, set-filter, size, to-list,
		to-mut-list

###
Mutable stack.

Elements are pushed and popped from the top of the stack.
Iteration functions move from the bottom to the top.
###
mut-stack<a> record mut
	.inner a mut[]

# New empty stack.
new<a> mut-stack a()
	(),

# Copy elements to a new stack.
copy<a> mut-stack a(a mut-stack a)
	a.inner.copy,

# Copy elements to a new stack.
to-mut-stack<a> mut-stack a(a a[])
	a.to-mut-list,

# Copy elements to a list.
to-list<a> a[](a mut-stack a)
	a.inner to-list

# True iff `a.size == 0`.
is-empty<a> bool(a mut-stack a)
	a.inner is-empty

###
Number of elements in the stack.

This is O(n).
###
size<a> nat64(a mut-stack a)
	a.inner size

# Pushes a value to the top of the stack.
~= <a> void(a mut-stack a, value a)
	a.inner ~= value

# Pushes many values to the top of the stack.
~~= <a> void(a mut-stack a, values a[])
	a.inner ~~= values

###
Returns the top element of the stack without removing it.

Returns an empty option iff `a` is empty.
###
peek<a> a?(a mut-stack a)
	unless a is-empty
		a.inner last

###
Removes and returns the top element of the stack.

Returns an empty option iff `a` is empty.
###
pop<a> a?(a mut-stack a)
	a.inner pop

region iteration

for-loop<a> void(a mut-stack a, f act void(a))
	a.inner for-loop f

###
Removes every element `x` where `!f[x]`.
This can be written as `a.filter := f`.
###
set-filter<a> void(a mut-stack a, f act bool(a))
	a.inner set-filter f
