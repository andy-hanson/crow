no-std
import
	../bool: !, &&, ||, bool
	../compare: >, >=, compare
	../fun-util: subscript
	../hash: hash, hash-mix
	../misc: is-data, new, void
	../number: ==, <=>, +, -, *, %, nat64
	../option: is-empty, new, option, option-equal
	../pointer: ==, +, *, mut-pointer, set-deref
	../string: ==, ~~, join, literal, string, to-string
	../test-util: is
	../tuple: from, new, pair, to
	./array: array
	./dict: dict, to-dict
	./mut-array:
		begin-pointer, end-pointer, fill, fill-mut-array, mut-array, for-loop, is-empty, new,
		set-subscript, size, subscript, to-array
	./list: ==, ~=, build-list, for-loop, list, list-equal, new, size, to-list, with-block
	./mut-list:
		~=, copy, find-index, for-loop, is-at-capacity, is-empty, mut-list, new,
		remove-unordered-at, set-filter, set-map, set-subscript, size, subscript

###
Mutable unordered dictionary.

Unordered means that iteration functions like `each` reach each entry in arbitrary order.
###
mut-dict<k, v> record mut
	.entries .mut mut-array entry<k, v>
	.total-size .mut nat64

.entry<k, v> union mut
	none
	one (k, v)
	many (k, v) mut[]

###
New dictionary containing the given pairs.

Later pairs overwrite earlier pairs with the same key.
###
new<k, v> v mut[k](...a array (k, v)) is-data<k> compare<k> hash-mix<k>
	a.to-list to-mut-dict

###
Convert a list of pairs to a dictionary.

Later pairs overwrite earlier pairs with the same key.
###
to-mut-dict<k, v> v mut[k](a (k, v)[]) is-data<k> compare<k> hash-mix<k>
	res v mut[k] = a.size mut-dict-with-capacity
	for pair : a
		res[pair from] := pair to
	res

###
New empty dictionary with space for about `capacity` entries.
This can have a small performance benefit if you know that much space will be needed.

Since hash conflicts are possible, this makes no guarantees about whether more allocations will be needed.
###
mut-dict-with-capacity<k, v> v mut[k](capacity nat64) is-data<k> compare<k> hash-mix<k>
	# TODO: should not need type annotation
	(capacity fill-mut-array none)::mut-array<entry<k, v>>, 0

# Copy pairs to a list.
to-list<k, v> (k, v)[](a v mut[k]) is-data<k> compare<k> hash-mix<k>
	for k, v : a
		k, v

# Copy pairs to a list and clear the dictionary.
move-to-list<k, v> (k, v)[](a v mut[k]) is-data<k> compare<k> hash-mix<k>
	res = a to-list
	a clear
	res

# Copy pairs to a new dictionary.
copy<k, v> v mut[k](a v mut[k]) is-data<k> compare<k> hash-mix<k>
	entries mut-array entry<k, v> = for entry : a entries
		match entry
		as none
			none
		as one ar
			ar one
		as many m
			m.copy many
	entries, a.total-size
test
	a string mut[nat64] = (1, "1"), (2, "2"), (5, "5")
	a verify "_ (1 -> 1) [2 -> 2, 5 -> 5]"

	b = a copy
	b verify "_ (1 -> 1) [2 -> 2, 5 -> 5]"

	a[6] := "6"
	a verify "(6 -> 6) (1 -> 1) [2 -> 2, 5 -> 5]"
	b verify "_ (1 -> 1) [2 -> 2, 5 -> 5]"

# `true` iff `a.size == 0`.
is-empty<k, v> bool(a v mut[k]) is-data<k> compare<k> hash-mix<k>
	a.size == 0

# Number of unique keys in the dictionary.
size<k, v> nat64(a v mut[k]) is-data<k> compare<k> hash-mix<k>
	a total-size

###
Gets the value associated with a key.

Returns an empty option if the key is not in the dictionary.
###
subscript<k, v> v?(a v mut[k], key k) trusted is-data<k> compare<k> hash-mix<k>
	unless a is-empty
		match a.entries[key.hash % a.entries.size]
		as none
			()
		as one ar
			ar.from == key ? ar.to,
		as many m
			if index ?= m find-index pair => pair.from == key
				m[index].to,

# `true` iff the key is in the dictinoary.
in<k, v> bool(key k, a v mut[k]) is-data<k> compare<k> hash-mix<k>
	!a[key].is-empty

###
Sets the key to the given value, so that `a[key]` will return `value`.

If the key is already in the dictionary, this overwrites the previous value.
###
set-subscript<k, v> void(a v mut[k], key k, value v) is-data<k> compare<k> hash-mix<k>
	_ = a update key, (value,)
	()

# Remove all keys, leaving the dictionary empty.
clear<k, v> void(a v mut[k]) is-data<k> compare<k> hash-mix<k>
	a.entries fill none
	a.total-size := 0

# Set the contents of `a` to the contents of `b` and vice versa.
swap<k, v> void(a v mut[k], b v mut[k]) is-data<k> compare<k> hash-mix<k>
	temp-entries = a.entries
	a.entries := b.entries
	b.entries := temp-entries
	temp-size = a.total-size
	a.total-size := b.total-size
	b.total-size := temp-size

###
If the key was in the dictionary, remove it and return the old value.

Returns an empty option if the key was not in the dictionary.
###
try-remove<k, v> v?(a v mut[k], key k) is-data<k> compare<k> hash-mix<k>
	a update key, ()

###
f: Takes the value (if it exists), returns the new value (or none to remove).
Returns the old value (if it exists).
###
.update<k, v> v?(a v mut[k], key k, new-value v?) trusted is-data<k> compare<k> hash-mix<k>
	if a.entries is-empty
		if value ?= new-value
			a.entries := (key, value).one,
			a.total-size := 1
		()
	else
		entry-index = key.hash % a.entries.size
		match a.entries[entry-index]
		as none
			if value ?= new-value
				a.entries[entry-index] := (key, value) one
				a.total-size := a.total-size + 1
			()
		as one ar
			if ar.from == key
				a.entries[entry-index] := if value ?= new-value
					(key, value) one
				else
					a.total-size := a.total-size - 1
					none
				ar.to,
			else
				if value ?= new-value
					if a should-expand
						a expand
						a[key] := value
					else
						a.entries[entry-index] := (ar, (key, value)) many
						a.total-size := a.total-size + 1
				()
		as many m
			if index ?= m find-index pair => pair.from == key
				old-value = m[index] to
				if value ?= new-value
					m[index] := key, value
				else
					_ = m remove-unordered-at index
					if m is-empty
						a.entries[entry-index] := none
					elif m.size == 1
						# TODO: should not need a local
						z = m[0]
						a.entries[entry-index] := z one
					a.total-size := a.total-size - 1
				old-value,
			else
				if value ?= new-value
					if m.is-at-capacity && a.should-expand
						a expand
						a[key] := value
					else
						m ~= (key, value)
						a.total-size := a.total-size + 1
				()

# This is the same as calling `a[b a] := b b`.
~= <k, v> void(a v mut[k], b (k, v)) is-data<k> compare<k> hash-mix<k>
	a[b from] := b to

# This is the same as calling `a ~= x` for every `x` in `b`.
~~= <k, v> void(a v mut[k], b (k, v)[]) is-data<k> compare<k> hash-mix<k>
	for pair : b
		a ~= pair

# Called when we're about to allocate a bigger bucket -- maybe we should expand the table instead
.should-expand<k, v> bool(a v mut[k])
	a.total-size >= a.entries.size

.expand<k, v> void(a v mut[k]) is-data<k> compare<k> hash-mix<k>
	forbid a.entries.size == 0
	new-size = a.entries.size * 2
	bigger v mut[k] = new-size mut-dict-with-capacity
	for key, value : a
		bigger[key] := value
	a swap bigger
	assert a.entries.size == new-size

region iteration

# Calls `f` for every key and value in the dictionary.
for-loop<k, v> void(a v mut[k], f act void(k, v)) is-data<k> compare<k> hash-mix<k>
	for entry : a entries
		match entry
		as none
			()
		as one pair
			f[pair from, pair to]
		as many m
			for pair : m
				f[pair from, pair to]

###
Calls `f` on every key and value and returns a list of the results.
###
for-loop<out, k, v> out[](a v mut[k], f act out(k, v)) trusted is-data<k> compare<k> hash-mix<k>
	with out : a.size build-list
		for key, value : a
			out ~= f[key, value]

###
Filters the dictionary in place.

Calls `f` on every key and value and removes the key if `f` returns false.
###
set-filter<k, v> void(a v mut[k], f act bool(k, v)) trusted is-data<k> compare<k> hash-mix<k>
	a.entries.begin-pointer set-filter-recur a.entries.end-pointer, f
.set-filter-recur<k, v> void(entry-pointer entry<k, v> mut*, entry-end entry<k, v> mut*, f act bool(k, v)) unsafe
	unless entry-pointer == entry-end
		match *entry-pointer
		as none
			()
		as one ar
			unless f[ar.from, ar.to]
				*entry-pointer := none
		as many m
			m set-filter pair => f[pair.from, pair.to]
			if m is-empty
				*entry-pointer := none
			elif m.size == 1
				# TODO: should not need a local
				z = m[0]
				*entry-pointer := z one
		entry-pointer + 1 set-filter-recur entry-end, f

###
Returns an immutable dictionary with this dictionary's contents.
In the process, clears the mutable dictionary.
###
move-to-dict<k, v> v[k](a v mut[k]) is-data<k> compare<k> hash-mix<k>
	res = (for k, v : a; k, v)::(k, v)[] to-dict
	a clear
	res

###
Calls `f` on every key and value, and replaces the value with the new value returned by `f`.
###
set-map-values<k, v> void(a v mut[k], f act v(k, v)) trusted is-data<k> compare<k> hash-mix<k>
	a.entries.begin-pointer set-map-values-recur a.entries.end-pointer, f
.set-map-values-recur<k, v> void(entry-pointer entry<k, v> mut*, entry-end entry<k, v> mut*, f act v(k, v)) unsafe
	unless entry-pointer == entry-end
		match *entry-pointer
		as none
			()
		as one ar
			*entry-pointer := (ar.from, f[ar.from, ar.to]) one
		as many m
			m set-map pair => pair.from, f[pair.from, pair.to]
		entry-pointer + 1 set-map-values-recur entry-end, f

.dbg<k, v> string(a v mut[k]) to-string<k> to-string<v>
	" " join for entry : a.entries.to-array.to-list
		match entry
		as none
			"_"
		as one ar
			"({ar dbg})"
		as many m
			"[{", " join for pair : m; pair dbg}]"
.dbg<k, v> string(a (k, v)) to-string<k> to-string<v>
	"{a from} -> {a to}"

.verify void(a string mut[nat64], expected string)
	a.dbg is expected

test
	a string mut[nat64] = ()

	a[6] := "6"
	a[5] is ()
	a[6] is ("6",)
	a.size is 1
	# TODO: a.arr is ((6, "6"),)
	(for pair : a to-list; pair from)::nat64[] is (6,)
	(for pair : a to-list; pair to)::string[] is ("6",)

	a verify "(6 -> 6)"

	a[6] := "six"
	a verify "(6 -> six)"

	a[3] := "3"
	a verify "(6 -> six) (3 -> 3)"

	a[4] := "4"
	a verify "(4 -> 4) _ (6 -> six) (3 -> 3)"

	a[8] := "8"
	a verify "[4 -> 4, 8 -> 8] _ (6 -> six) (3 -> 3)"

	a[4] is ("4",)
	a[8] is ("8",)
	a[5] is ()

	# TODO: a.to-list is ((4, "4"), (8, "8"), (6, "six"), (3, "3"))
	(for pair : a to-list; pair from)::nat64[] is (4, 8, 6, 3)
	(for pair : a to-list; pair to)::string[] is ("4", "8", "six", "3")

	a[12] := "12"
	a verify "(8 -> 8) _ _ (3 -> 3) [4 -> 4, 12 -> 12] _ (6 -> six) _"

	a try-remove 5 is ()
	a try-remove 4 is ("4",)

	a verify "(8 -> 8) _ _ (3 -> 3) (12 -> 12) _ (6 -> six) _"

	a[11] := "11"
	a verify "(8 -> 8) _ _ [3 -> 3, 11 -> 11] (12 -> 12) _ (6 -> six) _"

	a set-filter (key, value) => key > 10 || value == "six"
	a verify "_ _ _ (11 -> 11) (12 -> 12) _ (6 -> six) _"

	a set-map-values (key, value) => "{key}-{value}"
	a verify "_ _ _ (11 -> 11-11) (12 -> 12-12) _ (6 -> 6-six) _"

.== bool(a string?, b string?)
	a option-equal b

.== bool(a string[], b string[])
	a list-equal b
