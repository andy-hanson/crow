no-std
import
	string: memcpy
	../bool: &&, bool, false, true
	../compare: !=, <, <=, equal
	../fun-util: subscript
	../misc: new, void
	../number: ==, <=>, +, -, *, /, %, nat64
	../option: new, option
	../pointer:
		==, <=>, +, -, *, as-any-const-pointer, as-any-mut-pointer, as-const, as-mut, mut-pointer, pointer-cast,
		set-deref, set-subscript, size-of, subscript, swap
	../private/alloc: alloc-uninitialized, set-zero-range, unmanaged-alloc-elements, unmanaged-free-elements
	../private/range-low-level: .., range
	./array: array, exists, find-index, fold, for-loop, index-of, new, size, subscript
	./private/array-low-level: array-from-begin-end, begin-pointer, subscript

###
`mut-array` is a low-level type mostly useful for implementing other collections.
For normal code, a `mut[]` is preferable.

A `mut-array` is like a `mut[]`, but while a `mut[]` has capacity for pushing elements, `mut-array` is fixed-size.
This has a small performance benefit, since (like an immutable array)
a `mut-array` is just a pointer and size which can be stored by value.

You can't change what range of addresses a `mut-array` points to, though you can write to its elements.

Like an immutable array, and unlike a `mut[]`, it's cheap to take a slice of a `mut-array`.
Taking a slice aliases the elements, so writing to the slice writes to the original.
###
mut-array<a> record mut by-val
	.inner array a

# TODO:INTERNAL (or mut-array-low-level)
empty-mut-array<a> mut-array a() no-doc noctx
	# TODO: should not need type annotation
	inner array a = ()
	inner,

###
Creates a new `mut-array` with the given elements.
###
new<a> mut-array a(...a array a)
	a to-mut-array

###
Copies the array elements to a new `mut-array`.

This is O(n).
###
to-mut-array<a> mut-array a(a array a)
	a.size make-mut-array i => a[i]

###
Copies the elements to an immutable array.

This is O(n).

There is no `move-to-array` for `mut-array` since there might be slices that still refer to its elements.
###
to-array<a> array a(a mut-array a)
	a.copy.inner

# Get a `mut-array` from a poitner and range of indices.
subscript<a> mut-array a(a a mut*, range range nat64) noctx unsafe
	a.as-const[range],

# Creates a `mut-array` from a range of pointers.
mut-array-from-begin-end<a> mut-array a(begin a mut*, end a mut*) no-doc unsafe noctx
	assert begin <= end
	(begin.as-const array-from-begin-end end.as-const),

###
Gets the element at the index.
Throws if `index >= a.size`.
###
subscript<a> a(a mut-array a, index nat64) noctx trusted
	assert index < a.size
	a.begin-pointer[index]

###
Slices the mut-array.
This is O(1).

Slices alias the original: Writes to the slice will affect the original.
(If you don't want that, slice then use `copy`.)

Throws if the slice would be out of bounds.
###
subscript<a> mut-array a(a mut-array a, range range nat64)
	a.inner[range],

###
Write to a single element.
Throws if `index >= a.size`.
###
set-subscript<a> void(a mut-array a, index nat64, value a) noctx trusted
	assert index < a.size
	a.begin-pointer[index] := value

# `true` iff `a.size == 0`.
is-empty<a> bool(a mut-array a) noctx
	a.size == 0

# Number of elements.
size<a> nat64(a mut-array a) noctx
	a.inner size

# Pointer to the beginning of the `mut-array`.
begin-pointer<a> a mut*(a mut-array a) noctx unsafe
	a.inner.begin-pointer as-mut

###
Pointer to the end of the `mut-array`.
This is one past the last valid pointer.
###
end-pointer<a> a mut*(a mut-array a) noctx unsafe
	a.begin-pointer + a.size

###
Copies the elements to a new `mut-array`.

This is O(n).
###
copy<a> mut-array a(a mut-array a)
	for x : a
		x

###
Copies elements from `source` to `dest`.

Throws if `dest` and `source` are not the same size.
(Hint: Both `dest` and `source` can be slices of bigger arrays.)

This is O(n).
###
copy-from<a> void(dest mut-array a, source array a) trusted
	assert dest.size == source.size
	_ = dest.begin-pointer.as-any-mut-pointer memcpy source.begin-pointer.as-any-const-pointer, dest.size * size-of@<a>
	()

###
Copies elements from `source` to `dest`.

This is O(n).
###
copy-from<a> void(dest mut-array a, source mut-array a) trusted
	dest copy-from source.temp-as-array

###
Allocates a `mut-array` without initializing any of its elements.

Since `mut-array` is garbage-collected, this will cause problems unless either:
* You write to all the elements before the current task ends.
* You don't keep a reference to the `mut-array` or to any uninitialized slice.

Keep exceptions in mind: Don't store a reference to the result until it's initialized,
since initialization code might throw an exception.

See `zeroed-mut-array` for a safer alternative, or `make-mut-array` for a fully safe alternative.
###
uninitialized-mut-array<a> mut-array a(size nat64) unsafe
	pointer a mut* = size alloc-uninitialized
	pointer[0 .. size]

unmanaged-uninitialized-mut-array<a> mut-array a(size nat64) no-doc noctx unsafe
	pointer a mut* = size unmanaged-alloc-elements
	pointer[0 .. size]

free-unmanaged-mut-array<a> void(a mut-array a) no-doc noctx unsafe
	a.begin-pointer unmanaged-free-elements a.size

###
This is like `uninitialized-mut-array` but fills memory with zeroes.

Zeroed memory is GC-safe, since the GC ignores null pointers,
but is still not safe to expose to readers (who may try to dereference the null pointers).

This is used by collections like `mut[]` that need some excess capacity that will be GC-safe.
###
zeroed-mut-array<a> mut-array a(size nat64) unsafe
	res mut-array a = size uninitialized-mut-array
	res set-zero-elements
	res

###
Sets every element to `zeroed`. See comment on `zeroed-mut-array` for why this is useful.
###
set-zero-elements<a> void(a mut-array a) noctx unsafe
	a.begin-pointer set-zero-range a.size

###
`mut-array` where each element is `f[i]`.
Equivalent to `f[0], f[1], ...`.
###
.make-mut-array<a> mut-array a(size nat64, f act a(nat64)) trusted
	res mut-array a = size uninitialized-mut-array
	out mut = res begin-pointer
	i mut = 0
	loop
		if i == size
			assert out == res.end-pointer
			break res
		else
			*out := f[i]
			out := out + 1
			i := i + 1
			continue

###
`mut-array` where every element is `value`.
Equivalent to `value, value, ...`.
###
fill-mut-array<a> mut-array a(size nat64, value a)
	size make-mut-array _ => value

# Sets every element to `value`.
fill<a> void(a mut-array a, value a) noctx trusted
	unless a is-empty
		*a.begin-pointer := value
		a.begin-pointer + 1 mut-array-from-begin-end a.end-pointer fill value

in<a> bool(value a, a mut-array a) noctx trusted equal<a>
	cur mut = a.begin-pointer as-const
	end = a.end-pointer as-const
	loop
		if cur == end
			break false
		elif *cur == value
			break true
		else
			cur := cur + 1
			continue

###
Unsafe cast a mut-array to arr.
Equivalent to `temp-as-array`, but the implication is that
the `mut-array` will never be written to again, so the `arr` can be used forever.
###
cast-immutable<a> array a(a mut-array a) noctx unsafe
	a inner

first<a> a?(a mut-array a)
	unless a is-empty
		a[0],

last<a> a?(a mut-array a)
	unless a is-empty
		a[0],

tail<a> mut-array a(a mut-array a)
	forbid a is-empty
	a[1 .. a.size]

rtail<a> mut-array a(a mut-array a)
	forbid a is-empty
	a[0 .. a.size - 1]

###
Unsafe cast to immutable array.

This is O(1).

Unsafe because an array is expected to be fully immutable (meaning: no mutable references exist),
so this value should only be used temporarily.
###
temp-as-array<a> array a(a mut-array a) noctx unsafe
	a inner

# Equivalent to `a.temp-as-array index-of value`.
index-of<a> nat64?(a mut-array a, value a) trusted noctx equal<a>
	a.temp-as-array index-of value

# Equivalent to `a.temp-as-array find-index f`.
find-index<a> nat64?(a mut-array a, f act bool(a)) trusted
	a.temp-as-array find-index f

set-reverse<a> void(a mut-array a) trusted
	# avoid 'a.end-pointer - 1' overflowing for null arrays
	unless a is-empty
		a.begin-pointer set-reverse-recur a.end-pointer - 1
.set-reverse-recur<a> void(left a mut*, right a mut*) unsafe
	if left < right
		left swap right
		left + 1 set-reverse-recur right - 1

region iteration

exists<a> bool(a mut-array a, f act bool(a)) trusted
	a.temp-as-array exists f

# Calls 'f' on each element in the mut-array.
for-loop<a> void(a mut-array a, f act void(a)) trusted
	a.temp-as-array for-loop f

for-loop<a> void(a mut-array a, f act void(a, nat64)) trusted
	a.temp-as-array for-loop f

fold<a, b> a(acc a, a mut-array b, f act a(a, b)) trusted
	acc fold a.temp-as-array, f

set-map<a> void(a mut-array a, f act a(a)) trusted
	a.begin-pointer set-map-recur a.end-pointer, f
.set-map-recur<a> void(cur a mut*, end a mut*, f act a(a)) unsafe
	unless cur == end
		*cur := f[*cur]
		cur + 1 set-map-recur end, f

for-loop<out, in> mut-array out(a mut-array in, f act out(in)) trusted
	a.size make-mut-array i => f[a[i]]

for-loop<out, in> mut-array out(a mut-array in, f act out(in, nat64)) trusted
	a.size make-mut-array i => f[a[i], i]

###
Casts elements to a different type.
If the element type is different, adjusts the size so that the number of bytes is the same.
###
cast-mut-array<out, in> mut-array out(a mut-array in) noctx unsafe
	assert size-of@<in> != 0 && size-of@<out> != 0
	size-bytes = a.size * size-of@<in>
	assert size-bytes % size-of@<out> == 0
	size-out = size-bytes / size-of@<out>
	a.begin-pointer.pointer-cast@<out, in>[0 .. size-out]

for-loop<out, in> array out(a mut-array in, f act out(in)) trusted
	a for-loop f cast-immutable
