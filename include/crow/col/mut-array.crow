no-std
import
	../bool: &&, bool, false, true
	../compare: !=, <, <=, equal
	../fun-util: subscript
	../misc: void
	../number: ==, <=>, +, -, *, /, %, nat64
	../option: new, option
	../pointer:
		==, <=>, +, *, as-const, as-mut, mem-clear, mem-move, mut-pointer, pointer-cast,
		set-subscript, size-of, subscript
	../private/alloc: allocate
	../private/bootstrap: new-array, new-mut-array, mut-array-pointer, mut-array-size, todo
	../range: .., high, low, range, size
	../tuple: new
	./array:
		~=, ~~=, +, array, array-builder, as-array, begin, begin-pointer, build, iterate, new, next,
		size, slice, subscript
	./private/array-low-level: array-reference-equal
	./collection: build-options, for-loop, range-relative-to-end, set-subscript, subscript
export
	../private/bootstrap: mut-array

# True if they are identical.
+==[t] bool(a t mut-array, b t mut-array) bare, trusted
	a.cast-immutable array-reference-equal b.cast-immutable

empty-mut-array[t] t mut-array() bare, trusted
	() cast-mutable

###
Creates a new `mut-array` with the given elements.
###
+new[t] t mut-array(...a t array)
	a to

# Get a `mut-array` from a pointer and range of indices.
+subscript[t] t mut-array(a t mut*, indices nat64 range) bare, unsafe, native extern
	a.as-const[indices] cast-mutable

###
Copies the array elements to a new `mut-array`.

This is O(n).
###
+to[t] t mut-array(a t array) trusted
	# Not using a 'for' loop because the compiler may think it may yield (due to the virtual call)
	res t mut-array = a.size zeroed-mut-array
	i mut nat64 = 0
	while i < a.size
		res[i] := a[i]
		i +:= 1
	res

###
Copies the elements to an immutable array.

This is O(n).

There is no `move-to` for `mut-array` since there might be slices that still refer to its elements.
###
+to[t] t array(a t mut-array) trusted
	a.copy cast-immutable

# Creates a `mut-array` from a range of pointers.
+as-mut-array[t] t mut-array(a t mut* range) bare, unsafe, native extern
	assert a.low <= a.high
	(a.low .. a.high).as-array cast-mutable

+pointers[t] t mut* range(a t mut-array) bare, unsafe, native extern
	a.begin-pointer .. a.end-pointer

# Number of elements.
+size[t] nat64(a t mut-array) bare
	a mut-array-size

# `true` iff `a.size == 0`.
+is-empty[t] bool(a t mut-array) bare
	a.size == 0

# Pointer to the beginning of the `mut-array`.
+begin-pointer[t] t mut*(a t mut-array) bare, unsafe, native extern
	a mut-array-pointer

###
Pointer to the end of the `mut-array`.
This is one past the last valid pointer.
###
+end-pointer[t] t mut*(a t mut-array) bare, unsafe, native extern
	a.begin-pointer + a.size

###
Copies the elements to a new `mut-array`.

This is O(n).
###
+copy[t] t mut-array(a t mut-array)
	for x : a; x

###
Copies elements from `source` to `dest`.
Throws if `dest` and `source` are not the same size.
###
set-subscript[t] void(dest t mut-array, range nat64 range, source t array)
	assert range.size == source.size
	dest[range] copy-from source
set-subscript[t] void(dest t mut-array, range nat64 range, source t mut-array)
	dest[range] := trusted source.temp-as-array
set-subscript[t] void(dest t mut-array, range range-relative-to-end, source t array)
	dest[range] copy-from source
set-subscript[t] void(dest t mut-array, range range-relative-to-end, source t mut-array)
	dest[range] := trusted source.temp-as-array

-copy-from[t] void(dest t mut-array, source t array)
	assert dest.size == source.size
	trusted if extern native
		dest.begin-pointer mem-move source.begin-pointer, dest.size
	else
		todo # --------------------------------------------------------------------------------------------------------------

###
Allocates a `mut-array` full of zeroes.
If you just want to create a `mut-array`, use a `for` loop instead.

Zero values are not in general safe, but are GC-safe, since the GC ignores null pointers.

Keep exceptions in mind: Don't store a reference to the result until it's initialized,
since initialization code might throw an exception.

This is used by collections like `mut[]` that need some excess capacity that will be GC-safe.
###
+zeroed-mut-array[t] t mut-array(size nat64) unsafe
	if extern native
		(size.allocate::t mut*)[0 .. size]
	else
		todo # ------------------------------------------------------------------------------------------------------------

###
Sets every element to `zeroed`.
See `zeroed-mut-array` for why this is unsafe.
###
+zero-memory[t] void(a t mut-array) bare, unsafe
	if extern native
		a.begin-pointer mem-clear a.size
	else
		todo # ---------------------------------------------------------------------------------------------------------------

+in[t] bool(value t, a t mut-array) bare, t equal, trusted
	if extern native
		cur mut = a.begin-pointer as-const
		end = a.end-pointer as-const
		loop
			if cur == end
				break false
			elif *cur == value
				break true
			else
				cur +:= 1
				continue
	else
		todo # -----------------------------------------------------------------------------------------------------------

###
Unsafe cast a mut-array to an array.
It must never be written to again.
###
+cast-immutable[t] t array(a t mut-array) bare, unsafe
	if extern native
		a.size new-array a.begin-pointer.as-const
	else
		todo # js-cast ------------------------------------------------------------------------------------------------------------

~cast-mutable[t] t mut-array(a t array) bare, unsafe
	if extern native
		a.size new-mut-array a.begin-pointer.as-mut
	else
		todo # js-cast ---------------------------------------------------------------------------------------------------------------

###
Unsafe cast to immutable array.

This is O(1).

Unsafe because an array is expected to be fully immutable (meaning: no mutable references exist),
so this value must only be used temporarily.
###
+temp-as-array[t] t array(a t mut-array) bare, unsafe
	a cast-immutable

###
Casts elements to a different type.
If the element type is different, adjusts the size so that the number of bytes is the same.
###
+cast-mut-array[out, in] out mut-array(a in mut-array) bare, unsafe, native extern
	assert size-of@in != 0 && size-of@out != 0
	size-bytes = a.size * size-of@in
	assert size-bytes % size-of@out == 0
	size-out = size-bytes / size-of@out
	# TODO: shouldn't need parentheses
	(a.begin-pointer.pointer-cast::out mut*)[0 .. size-out]

region spec impls (see collection.crow)

# Calls 'f' on each element in the mut-array.
+iterate[t] bool(a t mut-array, f bool mut(elem t))
	trusted a.temp-as-array iterate f

+mut-array-iterator[t] record by-val, mut
	~collection t mut-array
	~next-index nat64

+begin[t] t mut-array-iterator(a t mut-array) bare
	a, 0

+end[t] t mut-array-iterator(a t mut-array) bare
	a, a.size

+ +[t] t mut-array-iterator(a t mut-array-iterator, b nat64) bare
	new-index = a.next-index + b
	assert new-index <= a.collection.size
	a.collection, new-index

+-[t] t mut-array-iterator(a t mut-array-iterator, b nat64) bare
	a.collection, a.next-index - b

+-[t] nat64(a t mut-array-iterator, b t mut-array-iterator) bare
	assert a.collection == b.collection
	a.next-index - b.next-index

+prev[t] (t mut-array-iterator, t)?(a t mut-array-iterator) bare
	guard a.next-index != 0
	assert a.next-index <= a.collection.size
	value = trusted if extern native
		a.collection.begin-pointer[a.next-index - 1]
	else
		todo # -----------------------------------------------------------------------------------------------------------
	((a.collection, a.next-index - 1), value),

+next[t] (t, t mut-array-iterator)?(a t mut-array-iterator) bare
	guard a.next-index < a.collection.size
	value = trusted if extern native
		a.collection.begin-pointer[a next-index]
	else
		todo # -----------------------------------------------------------------------------------------------------------
	(value, (a.collection, a.next-index + 1)),

+set-prev[t] void(a t mut-array-iterator, value t) bare
	assert a.next-index <= a.collection.size
	trusted if extern native
		a.collection.begin-pointer[a.next-index - 1] := value
	else
		todo #-----------------------------------------------------------------------------------------------------------------

+set-next[t] void(a t mut-array-iterator, value t) bare
	assert a.next-index < a.collection.size
	trusted if extern native
		a.collection.begin-pointer[a next-index] := value
	else
		todo # --------------------------------------------------------------------------------------------------------------

+slice[t] t mut-array(begin t mut-array-iterator, end t mut-array-iterator) bare, trusted
	assert begin.collection == end.collection
	inner-iter = begin.collection.cast-immutable begin
	inner-iter + begin.next-index slice inner-iter + end.next-index cast-mutable

+mut-array-builder[t] record mut
	-inner t array-builder
+build[t] t mut-array(a build-options, f void mut(out t mut-array-builder)) trusted
	(a build out => f[out,]) cast-mutable
+~=[t] void(a t mut-array-builder, value t)
	a.inner ~= value
+~~=[t] void(a t mut-array-builder, values t array)
	a.inner ~~= values
+size[t] nat64(a t mut-array-builder)
	a.inner size
