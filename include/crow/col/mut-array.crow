no-std
import
	string: memcpy
	../bool: &&, bool, false, true
	../compare: !=, <, <=, equal
	../fun-util: subscript
	../misc: new, void
	../number: ==, <=>, +, -, *, /, %, nat64
	../option: new, option
	../pointer:
		==, <=>, +, *, as-any-const-pointer, as-any-mut-pointer, as-const, as-mut, mut-pointer,
		pointer-cast, set-deref, set-subscript, size-of, subscript
	../private/alloc:
		alloc-uninitialized, set-zero-range, unmanaged-alloc-elements, unmanaged-free-elements
	../range: .., high, low, range
	../tuple: new, pair
	./array:
		~=, ~~=, +, array, array-builder, as-array, begin, begin-pointer, build, iterate, new, next,
		size, slice, subscript
	./private/array-low-level: array-reference-equal
	./collection: build-options, for-loop, subscript

###
`mut-array` is a low-level type mostly useful for implementing other collections.
For normal code, a `mut[]` is preferable.

A `mut-array` is like a `mut[]`, but while a `mut[]` has capacity for pushing elements, `mut-array` is fixed-size.
This has a small performance benefit, since (like an immutable array)
a `mut-array` is just a pointer and size which can be stored by value.

You can't change what range of addresses a `mut-array` points to, though you can write to its elements.

Like an immutable array, and unlike a `mut[]`, it's cheap to take a slice of a `mut-array`.
Taking a slice aliases the elements, so writing to the slice writes to the original.
###
!mut-array[t] record mut by-val
	.inner t array

# True if they are identical.
!==[t] bool(a t mut-array, b t mut-array) noctx
	trusted a.inner array-reference-equal b.inner

empty-mut-array[t] t mut-array() noctx
	# TODO: should not need type annotation
	inner t array = ()
	inner,

###
Creates a new `mut-array` with the given elements.
###
!new[t] t mut-array(...a t array)
	a to-mut-array

# Get a `mut-array` from a pointer and range of indices.
!subscript[t] t mut-array(a t mut*, indices nat64 range) noctx, unsafe
	a.as-const[indices],

###
Copies the array elements to a new `mut-array`.

This is O(n).
###
!to-mut-array[t] t mut-array(a t array)
	a.size make i => a[i]

###
Copies the elements to an immutable array.

This is O(n).

There is no `move-to-array` for `mut-array` since there might be slices that still refer to its elements.
###
!to-array[t] t array(a t mut-array)
	a.copy.inner

# Creates a `mut-array` from a range of pointers.
!as-mut-array[t] t mut-array(a t mut* range) noctx, unsafe
	assert a.low <= a.high
	(a.low .. a.high).as-array,

!pointers[t] t mut* range(a t mut-array) noctx, unsafe
	a.begin-pointer .. a.end-pointer

# `true` iff `a.size == 0`.
!is-empty[t] bool(a t mut-array) noctx
	a.size == 0

# Pointer to the beginning of the `mut-array`.
!begin-pointer[t] t mut*(a t mut-array) noctx, unsafe
	a.inner.begin-pointer as-mut

###
Pointer to the end of the `mut-array`.
This is one past the last valid pointer.
###
!end-pointer[t] t mut*(a t mut-array) noctx, unsafe
	a.begin-pointer + a.size

###
Copies the elements to a new `mut-array`.

This is O(n).
###
!copy[t] t mut-array(a t mut-array)
	for x : a; x

###
Copies elements from `source` to `dest`.

Throws if `dest` and `source` are not the same size.
(Hint: Both `dest` and `source` can be slices of bigger arrays.)

This is O(n).
###
!copy-from[t] void(dest t mut-array, source t array)
	assert dest.size == source.size
	trusted
		dest-ptr = dest.begin-pointer as-any-mut-pointer
		src-ptr = source.begin-pointer as-any-const-pointer
		_ = dest-ptr memcpy src-ptr, dest.size * size-of@t
		()

###
Copies elements from `source` to `dest`.

This is O(n).
###
!copy-from[t] void(dest t mut-array, source t mut-array)
	dest copy-from trusted source.temp-as-array

###
Allocates a `mut-array` without initializing any of its elements.

Since `mut-array` is garbage-collected, this will cause problems unless either:
* You write to all the elements before the current task ends.
* You don't keep a reference to the `mut-array` or to any uninitialized slice.

Keep exceptions in mind: Don't store a reference to the result until it's initialized,
since initialization code might throw an exception.

See `zeroed-mut-array` for a safer alternative, or `make` for a fully safe alternative.
###
uninitialized-mut-array[t] t mut-array(size nat64) unsafe
	pointer t mut* = size alloc-uninitialized
	pointer[0 .. size]

unmanaged-uninitialized-mut-array[t] t mut-array(size nat64) noctx, unsafe
	pointer t mut* = size unmanaged-alloc-elements
	pointer[0 .. size]

free-unmanaged-mut-array[t] void(a t mut-array) noctx, unsafe
	a.begin-pointer unmanaged-free-elements a.size

###
This is like `uninitialized-mut-array` but fills memory with zeroes.

Zeroed memory is GC-safe, since the GC ignores null pointers,
but is still not safe to expose to readers (who may try to dereference the null pointers).

This is used by collections like `mut[]` that need some excess capacity that will be GC-safe.
###
zeroed-mut-array[t] t mut-array(size nat64) unsafe
	res t mut-array = size uninitialized-mut-array
	res set-zero-elements
	res

###
Sets every element to `zeroed`. See comment on `zeroed-mut-array` for why this is useful.
###
set-zero-elements[t] void(a t mut-array) noctx, unsafe
	a.begin-pointer set-zero-range a.size

###
`mut-array` where each element is `f[i]`.
Equivalent to `f[0], f[1], ...`.
###
.make[t] t mut-array(size nat64, f act t(nat64))
	trusted
		res t mut-array = size uninitialized-mut-array
		out mut = res begin-pointer
		i mut = 0
		loop
			if i == size
				assert out == res.end-pointer
				break res
			else
				*out := f[i]
				out := out + 1
				i := i + 1
				continue

!in[t] bool(value t, a t mut-array) noctx, t equal
	trusted
		cur mut = a.begin-pointer as-const
		end = a.end-pointer as-const
		loop
			if cur == end
				break false
			elif *cur == value
				break true
			else
				cur := cur + 1
				continue

###
Unsafe cast a mut-array to an array.
It must never be written to again.
###
!cast-immutable[t] t array(a t mut-array) noctx, unsafe
	a inner

###
Unsafe cast to immutable array.

This is O(1).

Unsafe because an array is expected to be fully immutable (meaning: no mutable references exist),
so this value must only be used temporarily.
###
!temp-as-array[t] t array(a t mut-array) noctx, unsafe
	a inner

###
Casts elements to a different type.
If the element type is different, adjusts the size so that the number of bytes is the same.
###
cast-mut-array[out, in] out mut-array(a in mut-array) noctx, unsafe
	assert size-of@in != 0 && size-of@out != 0
	size-bytes = a.size * size-of@in
	assert size-bytes % size-of@out == 0
	size-out = size-bytes / size-of@out
	# TODO: shouldn't need parentheses
	(a.begin-pointer.pointer-cast::out mut*)[0 .. size-out]

region spec impls (see collection.crow)

# Number of elements.
!size[t] nat64(a t mut-array) noctx
	a.inner size

# Calls 'f' on each element in the mut-array.
!iterate[t] bool(a t mut-array, f act bool(t))
	trusted a.temp-as-array iterate f

!iterate[t] bool(a t mut-array, f act bool(nat64, t))
	trusted a.temp-as-array iterate f

subregion iterator

!mut-array-iterator[t] record mut by-val
	collection t mut-array
	next-index nat64

!begin[t] t mut-array-iterator(a t mut-array) noctx
	a, 0

!end[t] t mut-array-iterator(a t mut-array) noctx
	a, a.size

!+[t] t mut-array-iterator(a t mut-array-iterator, b nat64) noctx
	new-index = a.next-index + b
	assert new-index <= a.collection.size
	a.collection, new-index

!-[t] t mut-array-iterator(a t mut-array-iterator, b nat64) noctx
	a.collection, a.next-index - b

!-[t] nat64(a t mut-array-iterator, b t mut-array-iterator) noctx
	assert a.collection == b.collection
	a.next-index - b.next-index

!prev[t] (t mut-array-iterator, t)?(a t mut-array-iterator) noctx
	unless a.next-index == 0
		assert a.next-index <= a.collection.size
		((a.collection, a.next-index - 1), trusted a.collection.begin-pointer[a.next-index - 1]),

!next[t] (t, t mut-array-iterator)?(a t mut-array-iterator) noctx
	if a.next-index < a.collection.size
		(trusted a.collection.begin-pointer[a next-index], (a.collection, a.next-index + 1)),

!set-prev[t] void(a t mut-array-iterator, value t) noctx
	assert a.next-index <= a.collection.size
	trusted
		a.collection.begin-pointer[a.next-index - 1] := value

!set-next[t] void(a t mut-array-iterator, value t) noctx
	trusted
		assert a.next-index < a.collection.size
		a.collection.begin-pointer[a next-index] := value

!slice[t] t mut-array(begin t mut-array-iterator, end t mut-array-iterator) noctx
	assert begin.collection == end.collection
	inner-iter = begin.collection.inner begin
	(inner-iter + begin.next-index slice inner-iter + end.next-index),

subregion builder

!mut-array-builder[t] record mut
	.inner t array-builder
!build[t] t mut-array(a build-options, f act void(t mut-array-builder))
	(a build out => f[(out,)])::(t array),
!~=[t] void(a t mut-array-builder, value t)
	a.inner ~= value
!~~=[t] void(a t mut-array-builder, values t array)
	a.inner ~~= values
!size[t] nat64(a t mut-array-builder)
	a.inner size
