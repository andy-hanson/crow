no-std
import
	../bool: !, &&, bool, false, new
	../compare: ==, <, <=, compare, comparison, equal, greater, less
	../fun-util: subscript
	../misc: new, void
	../opt: !, ||, is-empty, new, opt
	../private/bootstrap: hard-assert, hard-forbid, hard-unreachable
	../private/exception-low-level: forbid-c-str
	../private/number-low-level: ==, <=>, nat64, wrap-add, wrap-sub, wrap-mul
	../ptr: ==, +, set-deref, size-of
	../tuple: a, b, new, pair
	./arr: ~, ~~, arr, is-empty, new, size, subscript
	./arr-util:
		arr-iter, end-iter, find-and-get, fold, iter, next, prev, repeat, reverse, rtail, slice-left,
		slice-right, tail
	./fix-arr: begin-ptr, cast-immutable, end-ptr, fix-arr, uninitialized-fix-arr
	./iter: new, rest, value, value-and-rest
	./mut-arr: ~=, move-to-arr, mut-arr, new

###
Immutable sequence type.

This type is designed so concatenation (`~` and `~~`) will be efficient.
It also is efficient to split from an iterator.

It does not support efficient access by an index, so iterators should be used instead.
###
seq<a> record
	by-val
	.root l0 a

get-root<a> l0 a(a seq a) no-doc
	a.root

# TODO: no-doc
l0<a> record
	by-val
	node node<a, l1 a>

# TODO: no-doc
l1<a> record
	by-val
	node node<a[], l2 a>

# TODO: no-doc
l2<a> record
	by-val
	nodes a[][][]

# TODO: no-doc
node<a, lower> union
	leaf a[]
	branch branch<a, lower>

###
TODO: no-doc
'left', 'down', and 'right' should all be non-empty.
(Else, this layer should be a leaf. See `branch-or-leaf`.)
###
branch<a, lower> record
	left a[]
	down lower
	right a[]

.new<a, lower> node<a, lower>()
	() leaf

.new<a> l1 a()
	(),
.new<a> l2 a()
	(),

.is-empty<a> bool(a l1 a)
	match a node
	as leaf xs
		xs is-empty
	as branch _
		false

.is-empty<a> bool(a l2 a)
	a.nodes is-empty

.depth<a> nat64(a seq a)
	match a.root.node
	as leaf _
		0
	as branch b
		b.down depth-l1

.depth-l1<a> nat64(a l1 a)
	match a node
	as leaf _
		1
	as branch b
		b.down depth-l2

.depth-l2<a> nat64(_ l2 a)
	2

.as-branch<a, lower> branch<a, lower>(a node<a, lower>) trusted
	match a
	as leaf _
		hard-unreachable
	as branch b
		b

.get-l0-branch<a> branch<a, l1 a>(a seq a)
	a.root.node as-branch

.try-get-l1<a> l1<a>?(a seq a)
	match a.root node
	as leaf _
		()
	as branch b
		b.down,

.get-l1<a> l1 a(a seq a)
	a.get-l0-branch down

.get-l2<a> l2 a(a seq a)
	a.get-l1.node.as-branch down

# Sequence containing the given elements.
new<a> seq a(...a a[]) noctx
	a to-seq

###
Convert an array to a sequence.
This is O(1).
###
to-seq<a> seq a(a a[]) noctx
	l l0 a = a.leaf,
	l,

seq-equal<a> bool(a seq a, b seq a) equal<a>
	a.iter seq-equal-recur b.iter
.seq-equal-recur<a> bool(a seq-iter a, b seq-iter a) equal<a>
	if na ?= a next
		if nb ?= b.next
			if na.value == nb.value
				na.rest seq-equal-recur nb.rest
	else
		b.next is-empty

seq-compare<a> comparison(a seq a, b seq a) compare<a>
	a.iter seq-compare-recur b.iter
.seq-compare-recur<a> comparison(a seq-iter a, b seq-iter a) compare<a>
	if na ?= a next
		if nb ?= b next
			cmp = na.value <=> nb.value
			if cmp == equal
				na.rest seq-compare-recur nb.rest
			else
				cmp
		else
			greater
	else
		b.next.is-empty ? equal : less

###
Convert a sequence to an array.
This is O(n).
###
to-arr<a> a[](a seq a)
	a map-to-arr x => x

###
`true` iff `a.size == 0`.
This is O(1).
###
is-empty<a> bool(a seq a)
	match a.root node
	as leaf xs
		xs is-empty
	as branch _
		false

###
Number of elements in the sequence.
This is O(n).
###
size<a> nat64(a seq a)
	0 fold-chunks a, (acc, xs) => acc wrap-add xs.size

###
Concatenate two sequences.
This is O(log n), where `n` is the size of the smaller sequence.
###
~~<a> seq a(a seq a, b seq a)
	# Concatenate the smaller sequence onto the bigger one.
	match a.depth <=> b.depth
	as less
		(a.root cat-left-node-l0 b.root),
	as equal
		if a.size < b.size
			(a.root cat-left-node-l0 b.root),
		else
			(a.root cat-right-node-l0 b.root),
	as greater
		(a.root cat-right-node-l0 b.root),

###
Prepend an element to a sequence.
This is O(log n), amortized O(1).
###
~<a> seq a(a a, b seq a)
	((a,)::a[] cat-left-array-l0 b.root),

###
Appendd an element to a sequence.
This is O(log n), amortized O(1).
###
~<a> seq a(a seq a, b a)
	(a.root cat-right-array-l0 (b,)),

# 'r' is bigger, add 'l' to the left
.cat-left-node-level<a, lower> node<a, lower>
	l node<a, lower>
	r node<a, lower>
	cat-left-array-lower act lower(a[], lower)
	cat-left-node-lower act lower(lower, lower)
spec
	trusted
	new<lower>
body
	match l
	as leaf xs
		xs cat-left-array-level r, cat-left-array-lower
	as branch bl
		match r
		as leaf _
			# 'r' should be bigger
			hard-unreachable
		as branch br
			down1 = cat-left-array-lower[bl.right ~~ br.left, br.down]
			down2 = cat-left-node-lower[bl.down, down1]
			(bl.left, down2, br.right) branch

# 'l' is bigger, add 'r' to right
.cat-right-node-level<a, lower> node<a, lower>
	l node<a, lower>
	r node<a, lower>
	cat-right-array-lower act lower(lower, a[])
	cat-right-node-lower act lower(lower, lower)
spec
	trusted
	new<lower>
body
	match r
	as leaf xs
		l cat-right-array-level xs, cat-right-array-lower
	as branch br
		match l
		as leaf _
			# 'l' should be bigger
			hard-unreachable
		as branch bl
			down1 = cat-right-array-lower[bl.down, bl.right ~~ br.left]
			down2 = cat-right-node-lower[down1, br.down]
			(bl.left, down2, br.right) branch

# 'r' is bigger
.cat-left-array-level<a, lower> node<a, lower>
	l a[]
	r node<a, lower>
	cat-left-lower act lower(a[], lower)
spec
	new<lower>
body
	match r
	as leaf xs
		if (l.size-bytes wrap-add xs.size-bytes) <= max-leaf-bytes
			l ~~ xs leaf
		else
			(l, (), xs) branch
	as branch br
		if (l.size-bytes wrap-add br.left.size-bytes) <= max-leaf-bytes
			(l ~~ br.left, br.down, br.right) branch
		else
			(l, cat-left-lower[br.left, br.down], br.right) branch

.cat-right-array-level<a, lower> node<a, lower>
	l node<a, lower>
	r a[]
	cat-right-lower act lower(lower, a[])
spec
	new<lower>
body
	match l
	as leaf xs
		if (xs.size-bytes wrap-add r.size-bytes) <= max-leaf-bytes
			xs ~~ r leaf
		else
			(xs, (), r) branch
	as branch br
		if (br.right.size-bytes wrap-add r.size-bytes) <= max-leaf-bytes
			(br.left, br.down, br.right ~~ r) branch
		else
			(br.left, cat-right-lower[br.down, br.right], r) branch

.cat-left-node-l0<a> l0 a(l l0 a, r l0 a)
	new: l.node cat-left-node-level r.node, ((xs, lower) => (xs,) cat-left-array-l1 lower), (x, y) =>
		x cat-left-node-l1 y

.cat-right-node-l0<a> l0 a(l l0 a, r l0 a)
	new: l.node cat-right-node-level r.node, ((lower, xs) => lower cat-right-array-l1 (xs,)), (x, y) =>
		x cat-right-node-l1 y

.cat-left-array-l0<a> l0 a(l a[], r l0 a)
	new: l cat-left-array-level r.node, (xs, lower) => (xs,) cat-left-array-l1 lower

.cat-right-array-l0<a> l0 a(l l0 a, r a[])
	new: l.node cat-right-array-level r, (lower, xs) => lower cat-right-array-l1 (xs,)

.cat-left-node-l1<a> l1 a(l l1 a, r l1 a)
	new: l.node cat-left-node-level r.node, ((xs, lower) => (xs,) cat-left-array-l2 lower), (x, y) =>
		x cat-left-node-l2 y

.cat-right-node-l1<a> l1 a(l l1 a, r l1 a)
	new: l.node cat-right-node-level r.node, ((lower, xs) => lower cat-right-array-l2 (xs,)), (x, y) =>
		x cat-right-node-l2 y

.cat-left-array-l1<a> l1 a(l a[][], r l1 a)
	new: l cat-left-array-level r.node, (xs, lower) => (xs,) cat-left-array-l2 lower

.cat-right-array-l1<a> l1 a(l l1 a, r a[][])
	new: l.node cat-right-array-level r, (lower, xs) => lower cat-right-array-l2 (xs,)

.cat-left-node-l2<a> l2 a(l l2 a, r l2 a)
	l.nodes ~~ r.nodes,

.cat-right-node-l2<a> l2 a(l l2 a, r l2 a)
	l.nodes ~~ r.nodes,

.cat-left-array-l2<a> l2 a(l a[][][], r l2 a)
	l ~~ r.nodes,

.cat-right-array-l2<a> l2 a(l l2 a, r a[][][])
	l.nodes ~~ r,

# Leaves are allowed to be arbitrarily long, this is just the limit when we would normally make a branch
.max-leaf-bytes nat64()
	64

# 'trusted' since we are just using it to guide branch sizes (which should have no observable effect)
.size-bytes<a> nat64(a a[]) trusted
	a.size wrap-mul size-of@<a>

region iter and slice


# Immutable iterator over a sequence.
seq-iter<a> record
	by-val
	.seq seq a
	.state seq-iter-state a

.seq-iter-state<a> union
	# If l0 is a leaf, l0-left and l0-right are equivalent
	l0-left arr-iter a
	l0-right arr-iter a
	# If l1 is a leaf, l1-left and l1-right are equivalent
	l1-left arr-2d-iter a
	l1-right arr-2d-iter a
	l2 arr-3d-iter a

# New iterator at the beginning of the sequence.
iter<a> seq-iter<a>(a seq a)
	match a.root node
	as leaf xs
		a, xs.iter.l0-right
	as branch b
		a, b.left.iter.l0-left

end-iter<a> seq-iter<a>(a seq a)
	match a.root node
	as leaf xs
		a, xs.end-iter.l0-left
	as branch b
		a, b.right.end-iter.l0-right

###
Returns the next element and next iterator.
Returns an empty option if the iterator was already at the end of the sequence.
###
next<a> value-and-rest<a, seq-iter a>?(a seq-iter a)
	match a state
	as l0-left iter
		if n ?= iter next
			(n.value, (a.seq, n.rest.l0-left)),
		else
			a.seq.l1-iter next
	as l0-right iter
		if n ?= iter next
			(n.value, (a.seq, n.rest.l0-right)),
	as l1-left iter
		if n ?= iter next
			(n.value, (a.seq, n.rest.l1-left)),
		else
			a.seq.l2-iter next
	as l1-right iter
		if n ?= iter next
			(n.value, (a.seq, n.rest.l1-right)),
		else
			l0 seq-iter a = a.seq, a.seq.get-l0-branch.right.iter l0-right
			l0 next
	as l2 iter
		if n ?= iter next
			(n.value, (a.seq, n.rest.l2)),
		else
			l1 seq-iter a = a.seq, a.seq.get-l1.node.as-branch.right.iter2d l1-right
			l1 next

prev<a> value-and-rest<a, seq-iter a>?(a seq-iter a)
	match a state
	as l0-left iter
		if p ?= iter prev
			(p.value, (a.seq, p.rest.l0-left)),
	as l0-right iter
		if p ?= iter prev
			(p.value, (a.seq, p.rest.l0-right)),
		else
			a.seq.l1-end-iter prev
	as l1-left iter
		if p ?= iter prev
			(p.value, (a.seq, p.rest.l1-left)),
		else
			l0 seq-iter a = a.seq, a.seq.get-l0-branch.left.end-iter l0-left
			l0 prev
	as l1-right iter
		if p ?= iter prev
			(p.value, (a.seq, p.rest.l1-right)),
		else
			a.seq.l2-end-iter prev
	as l2 iter
		if p ?= iter prev
			(p.value, (a.seq, p.rest.l2)),
		else
			l1 seq-iter a = a.seq, a.seq.get-l1.node.as-branch.left.end-iter2d l1-left
			l1 prev

# Mutable iterator over a sequence.
mut-seq-iter<a> record mut
	.inner mut seq-iter a

# New mutable iterator at the beginning of the sequence.
mut-iter<a> mut-seq-iter a(a seq a)
	a.iter,

end-mut-iter<a> mut-seq-iter a(a seq a)
	a.end-iter,

# Copy to an immutable iterator.
iter<a> seq-iter a(a mut-seq-iter a)
	a.inner

copy<a> mut-seq-iter a(a mut-seq-iter a)
	a.inner,

###
Advances the iterator and returns the next element.
Returns an empty option if the iterator was already at the end of the sequence.
###
next<a> a?(a mut-seq-iter a)
	if pair ?= a.inner next
		a.inner := pair rest
		pair.value,

prev<a> a?(a mut-seq-iter a)
	if pair ?= a.inner prev
		a.inner := pair rest
		pair.value,

###
Returns the next element without advancing the iterator.
Returns an empty option if the iterator is at the end of the sequence.
###
peek<a> a?(a mut-seq-iter a)
	if pair ?= a.inner next
		pair.value,

###
Seq of every element to the left of the iterator.
This is O(log n).
###
slice-left<a> seq a(a seq-iter a)
	match a state
	as l0-left iter
		iter.slice-left to-seq
	as l0-right iter
		match a.seq.root node
		as leaf _
			iter.slice-left to-seq
		as branch b
			(b.left branch-or-leaf-l0 b.down, iter.slice-left),
	as l1-left iter
		a.seq slice-left-update-l1 l1 =>
			match l1 node
			as leaf _
				iter.slice-left.leaf,
			as branch b
				iter.slice-left branch-or-leaf-l1 b.down, b.right
	as l1-right iter
		a.seq slice-left-update-l1 l1 =>
			match l1 node
			as leaf _
				iter.slice-left.leaf,
			as branch b
				b.left branch-or-leaf-l1 b.down, iter.slice-left
	as l2 iter
		a.seq slice-left-update-l2 ignore =>
			iter.slice-left,

###
Seq of every element to the right of the iterator.
This is O(log n).
###
slice-right<a> seq a(a seq-iter a)
	match a state
	as l0-left iter
		match a.seq.root node
		as leaf _
			iter.slice-right to-seq
		as branch b
			(iter.slice-right branch-or-leaf-l0 b.down, b.right),
	as l0-right iter
		iter.slice-right to-seq
	as l1-left iter
		a.seq slice-right-update-l1 l1 =>
			b = l1.node as-branch
			iter.slice-right branch-or-leaf-l1 b.down, b.right
	as l1-right iter
		a.seq slice-right-update-l1 l1 =>
			match l1 node
			as leaf _
				iter.slice-right.leaf,
			as branch b
				b.left branch-or-leaf-l1 b.down, iter.slice-right
	as l2 iter
		a.seq slice-right-update-l2 ignore =>
			iter.slice-right,

.level<l, a> spec
	is-empty bool(level l)
	pop-left pair<a[], l>(level l)
	pop-right pair<l, a[]>(level l)

.branch-or-leaf-generic<a, lower> node<a, lower>(left a[], down lower, right a[]) trusted level<lower, a>
	if down is-empty
		if left is-empty
			right leaf
		elif right is-empty
			left leaf
		else
			left ~~ right leaf
	else
		hard-forbid: left.is-empty && right.is-empty
		if left is-empty
			pair = down pop-left
			if pair.b is-empty
				pair.a ~~ right leaf
			else
				(pair.a, pair.b, right) branch
		elif right is-empty
			pair = down pop-right
			if pair.a is-empty
				left ~~ pair.b leaf
			else
				(left, pair.a, pair.b) branch
		else
			(left, down, right) branch

.branch-or-leaf-l0<a> l0 a(left a[], down l1 a, right a[])
	(left branch-or-leaf-generic down, right),

.branch-or-leaf-l1<a> l1 a(left a[][], down l2 a, right a[][])
	(left branch-or-leaf-generic down, right),

.pop-left<a> pair<a[], l1 a>(a l1 a)
	match a node
	as leaf xs
		xs[0], (xs.tail.leaf,)
	as branch b
		popped = b.left[0]
		rest l1 a = if b.left.tail is-empty
			if b.down is-empty
				b.right.leaf,
			else
				pair = b.down pop-left
				(pair.a, pair.b, b.right).branch,
		else
			(b.left.tail, b.down, b.right).branch,
		popped, rest

.pop-right<a> pair<l1 a, a[]>(a l1 a)
	match a node
	as leaf xs
		(xs.rtail.leaf,), xs[xs.size wrap-sub 1]
	as branch b
		popped = b.right[b.right.size wrap-sub 1]
		rest l1 a = if b.right.rtail is-empty
			if b.down is-empty
				b.left.leaf,
			else
				pair = b.down pop-right
				(b.left, pair.a, pair.b).branch,
		else
			(b.left, b.down, b.right.rtail).branch,
		rest, popped

.pop-left<a> pair<a[][], l2 a>(a l2 a)
	a.nodes[0], (a.nodes.tail,)

.pop-right<a> pair<l2 a, a[][]>(a l2 a)
	(a.nodes.rtail,), a.nodes[a.nodes.size wrap-sub 1]

.slice-left-update-l1<a> seq a(a seq a, f act l1 a(l1 a))
	old-l0 = a get-l0-branch
	new-l1 = f[old-l0 down]
	(old-l0.left branch-or-leaf-l0 new-l1, ()),

.slice-right-update-l1<a> seq a(a seq a, f act l1 a(l1 a))
	old-l0 = a get-l0-branch
	new-l1 = f[old-l0 down]
	(()::a[] branch-or-leaf-l0 new-l1, old-l0.right),

.slice-left-update-l2<a> seq a(a seq a, f act l2 a(l2 a))
	a slice-left-update-l1 old-l1 =>
		b = old-l1.node as-branch
		new-l2 = f[b down]
		if new-l2 is-empty
			b.left.leaf,
		else
			pair = new-l2 pop-right
			(b.left, pair.a, pair.b).branch,

.slice-right-update-l2<a> seq a(a seq a, f act l2 a(l2 a))
	a slice-right-update-l1 old-l1 =>
		b = old-l1.node as-branch
		new-l2 = f[b down]
		if new-l2 is-empty
			b.right.leaf,
		else
			pair = new-l2 pop-left
			(pair.a, pair.b, b.right).branch,

# Iterator through an a[][].
# This will always be non-empty.
.arr-2d-iter<a> record
	by-val
	cur-array arr-iter a
	# This iterator comes after 'cur-array'
	# That means 'prev' is more complicated than 'next'
	after-cur-array arr-iter a[]

.iter2d<a> arr-2d-iter a(a a[][])
	outer = a iter
	if n ?= outer next
		n.value.iter, n.rest
	else
		().iter, outer

.end-iter2d<a> arr-2d-iter a(a a[][])
	outer = a end-iter
	if p ?= outer prev
		p.value.end-iter, p.rest
	else
		().end-iter, outer

.next<a> value-and-rest<a, arr-2d-iter a>?(a arr-2d-iter a)
	if n ?= a.cur-array next
		(n.value, (n.rest, a.after-cur-array)),
	elif n ?= a.after-cur-array next
		(n.value.iter, n.rest)::arr-2d-iter<a> next

.prev<a> value-and-rest<a, arr-2d-iter a>?(a arr-2d-iter a)
	if p ?= a.cur-array prev
		(p.value, (p.rest, a.after-cur-array)),
	else
		iter-cur-array = a.after-cur-array.prev!.rest
		if p ?= iter-cur-array prev
			(p.value.end-iter, iter-cur-array)::arr-2d-iter<a> prev

.slice-left<a> a[][](a arr-2d-iter a)
	# rtail since the last one is 'cur-array'
	left-arrays = a.after-cur-array.slice-left rtail
	last-part = a.cur-array slice-left
	last-part.is-empty ? left-arrays : left-arrays ~ last-part

.slice-right<a> a[][](a arr-2d-iter a)
	first = a.cur-array slice-right
	rest = a.after-cur-array slice-right
	first.is-empty ? rest : first ~ rest

# Iterator through an a[][][]
.arr-3d-iter<a> record
	by-val
	cur-iter2d arr-2d-iter a
	rest-arrays arr-iter a[][]

.iter3d<a> arr-3d-iter a(a a[][][])
	outer = a iter
	if n ?= outer next
		n.value.iter2d, n.rest
	else
		().iter2d, outer

.end-iter3d<a> arr-3d-iter a(a a[][][])
	outer = a iter
	if p ?= outer prev
		p.value.end-iter2d, p.rest
	else
		().iter2d, outer

.next<a> value-and-rest<a, arr-3d-iter a>?(a arr-3d-iter a)
	if n ?= a.cur-iter2d next
		(n.value, (n.rest, a.rest-arrays)),
	elif n ?= a.rest-arrays next
		cur-array-next = n.value.iter2d.next!
		(cur-array-next.value, (cur-array-next.rest, n.rest)),

.prev<a> value-and-rest<a, arr-3d-iter a>?(a arr-3d-iter a)
	if p ?= a.cur-iter2d prev
		(p.value, (p.rest, a.rest-arrays)),
	elif p ?= a.rest-arrays.prev!.rest prev
		cur-iter2d-prev = p.value.end-iter2d.prev!
		(cur-iter2d-prev.value, (cur-iter2d-prev.rest, p.rest)),

.slice-left<a> a[][][](a arr-3d-iter a)
	left-arrays = a.rest-arrays.slice-left rtail
	last-part = a.cur-iter2d slice-left
	last-part.is-empty ? left-arrays : left-arrays ~ last-part

.slice-right<a> a[][][](a arr-3d-iter a)
	first = a.cur-iter2d slice-right
	rest = a.rest-arrays slice-right
	first.is-empty ? rest : first ~ rest

.l1-iter<a> seq-iter a(a seq a)
	a, if l1 ?= a try-get-l1
		match l1 node
		as leaf xs
			if xs is-empty
				a.get-l0-branch.right.iter l0-right
			else
				xs.iter2d l1-right
		as branch b
			b.left.iter2d l1-left
	else
		l0-right: match a.root node
		as leaf xs
			xs end-iter
		as branch b
			b.right iter

.l1-end-iter<a> seq-iter a(a seq a)
	a, if l1 ?= a try-get-l1
		match l1 node
		as leaf xs
			if xs is-empty
				a.get-l0-branch.left.end-iter l0-left
			else
				xs.end-iter2d l1-left
		as branch b
			b.right.end-iter2d l1-right
	else
		l0-left: match a.root node
		as leaf xs
			xs.iter
		as branch b
			b.left.end-iter

.l2-iter<a> seq-iter a(a seq a)
	a, (a.get-l2.nodes.iter3d).l2

.l2-end-iter<a> seq-iter a(a seq a)
	a, (a.get-l2.nodes.end-iter3d).l2

region iteration

each<a> void(a seq a, f act void(a))
	() fold a, (ignore, x) => f[x]

fold<a, b> a(acc a, a seq b, f act a(a, b))
	acc fold-chunks a, (cur, chunk) =>
		cur fold chunk, f

some<a> bool(a seq a, f act bool(a))
	v void? = a find-and-get x =>
		if f[x]
			(),
	!v.is-empty

every<a> bool(a seq a, f act bool(a))
	!(a some x => !f[x])

find-and-get<out, in> out?(a seq in, f act out?(in))
	a find-and-get-chunk chunk =>
		chunk find-and-get f

filter<a> seq a(a seq a, f act bool(a))
	a flat-map x =>
		if f[x]
			x,

drop-while<a> seq a(a seq a, f act bool(a))
	a.iter drop-while-recur f
.drop-while-recur<a> seq a(a seq-iter a, f act bool(a))
	if n ?= a next
		if f[n value]
			n.rest drop-while-recur f
		else
			a slice-right

drop-right-while<a> seq a(a seq a, f act bool(a))
	a.end-iter drop-right-while-recur f
.drop-right-while-recur<a> seq a(a seq-iter a, f act bool(a))
	if p ?= a prev
		if f[p value]
			p.rest drop-right-while-recur f
		else
			a slice-left

map<out, in> seq out(a seq in, f act out(in))
	a map-to-arr f to-seq

flat-map<out, in> seq out(a seq in, f act seq out(in))
	res out mut[] = ()
	a each in =>
		f[in] each out =>
			res ~= out
	res.move-to-arr to-seq

map-to-arr<out, in> out[](a seq in, f act out(in)) trusted
	res fix-arr out = a.size uninitialized-fix-arr
	end = res.begin-ptr fold a, (out, x) =>
		*out := f[x]
		out + 1
	hard-assert: end == res.end-ptr
	res cast-immutable

.fold-chunks<a, b> a(acc a, xs seq b, f act a(a, b[]))
	match xs.root node
	as leaf l
		f[acc, l]
	as branch b
		mid = f[acc, b left] fold-chunks-l1 b.down, f
		f[mid, b right]

.fold-chunks-l1<a, b> a(acc a, xs l1 b, f act a(a, b[]))
	match xs node
	as leaf l
		acc fold l, f
	as branch b
		acc fold b.left, f fold-chunks-l2 b.down, f fold b.right, f

.fold-chunks-l2<a, b> a(acc a, xs l2 b, f act a(a, b[]))
	acc fold xs.nodes, (cur, nodes) =>
		cur fold nodes, f

.find-and-get-chunk<out, in> out?(a seq in, f act out?(in[]))
	match a.root node
	as leaf l
		f[l]
	as branch b
		f[b.left] || b.down find-and-get-chunk-l1 f || f[b.right]

.find-and-get-chunk-l1<out, in> out?(a l1 in, f act out?(in[]))
	match a node
	as leaf l
		l find-and-get f
	as branch b
		b.left find-and-get f || b.down find-and-get-chunk-l2 f || b.right find-and-get f

.find-and-get-chunk-l2<out, in> out?(a l2 in, f act out?(in[]))
	a.nodes find-and-get xs =>
		xs find-and-get f

starts-with<a> bool(a seq a, b seq a) equal<a>
	!(a.iter iter-starts-with b.iter).is-empty

ends-with<a> bool(a seq a, b seq a) equal<a>
	!(a.end-iter iter-ends-with b.end-iter).is-empty

try-remove-start<a> seq<a>?(a seq a, b seq a) equal<a>
	if iter ?= a.iter iter-starts-with b.iter
		iter.slice-right,

try-remove-end<a> seq<a>?(a seq a, b seq a) equal<a>
	if iter ?= a.end-iter iter-ends-with b.end-iter
		iter.slice-left,

###
`true` iff `b` is a subsequence of `a`.

That means: The characters in `b` appear in `a`, in order, without interruptions.
###
contains-subseq<a> bool(a seq a, b seq a) equal<a>
	!(a iters-of-subseq b).is-empty

split-seq<a> record
	left seq a
	middle a
	right seq a

try-split-once-by-fn<a> split-seq<a>?(a seq a, f act bool(a)) equal<a>
	a.iter try-split-once-by-fn-recur f
.try-split-once-by-fn-recur<a> split-seq<a>?(a seq-iter a, f act bool(a))
	if n ?= a next
		if f[n value]
			(a.slice-left, n.value, n.rest.slice-right),
		else
			n.rest try-split-once-by-fn-recur f

###
Finds the first apperance of `b` as a subsequence of `a`,
and returns slices of `a` to the left and right of that.

Returns an empty option iff `b` is not a subsequence of `a`.
###
try-split-once-by-subseq<a> pair<seq a, seq a>?(a seq a, b seq a) equal<a>
	"can't split by empty sequence" forbid-c-str b.is-empty
	if pair ?= a iters-of-subseq b
		(pair.a.slice-left, pair.b.slice-right),

try-split-once-by-subseq-right<a> pair<seq a, seq a>?(a seq a, b seq a) equal<a>
	"can't split by empty sequence" forbid-c-str b.is-empty
	if pair ?= a iters-of-subseq-right b
		(pair.a.slice-left, pair.b.slice-right),

split-by-subseq<a> seq seq a(a seq a, b seq a) equal<a>
	"can't split by empty sequence" forbid-c-str b.is-empty
	out seq<a> mut[] = ()
	out split-by-subseq-recur a, b
	out.move-to-arr to-seq
.split-by-subseq-recur<a> void(out seq<a> mut[], a seq a, b seq a) equal<a>
	if pair ?= a try-split-once-by-subseq b
		out ~= pair.a
		out split-by-subseq-recur pair.b, b
	else
		unless a is-empty
			out ~= a

.iters-of-subseq<a> pair<seq-iter a, seq-iter a>?(a seq a, b seq a) equal<a>
	a.iter iters-of-subseq-recur b
.iters-of-subseq-recur<a> pair<seq-iter a, seq-iter a>?(a seq-iter a, b seq a) equal<a>
	if end-iter ?= a iter-starts-with b.iter
		(a, end-iter),
	elif n ?= a next
		n.rest iters-of-subseq-recur b

.iters-of-subseq-right<a> pair<seq-iter a, seq-iter a>?(a seq a, b seq a) equal<a>
	a.end-iter iters-of-subseq-right-recur b
.iters-of-subseq-right-recur<a> pair<seq-iter a, seq-iter a>?(a seq-iter a, b seq a) equal<a>
	if start-iter ?= a iter-ends-with b.end-iter
		(start-iter, a),
	elif p ?= a prev
		p.rest iters-of-subseq-right-recur b

.iter-starts-with<a> seq-iter<a>?(a seq-iter a, b seq-iter a) equal<a>
	if bn ?= b next
		if an ?= a next
			if an.value == bn.value
				an.rest iter-starts-with bn.rest
	else
		a,

.iter-ends-with<a> seq-iter<a>?(a seq-iter a, b seq-iter a) equal<a>
	if bp ?= b prev
		if ap ?= a prev
			if ap.value == bp.value
				ap.rest iter-ends-with bp.rest
	else
		a,

repeat<a> seq a(a seq a, n nat64)
	a.to-arr repeat n to-seq

reverse<a> seq a(a seq a)
	a.to-arr.reverse to-seq
