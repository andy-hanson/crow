no-std
import
	./bool: ||, bool
	./cell: *, cell, new, set-deref
	./compare: <, comparison
	./col/array: array
	./col/iter: new, rest, value, value-and-rest
	./col/list:
		~, ~~, contains-sublist, copy, drop-right-while, drop-while, end-iter, ends-with, for-loop,
		is-empty, iter, left, list, list-cast, list-compare, list-equal, list-iter, middle,
		mut-iter, mut-list-iter, new, next, peek, prev, repeat, reverse, right, slice-left,
		slice-right, split-by-sublist, starts-with, to-array, to-list, try-remove-end,
		try-remove-start, try-split-once-by-fn, try-split-once-by-sublist,
		try-split-once-by-sublist-right
	./col/mut-list: copy, move-to-list, to-mut-list
	./col/private/array-low-level: array-from-begin-end, begin-ptr
	./hash: hasher, hash-mix
	./misc: void
	./number:
		==, <=>, +, *, /, %, abs, float64, float32, int8, int16, int32, int64, is-nan, min-int64,
		nat8, nat16, nat32, nat64, to-float64, to-int64, to-nat64, truncate-to-int64
	./opt: ??, new, opt
	./private/c-string-util: find-c-string-end
	./private/number-low-level: wrap-sub
	./tuple: a, b, new, pair
export
	./private/bootstrap: char8, c-string, to-char8, to-nat8

###
String type.

Strings are optimized for fast concatenation and splitting.
They don't support efficient access by index, so use iterators instead.
###
string record by-val
	.inner char8[]

char8s char8[](a string)
	a inner

literal string(a c-string) noctx trusted
	(a array-from-begin-end a.find-c-string-end).to-list as-string

###
Concatenate two strings.
This is O(n).

If you need to concatenate many strings, consider using the `crow/writer` module.
###
~~ string(a string, b string)
	a.inner ~~ b.inner,

~ string(a char8, b string)
	a ~ b.inner,

~ string(a string, b char8)
	a.inner ~ b,

# Same as `""`.
new string() noctx
	""

# Same as `a == ""`.
is-empty bool(a string)
	a.inner is-empty

== bool(a string, b string) noctx trusted
	a.inner list-equal b.inner
== bool(a char8, b char8) noctx
	a.to-nat8 == b.to-nat8

<=> comparison(a string, b string) trusted
	a.inner list-compare b.inner
<=> comparison(a char8, b char8) noctx
	a.to-nat8 <=> b.to-nat8

hash-mix void(hasher hasher, a string)
	for c : a char8s
		hasher hash-mix c.to-nat8.to-nat64

region conversion

###
Treat a list of UTF-8 character codes as a string.
This is O(1).
###
as-string string(a char8[]) noctx
	a,
as-string string(a array char8) noctx
	a.to-list as-string

###
Treats UTF-8 bytes as a string.
This is O(1).
###
as-string string(bytes nat8[]) unsafe
	bytes.list-cast@<char8, nat8> as-string
as-string string(bytes array nat8) unsafe
	bytes.to-list as-string

###
Treats a string as a list of UTF-8 bytes.
This is O(1).
###
to-bytes nat8[](a string) trusted
	a.char8s list-cast@<nat8, char8>

# TODO: this should probably be 'unsafe', but safe functions like "throw" need a c-string
###
Converts a string to a c-string by appending "\0".
This is O(n).

WARN: The resulting string is allocated from GC memory but isn't traced by the garbage collector,
so it may be freed after the current task.
###
to-c-string c-string(a string) trusted
	(a ~ "\0").char8s.to-array begin-ptr

###
Treats a c-string as a string by walking to the end.
This copies the memory, so the result is safe to use immutably.

Unsafe because `a` must end in a `"\0"` character somewhere.
###
to-string string(a c-string) unsafe
	# TODO: 'copy' seems unnecessary since 'to-mut-list' should do that?
	a.literal.char8s.to-mut-list.copy.move-to-list as-string

# `"true"` or `"false"`.
to-string string(b bool) noctx
	if b
		"true"
	else
		"false"

# String containing a single character.
to-string string(c char8)
	(c,)::char8[] as-string

###
Converts a nat64 to a string.
Result is only digits, no punctuation.
###
to-string string(a nat64)
	a to-base 10

.digit-to-string string(a nat64)
	if a == 0
		"0"
	elif a == 1
		"1"
	elif a == 2
		"2"
	elif a == 3
		"3"
	elif a == 4
		"4"
	elif a == 5
		"5"
	elif a == 6
		"6"
	elif a == 7
		"7"
	elif a == 8
		"8"
	elif a == 9
		"9"
	elif a == 10
		"a"
	elif a == 11
		"b"
	elif a == 12
		"c"
	elif a == 13
		"d"
	elif a == 14
		"e"
	elif a == 15
		"f"
	else
		"?"

to-binary string(a nat64)
	a to-base 2

to-hex string(a nat64)
	a to-base 16
to-hex string(a nat8)
	a to-base 16

.to-base string(a nat64, base nat64)
	if a < base
		a digit-to-string
	else
		(a / base to-base base) ~~ (a % base).digit-to-string
.to-base string(a nat8, base nat64)
	a.to-nat64 to-base base

# Spec of any type that could be converted to a string.
to-string<a> spec
	to-string string(a a)

# Identity function to satisfy the `to-string` spec.
to-string string(a string)
	a

to-string string(a nat8)
	a.to-nat64 to-string

to-string string(a nat16)
	a.to-nat64 to-string

to-string string(a nat32)
	a.to-nat64 to-string

to-string string(a int8)
	a.to-int64 to-string

to-string string(a int16)
	a.to-int64 to-string

to-string string(a int32)
	a.to-int64 to-string

to-string string(a int64)
	if a == min-int64
		"-9223372036854775808"
	else
		s = a.abs to-string
		a < 0 ? "-{s}" : s

to-string string(a float32)
	a.to-float64 to-string

to-string string(a float64)
	if a is-nan
		"NaN"
	else
		pos = a.abs to-string-positive
		if a < 0
			"-{pos}"
		else
			pos

.to-string-positive string(a float64)
	# TODO: Use a real float to string algorithm
	times-1000 = (a * 1000.0).truncate-to-int64
	thousandths = times-1000 % 1000
	whole-part = "{times-1000 / 1000}"
	hundredths = thousandths / 10
	tenths = hundredths / 10
	d0 = tenths digit-to-string
	d1 = hundredths % 10 digit-to-string
	d2 = thousandths % 10 digit-to-string
	if thousandths == 0
		whole-part
	else
		"{whole-part}.{d0}{d1}{d2}" strip-right "0"

region iterator

string-iter record by-val
	.inner list-iter char8

iter string-iter(a string)
	a.char8s.iter,

end-iter string-iter(a string)
	a.char8s.end-iter,

next value-and-rest<char8, string-iter>?(a string-iter)
	if res ?= a.inner next
		(res.value, (res.rest,)),

prev value-and-rest<char8, string-iter>?(a string-iter)
	if res ?= a.inner prev
		(res.value, (res.rest,)),

slice-left string(a string-iter)
	a.inner.slice-left as-string

slice-right string(a string-iter)
	a.inner.slice-right as-string

mut-string-iter record mut
	.inner mut-list-iter char8

mut-iter mut-string-iter(a string)
	a.char8s.mut-iter,

copy mut-string-iter(a mut-string-iter)
	a.inner.copy,

peek char8?(a mut-string-iter)
	a.inner peek

next char8?(a mut-string-iter)
	a.inner next

region split

###
`true` iff `b` is a subtring of `a`.

That means: The characters in `b` appear in `a`, in order, without interruptions.
###
contains-substr bool(a string, b string)
	a.char8s contains-sublist b.char8s

# `true` iff the first characters of `a` are `b`.
starts-with bool(a string, b string)
	a.char8s starts-with b.char8s

# `true` iff the last characters of `a` are `b`.
ends-with bool(a string, b string)
	a.char8s ends-with b.char8s

###
If `a` starts with `b`, returns the rest.
Else, returns an empty option.
###
try-remove-start string?(a string, b string)
	if res ?= a.char8s try-remove-start b.char8s
		res.as-string,

###
If `a` ends with `b, returns the rest.
Else, returns an empty option.
###
try-remove-end string?(a string, b string)
	if res ?= a.char8s try-remove-end b.char8s
		res.as-string,

###
If `b` is a substring of `a`, removes it and return the parts of `a` before and after it.
###
try-split-once (string, string)?(a string, b string)
	if res ?= a.char8s try-split-once-by-sublist b.char8s
		(res.a.as-string, res.b.as-string),

# Like `try-split-once`, but splits on the rightmost occurrence of `b`.
try-split-once-right (string, string)?(a string, b string)
	if res ?= a.char8s try-split-once-by-sublist-right b.char8s
		(res.a.as-string, res.b.as-string),

###
Removes every occurrence of the substring `b` in `a` and return the remaining parts as an array.
The result may include empty strings.
###
split-by string[](a string, b string)
	for x : a.char8s split-by-sublist b.char8s
		x as-string

lines string[](a string)
	a split-by "\n"

split-string record by-val
	left string
	middle char8
	right string

###
Find the first character where `f` returns `true`, and return the parts of `a` before and after it.
Returns an empty option if `f` does not returns `true` for any character in `a`.
###
try-split-once-by-fn split-string?(a string, f act bool(char8))
	if res ?= a.char8s try-split-once-by-fn f
		(res.left.as-string, res.middle, res.right.as-string),

# Finds the first whitespace character in `a` and returns the parts before and after it.
try-split-once-by-whitespace (string, string)?(a string)
	if x ?= a try-split-once-by-fn x => x is-whitespace
		(x.left, x.right),

###
Returns a string containing each element of `a`, with `b` as a separator.
This is `parts[0] ~ joiner ~ parts[1] ~ joiner ~ ... ~ parts[parts.size - 1]`.
###
join string(joiner string, parts string[])
	# TODO: NO CELl
	res cell string? = (),
	for part : parts
		*res := if cur ?= *res
			(cur ~~ joiner ~~ part),
		else
			part,
	*res ?? ""

# Lower cases every character in `a`.
lower-case string(a string)
	(for x : a char8s; x lower-case-char)::char8[] as-string

.lower-case-char char8(a char8) noctx
	diff = a.to-nat8 wrap-sub "A".to-nat8
	if diff < 26
		"a".to-nat8 + diff to-char8
	else
		a

# Capitalizes every character in `a`.
upper-case string(a string)
	(for x : a char8s; x upper-case-char)::char8[] as-string

.upper-case-char char8(a char8) noctx
	diff = a.to-nat8 wrap-sub "a".to-nat8
	if diff < 26
		"A".to-nat8 + diff to-char8
	else
		a

# String with characters in reverse order.
reverse string(a string)
	a.char8s.reverse as-string

# Replaces every instance of the substring `find` with `replace-with`.
replace string(a string, find string, replace-with string)
	# TODO:PERF
	if pair ?= a try-split-once find
		pair.a ~~ replace-with ~~ (pair.b replace find, replace-with)
	else
		a

region strip

# Strips whitespace from both ends.
strip string(a string)
	a.strip-left strip-right

# Strips whitespace from the front.
strip-left string(a string)
	a.char8s drop-while (x => x is-whitespace) as-string

# Removes all occurrences of 'b' from the front of 'a'.
strip-left string(a string, b string)
	if x ?= a try-remove-start b
		x strip-left b
	else
		a

# Strips whitespace from the end.
strip-right string(a string)
	a.char8s drop-right-while (x => x is-whitespace) as-string

# Removes all occurrences of 'b' from the end of 'a'.
strip-right string(a string, b string)
	if x ?= a try-remove-end b
		x strip-right b
	else
		a

.is-whitespace bool(a char8)
	a == " " || a == "\r" || a == "\n" || a == "\t"

normalize-newlines string(a string)
	a replace "\r\n", "\n" replace "\r", "\n"

region other

repeat string(a string, n nat64)
	a.char8s repeat n as-string

# Enclose a string in quotes and escape special characters.
quote string(a string)
	inner = "" join for x : a char8s
		if x == "\""
			"\\\""
		elif x == "\n"
			"\\n"
		elif x == "\r"
			"\\r"
		elif x == "\t"
			"\\t"
		elif x == "\\"
			"\\\\"
		else
			# TODO: "{x}"
			(x,)::char8[] as-string
	# TODO: "\"{inner}\""
	"\"" ~~ inner ~~ "\""

unindent string(a string)
	a replace "\t", ""
