no-std
import
	..number: +, -, *, ^, float, square-root
	..str: str
	..str-conv: finish, interp, to-str, with-str, with-value

###
2-dimensional vector.
Each element is a 64-bit float.
###
vec-2 record
	x float
	y float

# Vector addition -- adds corresponding elements.
+ vec-2(a vec-2, b vec-2)
	vec-2: a.x + b.x, a.y + b.y

# Vector subtraction -- subtracts corresponding elements.
- vec-2(a vec-2, b vec-2)
	vec-2: a.x - b.x, a.y - b.y

# Scales a vector by a scalar.
* vec-2(a vec-2, b float)
	vec-2: a.x * b, a.y * b

# Equivalent to `a.length ^ 2` (but more efficient).
length-squared float(a vec-2)
	a.x ^ 2 + a.y ^ 2

# Vector length, also known as the magnitude, norm, or |a|.
length float(a vec-2)
	a.length-squared square-root

to-str str(a vec-2)
	"<{a x}, {a y}>"
