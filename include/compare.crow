###
Functions for comparing things

All `data` values have an ordering.
(For non-`data`, you'll have to write your own comparison functions.
They must have names specific to the type like `ptr-less?`.)

For records, fields are compared in order.

For unions, a member type listed earlier is considered less than a member type listed later.

Restricting comparison to `data` means that it is ensured to be safe:

* If `a == b`, `a.f == b.f`.
* If `a < b` and `b < c`, `a < c`.
###

no-std
import
	.private.bool-low-level: ?, bool, false, not, true
	.private.bootstrap: str

# Comparison for `a < b`
less record
# Comparison for `a == b`
equal record
# Comparison for `a > b`
greater record
comparison union
	less
	equal
	greater

== bool(a comparison, b comparison) noctx
	match a
	as less
		match b
		as less
			true
		as equal
			false
		as greater
			false
	as equal
		match b
		as less
			false
		as equal
			true
		as greater
			false
	as greater
		match b
		as less
			false
		as equal
			false
		as greater
			true

equal<?a> spec
	== bool(a ?a, b ?a)

compare<?a> spec
	== bool(a ?a, b ?a)
	<=> comparison(a ?a, b ?a)

# True iff `a == b not`.
!= bool(a ?a, b ?a) noctx equal<?a>
	a == b not

# True iff `(a <=> b) == less`.
< bool(a ?a, b ?a) noctx compare<?a>
	match a <=> b
	as less
		true
	as equal
		false
	as greater
		false

# True iff `not: a > b`
<= bool(a ?a, b ?a) noctx compare<?a>
	b < a not

# True iff `(a <=> b) == greater`.
> bool(a ?a, b ?a) noctx compare<?a>
	b < a

# True iff `not: a < b`.
>= bool(a ?a, b ?a) noctx compare<?a>
	a < b not

# Returns the lesser argument
min ?a(a ?a, b ?a) noctx compare<?a>
	a < b ? a, b

# Returns the greater argument
max ?a(a ?a, b ?a) noctx compare<?a>
	a < b ? b, a

# Returns "less", "equal", or "greater"/
to-str str(a comparison)
	match a
	as less
		"less"
	as equal
		"equal"
	as greater
		"greater"
