no-std
import
	..bool: ?, bool, false, true
	..compare: !=, <, >, equal, min
	..exception: assert, forbid
	..fun: subscript
	..misc: ->, drop, void
	..opt: ==, empty?, has?, none, opt, some
	..ptr: +, -, ptr, ptr-eq?, set-subscript, subscript
	..str: str
	..test: is
	..writer: ~=, backup!, move-to-str!, writer
	..private.bootstrap: hard-assert, hard-unreachable
	..private.number-low-level: ==, <=>, nat, wrap-add, wrap-sub
	.arr: ==, arr, arr-equal?, begin-ptr, end-ptr, empty?, size, unsafe-slice
	.arr-util: each, each-with-index, first, first-some, fold, last, rtail
	.mut-arr:
		begin-ptr, cast-immutable, copy-from!, end-ptr, mut-arr, mut-arr-from-begin-end,
		set-subscript, size, subscript, uninitialized-mut-arr

###
Immutable list type.
Appending a value with `~` is amortized O(1).
Popping a value with `rtail` and `last` is always O(1).
###
list<?a> record
	# TODO:PRIVATE
	ignore void
	prev opt list ?a
	values arr ?a

# Empty list.
list list ?a()
	list: []

###
Convert an arr to a list.
This is O(1).
###
list list ?a(a arr ?a)
	list: void, none, a

# TODO:KILL
list-equal? bool(a list ?a, b list ?a, eq act bool(?a, ?a))
	a.arr arr-equal? b.arr, eq

# TODO: should be generic
list-equal? bool(a list ?a, b list ?a) trusted noctx equal<?a>
	if a.size == b.size
		match a equal-recur b
		as not-equal
			false
		as left-longer ll
			hard-assert: ll.remainder empty?
			true
		as right-longer
			hard-unreachable
	else
		false

.equal-with-remainder<?a> union
	not-equal
	left-longer<?a>
	right-longer<?a>
.not-equal record
.left-longer<?a> record
	by-val
	remainder arr ?a
.right-longer<?a> record
	by-val
	remainder arr ?a

.mirror equal-with-remainder ?a(a equal-with-remainder ?a) noctx
	match a
	as not-equal
		not-equal
	as left-longer ll
		ll.remainder right-longer
	as right-longer rl
		rl.remainder left-longer

.equal-recur equal-with-remainder ?a(a list ?a, b list ?a) unsafe noctx equal<?a>
	if ap ?= a prev
		if bp ?= b prev
			# Problem: The remainder could be a lit that ends partially...
			match ap equal-recur bp
			as not-equal
				not-equal
			as left-longer ll
				ll.remainder equal-with-remainder a.values, b.values
			as right-longer rl
				rl.remainder equal-with-remainder b.values, a.values mirror
		else
			match ap equal-recur b
			as not-equal
				not-equal
			as left-longer
				# Shouldn't happen if they are the same total size
				hard-unreachable
			as right-longer rl
				a.values equal-with-remainder rl.remainder
	elif bp ?= b prev
		match a equal-recur bp
		as not-equal
			not-equal
		as left-longer ll
			# Shouldn't happen if they are the same total size
			ll.remainder equal-with-remainder b.values
		as right-longer
			hard-unreachable
	else
		a.values equal-with-remainder b.values

# WARN: For simplicity, this returns empty 'remainder'
.equal-with-remainder equal-with-remainder ?a(a arr ?a, b arr ?a) unsafe noctx equal<?a>
	size = a.size min b.size
	if (a unsafe-slice 0, size) arr-equal? (b unsafe-slice 0, size)
		if a.size < size
			b unsafe-slice size, b.size right-longer
		else
			a unsafe-slice size, a.size left-longer
	else
		not-equal

.equal-with-remainder equal-with-remainder ?a(a0 arr ?a, a1 arr ?a, b arr ?a) unsafe noctx equal<?a>
	hard-assert: a0.size < b.size
	if a0 arr-equal? (b unsafe-slice 0, a0.size)
		a1 equal-with-remainder: b unsafe-slice a0.size, b.size
	else
		not-equal

###
Copy a list to an arr.
This is O(n).
###
arr arr ?a(a list ?a) trusted
	res = a.size uninitialized-mut-arr<?a>
	res.end-ptr write-out! a
	res cast-immutable

###
Append a value to the list.
This is amortized O(1).
This is because about every 2 steps it allocates an array of size 2,
every 4 steps it allocates an array of size 4, etc.
###
~ list ?a(a list ?a, value ?a) trusted
	res = a push-recur 1
	res.values[res.values.size wrap-sub 1] := value
	list: void, res.prev-list, res.values.cast-immutable

.list-builder<?a> record mut
	prev-list opt list ?a
	values mut-arr ?a

.push-recur list-builder ?a(a list ?a, size-right nat) unsafe
	if a.values.size > size-right
		list-builder: a.some, size-right.uninitialized-mut-arr
	else
		new-size-right = size-right wrap-add a.values.size
		res = if prev ?= a prev
			prev push-recur new-size-right
		else
			list-builder<?a>: none, new-size-right.uninitialized-mut-arr
		offset = res.values.size wrap-sub size-right wrap-sub a.values.size
		res.values[offset ->: offset wrap-add a.values.size] copy-from! a.values
		res

# True iff the list is empty.
empty? bool(a list ?a)
	res = a.values empty?
	if res
		assert: a.prev empty?
	res

###
Oldest appended value.
This is O(a.size ln)
###
first opt ?a(a list ?a)
	if prev ?= a prev
		prev first
	else
		a.values first

###
Most recently appended value.
This is O(1).
###
last opt ?a(a list ?a)
	a.values last

###
All but the last value.
This is O(1).
Throws if the list is empty.
###
rtail list ?a(a list ?a)
	forbid: a.values.size == 0
	if a.values.size == 1
		if prev ?= a prev
			prev
		else
			list
	else
		list: void, a.prev, a.values.rtail
		
###
Number of elements in the list.
This is O(ln a.size).
###
size nat(a list ?a) noctx
	a.values.size size-recur a.prev
.size-recur nat(acc nat, a opt list ?a) noctx
	if node ?= a
		acc wrap-add node.size size-recur node.prev
	else
		acc

###
Concatenate an array to a list.
This is O(1).
###
~ list ?a(a list ?a, b arr ?a)
	list: void, (a.empty? ? none, a.some), b

###
Concatenate two lists.
This is O(n).
###
~ list ?a(a list ?a, b list ?a) trusted
	a-size = a size
	res = a-size wrap-add b.size uninitialized-mut-arr<?a>
	res.end-ptr write-out! b
	res.begin-ptr + a-size write-out! a
	list: res.cast-immutable

.write-out! void(end ptr ?a, a list ?a) unsafe
	new-end = end - a.values.size
	new-end mut-arr-from-begin-end end copy-from! a.values
	if prev ?= a prev
		new-end write-out! prev

region iteration

# Call a function for each element in the list.
each void(a list ?a, f act void(?a))
	if prev ?= a prev
		prev each f
	a.values each f

# Call a function for each element in the list and accumulate.
fold ?a(acc ?a, a list ?b, f act ?a(?a, ?b))
	v = if prev ?= a prev
		acc fold prev, f
	else
		acc
	v fold a.values, f

# Produce a list with `f[x]` applied to each element `x` in the input.
map list ?out(a list ?in, f act ?out(?in)) trusted
	res = a.size uninitialized-mut-arr<?out>
	drop: res.begin-ptr map-recur a, f
	list: res.cast-immutable
.map-recur ptr ?out(out ptr ?out, a list ?in, f act ?out(?in)) unsafe
	next-out = if prev ?= a prev
		out map-recur prev, f
	else
		out
	a.values each-with-index \value, index
		next-out[index] := f[value]
	next-out + a.values.size

filter list ?a(a list ?a, f act bool(?a)) trusted
	res = a.size uninitialized-mut-arr<?a>
	end = res.begin-ptr filter-recur a, f
	list: res[0 -> end - res.begin-ptr] cast-immutable
.filter-recur ptr ?a(out ptr ?a, a list ?a, f act bool(?a)) unsafe
	new-out = if prev ?= a prev
		out filter-recur prev, f
	else
		out
	new-out filter-recur-arr a.values.begin-ptr, a.values.end-ptr, f
.filter-recur-arr ptr ?a(out ptr ?a, cur ptr ?a, end ptr ?a, f act bool(?a)) unsafe
	if cur ptr-eq? end
		out
	else
		new-out = if f[cur[]]
			out[] := cur[]
			out + 1
		else
			out
		new-out filter-recur-arr cur + 1, end, f

first-some opt ?out(a list ?in, f act opt ?out(?in))
	before = if prev ?= a prev
		prev first-some f
	else
		none

	if before has?
		before
	else
		a.values first-some f

.dbg str(a list nat)
	res = writer
	res dbg-recur! a
	res move-to-str!
.dbg-recur! void(out writer, a list nat)
	if prev ?= a prev
		out dbg-recur! prev
		out ~= " "
	out ~= "["
	if a.values empty?
		out ~= "]"
	else
		a.values each \x
			out ~= x
			out ~= " "
		out backup!
		out ~= "]"

test
	a = list<nat>
	assert: a empty?
	a.arr is []
	a.dbg is "[]"
	a.last is none

	b = a ~ 1
	b.dbg is "[1]"
	b.last is 1.some
	b.rtail.arr is []
	forbid: b empty?

	c = b ~ 2
	c.dbg is "[1 2]"
	c.last is 2.some
	c.rtail.arr is [1]

	d = c ~ 3
	d.dbg is "[1 2] [3]"
	d.last is 3.some
	d.rtail.arr is [1, 2]

	e = d ~ 4
	e.dbg is "[1 2 3 4]"
	e.last is 4.some
	e.rtail.arr is d.arr
	e.size is 4

	e56 = e ~ [5, 6]
	e56.dbg is "[1 2 3 4] [5 6]"
	(c ~ d).dbg is "[1 2 1 2 3]"

	e2 = e map {it wrap-add 10}
	e2.dbg is "[11 12 13 14]"

	e3 = e filter {it != 2}
	e3.dbg is "[1 3 4]"

	folded = 5 fold e, \x, y
		x wrap-add y
	folded is 15

	# TODO: compiler crashes without a type argument here
	(e first-some<str, nat> {it == 3 ? "three".some, none}) is "three".some
	(e first-some<str, nat> {it == 5 ? "five".some, none}) is none
