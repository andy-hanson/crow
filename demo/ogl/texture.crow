no-std
import
	crow/cell: *, as-ptr, cell, new
	crow/compare: !=
	crow/exception: throw
	crow/io/print: print
	crow/misc: void
	crow/number: to-int32
	crow/ptr: as-const, ptr-to
	crow/str: str
	crow/str-conv: finish, interp, to-str, with-str, with-value
	GLES2/gl2:
		==, glBindTexture, glDeleteTextures, glGetError, glGenTextures, GL_LINEAR, GL_NO_ERROR,
		GL_RGBA, glTexImage2D, glTexParameteri, GL_TEXTURE_MAG_FILTER, GL_TEXTURE_MIN_FILTER,
		GL_TEXTURE_2D, GLuint, GL_UNSIGNED_BYTE, to-str
	./load-webp: height, image-data, width, with-webp

# Remember to call 'free-texture'
load-texture GLuint(path str) unsafe summon
	path with-webp image =>
		texture-cell cell GLuint = 0,
		1 glGenTextures texture-cell.as-ptr
		texture = *texture-cell

		GL_TEXTURE_2D glBindTexture texture
		w = image.width to-int32
		h = image.height to-int32
		image-data = image.image-data as-const
		print: "width is {w}, height is {h}"
		GL_TEXTURE_2D glTexImage2D 0, GL_RGBA, w, h, 0, GL_RGBA, GL_UNSIGNED_BYTE, image-data

		err = glGetError
		if err != GL_NO_ERROR
			"Error loading image {path}: code {err}" throw

		GL_TEXTURE_2D glTexParameteri GL_TEXTURE_MAG_FILTER, GL_LINEAR
		GL_TEXTURE_2D glTexParameteri GL_TEXTURE_MIN_FILTER, GL_LINEAR

		texture

free-texture void(tex-name GLuint) unsafe summon
	1 glDeleteTextures tex-name.ptr-to
