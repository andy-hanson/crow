no-std
import
	crow/c-types: c-int
	crow/cell: cell, subscript
	crow/col/arr: begin-ptr, size
	crow/exception: finally
	crow/fun: subscript
	crow/io/file: read-file-binary
	crow/misc: void
	crow/number: nat, nat8, to-nat64
	crow/ptr: ptr
	crow/str: str
	webp/decode: WebPDecodeRGBA
	webp/types: WebPFree

# WARN: Remember to free
webp-rgba-image record mut
	data ptr nat8
	width nat
	height nat

with-webp<a> a(path str, cb act a(webp-rgba-image)) unsafe summon
	image = path load-webp
	(() => image free-webp) finally () => cb[image]

.load-webp webp-rgba-image(path str) unsafe summon
	compressed = path read-file-binary
	width = 0 cell<c-int>
	height = 0 cell<c-int>
	uncompressed = compressed.begin-ptr WebPDecodeRGBA compressed.size, width, height
	uncompressed webp-rgba-image width[].to-nat64, height[].to-nat64

.free-webp void(a webp-rgba-image) unsafe summon
	a.data WebPFree


