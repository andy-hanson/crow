#!/usr/bin/env crow
import
	crow/io/path: get-extension
	crow/io/print: out, print
	audio/read-sound-file: read-sound-file
	audio/write-sound-file: flac, ogg-vorbis, sound-file-format, wav, write-sound-file
	audio/sound: format, to, with-sample-rate

main nat(args string[]) summon, (native, sndfile) extern
	assert args.size == 2 : "Usage: crow convert-file.crow toccata.ogg toccata.flac" error
	in-path = args[0]
	out-path = args[1]
	ext = out-path.get-extension ?? throw "Output file needs extension" error
	format sound-file-format = default-file-types[ext to] ?? throw describe-out-exts error

	sound = in-path.read-sound-file with-sample-rate 48000
	out print "sound format is {sound format}"
	out-path write-sound-file sound, format
	0

-describe-out-exts string()
	supported = ", " join for key, _ : default-file-types; key to
	"Supported output file extensions: {supported}"

-default-file-types sound-file-format[symbol]()
	("flac", flac), ("ogg", ogg-vorbis), ("wav", wav)
