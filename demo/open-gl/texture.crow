import
	crow/cell: *, as-mut-ptr, cell, new
	crow/ptr: as-const, ptr-to
	GLES2/gl2:
		==, glBindTexture, glDeleteTextures, glGetError, glGenTextures, GL_LINEAR, GL_NO_ERROR,
		GL_RGBA, glTexImage2D, glTexParameteri, GL_TEXTURE_MAG_FILTER, GL_TEXTURE_MIN_FILTER,
		GL_TEXTURE_2D, GLuint, GL_UNSIGNED_BYTE, to-str
	./load-webp: height, image-data, width, with-webp

# Remember to call 'free-texture'
load-texture GLuint(compressed nat8[]) unsafe summon
	compressed with-webp image =>
		texture-cell cell GLuint = 0,
		1 glGenTextures texture-cell.as-mut-ptr
		texture = *texture-cell

		GL_TEXTURE_2D glBindTexture texture
		w = image.width to-int32
		h = image.height to-int32
		image-data = image.image-data as-const
		GL_TEXTURE_2D glTexImage2D 0, GL_RGBA, w, h, 0, GL_RGBA, GL_UNSIGNED_BYTE, image-data

		err = glGetError
		unless err == GL_NO_ERROR
			"Error loading image: code {err}" throw

		GL_TEXTURE_2D glTexParameteri GL_TEXTURE_MAG_FILTER, GL_LINEAR
		GL_TEXTURE_2D glTexParameteri GL_TEXTURE_MIN_FILTER, GL_LINEAR

		texture

free-texture void(tex-name GLuint) unsafe summon
	1 glDeleteTextures tex-name.ptr-to
