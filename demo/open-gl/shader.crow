import
	OpenGL/gl
	OpenGL/headers

shader-prog-load GLuint(
	gl gl-ext,
	vert-shader-source string,
	frag-shader-source string,
) summon, unsafe, native extern
	vert-shader = gl shader-load "vertex shader", vert-shader-source, GL_VERTEX_SHADER
	frag-shader = gl shader-load "fragment shader", frag-shader-source, GL_FRAGMENT_SHADER
	shader-prog GLuint = gl glCreateProgram
	assert shader-prog != 0
	gl glAttachShader shader-prog, vert-shader
	gl glAttachShader shader-prog, frag-shader
	gl glLinkProgram shader-prog
	linking-succeeded mut GLint = GL_FALSE
	gl glGetProgramiv shader-prog, GL_LINK_STATUS, &linking-succeeded
	gl glDeleteShader vert-shader
	gl glDeleteShader frag-shader
	if linking-succeeded == GL_TRUE
		shader-prog
	else
		log-length mut GLint = 0
		gl glGetProgramiv shader-prog, GL_INFO_LOG_LENGTH, &log-length
		err-log GLchar mut-array = log-length.to gc-safe-mut-array
		gl glGetProgramInfoLog shader-prog, log-length, &log-length, err-log.begin-pointer
		gl glDeleteProgram shader-prog
		throw "Loading shaders" gl-error err-log.to::(char8 array).to

shader-prog-destroy void(gl gl-ext, shader-prog GLuint) summon, unsafe
	gl glDeleteProgram shader-prog

-shader-load GLuint(
	gl gl-ext,
	name string,
	source string,
	shader-type GLenum,
) summon, unsafe, native extern
	shader = gl create-shader-from-source source, shader-type
	gl glCompileShader shader
	compile-succeeded mut GLint = GL_FALSE
	gl glGetShaderiv shader, GL_COMPILE_STATUS, &compile-succeeded
	if compile-succeeded == GL_TRUE
		shader
	else
		log-length mut GLint = 0
		gl glGetShaderiv shader, GL_INFO_LOG_LENGTH, &log-length
		err-log GLchar mut-array = log-length.to gc-safe-mut-array
		gl glGetShaderInfoLog shader, log-length, &log-length, err-log.begin-pointer
		gl glDeleteShader shader
		throw "Compiling GLSL {name}" gl-error err-log.to::(char8 array).to

-create-shader-from-source GLuint(
	gl gl-ext,
	source string,
	shader-type GLenum,
) summon, unsafe, native extern
	shader = gl glCreateShader shader-type
	shader-sources c-string array = source.to,
	gl glShaderSource shader, shader-sources.size.to, shader-sources.begin-pointer, null
	shader
