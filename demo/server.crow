#!/usr/bin/env crow
import
	crow/io/net/http-server: http-handler, http-server, with-block
	crow/io/net/http-types
	crow/io/stdin: read-stdin

main void() summon, native extern
	port nat16 = 8080
	handler http-handler = req =>
		descr = req describe-request
		info log descr
		200, "OK", (), "Your request:\n{descr}".to-bytes
	with _ : port http-server handler
		info log "Running at http://localhost:{port}/ ; press enter to stop"
		_ = read-stdin

-describe-request string(a http-request)
	headers = "\n" join for k, v : a headers
		"{k}: {v}"
	"{a method} {a path}{a query}\n{headers}\n\n{a.body as-string}"

show string(a string[symbol])
	if a is-empty
		""
	else
		"?" ~~ ("&" join for k, v : a; "{k}={v}")
