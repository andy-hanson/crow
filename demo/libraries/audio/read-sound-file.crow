import
	crow/private/alloc: mem-copy
	sndfile/headers:
		channels, frames, new, samplerate, sf_close, sf_count_t, sf_error, SF_INFO, SFM_READ,
		sf_open, sf_open_virtual, sf_read_float, SF_VIRTUAL_IO, SNDFILE
	sndfile/util: check-sndfile-error
	system/stdio: SEEK
	./sound: new, sound

+read-sound-file sound(path string) summon
	trusted
		info mut SF_INFO = ()
		f = path.to sf_open SFM_READ, &info
		f common info

+sound-from-file-content sound(content nat8 array)
	trusted
		info mut SF_INFO = ()
		user-data mut user-data = content, content.begin-pointer
		virtual SF_VIRTUAL_IO = &get_filelen, &seek, &read, &write, &tell
		f = &virtual sf_open_virtual SFM_READ, &info, (&user-data)::user-data mut*.pointer-cast
		f common info

-common sound(f SNDFILE mut*, info SF_INFO) unsafe
	f.sf_error check-sndfile-error
	n-channels nat64 = info.channels to
	sample-rate nat64 = info.samplerate to
	samples float32 mut-array = info.frames * n-channels uninitialized-mut-array
	n-read = f sf_read_float samples.begin-pointer, samples.size
	f.sf_close check-sndfile-error
	assert n-read == samples.size
	(n-channels, sample-rate), samples.cast-immutable

-get_filelen sf_count_t(user-data-pointer any-mut-pointer) bare, unsafe
	(user-data-pointer.pointer-cast::user-data mut*)->content size

-seek sf_count_t(offset sf_count_t, whence SEEK, user-data-pointer any-mut-pointer) bare, unsafe
	ap user-data mut* = user-data-pointer pointer-cast
	a = *ap
	start = match whence
	as SEEK_SET
		a.content begin-pointer
	as SEEK_CUR
		a cur
	as SEEK_END
		a.content end-pointer
	pos = start + offset
	assert a.content.begin-pointer <= pos && pos <= a.content.end-pointer
	*ap := a.content, pos
	pos - a.content.begin-pointer

-read sf_count_t(
	pointer nat8 mut*,
	count sf_count_t,
	user-data-pointer any-mut-pointer,
) bare, unsafe
	ap user-data mut* = user-data-pointer pointer-cast
	a = *ap
	n-to-read = count min (a.content.end-pointer - a.cur)
	pointer mem-copy a.cur, n-to-read
	*ap := a.content, a.cur + n-to-read
	n-to-read

-write sf_count_t(_ nat8*, _ sf_count_t, _ any-mut-pointer) bare, unsafe
	unreachable

-tell sf_count_t(user-data-pointer any-mut-pointer) bare, unsafe
	a user-data mut* = user-data-pointer pointer-cast
	a->cur - a->content.begin-pointer

-user-data record(content nat8 array, cur nat8*) by-val, mut
