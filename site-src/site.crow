import
	crow/col/dict: to-dict
	crow/io/file: current-directory, make-directory, make-symlink-to, path-exists, read-file, try-read-file, write-file
	crow/io/path: child-path, parent-path
	crow/io/print: print
	crow/json: json-object, json-str, to-str
	crow/str-util: ends-with, try-remove-start
	./document: documentation
	./pages: pages
	./path-and-content: content, out-path, new, path-and-content
	./search-dir-recursive: remove-include-and-crow, search-dir-recursive

main fut nat(_ str[]) summon
	all-content = documentation ~~ pages ~ include-all
	for content : all-content
		content ensure-up-to-date
	cwd = current-directory
	for to-link : all-to-link
		to-link ensure-link cwd
	()

.ensure-up-to-date void(a path-and-content) summon
	full-out-path = "site" child-path a.out-path
	unless full-out-path is-up-to-date a.content
		full-out-path.parent-path ensure-directory-recursive
		full-out-path write-file a.content

.ensure-link void(a link, cwd str) summon
	full-out-path = "site" child-path a.link-path
	# TODO: check that link target is up to date
	unless full-out-path path-exists
		full-out-path.parent-path ensure-directory-recursive
		full-target = cwd child-path a.target
		print: "link {full-out-path} -> {full-target}"
		full-out-path make-symlink-to full-target

.is-up-to-date bool(path str, content str) summon
	if cur-content ?= path try-read-file
		cur-content == content

.ensure-directory-recursive void(path str) summon
	unless path == "" || path.path-exists
		path.parent-path ensure-directory-recursive
		path make-directory

link record
	target str
	link-path str

.all-to-link link[]() summon
	example = "site-src/example".search-dir-recursive to-links "site-src/"
	in-src-paths link[] = for x : ("crow.svg", "index.css")::str[]
		("site-src" child-path x), x
	script = ("site-src/script" search-dir-recursive x => x ends-with ".js") to-links "site-src/"
	font = "site-src/font".search-dir-recursive to-links "site-src/"
	other link[] = for x : "bin/crow.wasm", "bin/crow.tar.xz", "crow-js/crow.js"
		x, x
	example ~~ in-src-paths ~~ script ~~ font ~~ other

.include-all path-and-content() summon
	include = "include" search-all-with-content ""
	content = (for x : include; x.out-path.remove-include-and-crow, x.content.json-str).to-dict.json-object to-str
	"include-all.json", content

.search-all-with-content path-and-content[](path str, remove-prefix str) summon
	path.search-dir-recursive to-paths-and-contents remove-prefix

.to-links link[](targets str[], remove-prefix str)
	for target : targets
		target, target try-remove-start remove-prefix ?? throw "{target} didn't start with {remove-prefix}"

.to-paths-and-contents path-and-content[](paths str[], remove-prefix str) summon
	for path : paths
		if rel-path ?= path try-remove-start remove-prefix
			rel-path, path.read-file
		else
			throw "{path} didn't start with {remove-prefix}"
