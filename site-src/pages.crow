import
	crow/io/file: read-file
	crow/parse
	./page-template: page-template
	./path-and-content: path-and-content

pages path-and-content[]^() summon
	index path-and-content = "index.html" page-template "index", "site-src/index.html".read-file
	download path-and-content = "download.html" page-template "download", "site-src/download.html".read-file
	(index, download) ~~ tutorial-pages,

tutorial-pages path-and-content[]() summon
	metas = tutorial-metas
	for index : 0 .. metas.size
		meta = metas[index]
		next tutorial-meta? = unless index + 1 == metas.size
			metas[index + 1],
		meta.path tutorial-template meta.title, "site-src/tutorial/{meta key}.html".read-file, next

-tutorial-metas tutorial-meta[]()
	input = """index: hello, world!
	if: functions, 'if', and locals
	types: basic types
	infer: type inference
	option: optional values
	collection: collections
	record: records
	enum: enums and flags
	union: unions
	fun: function values
	future: futures and function references
	template: templates
	spec: specs
	module: modules
	extern: extern functions
	next: where from here?"""
	name symbol grammar = word map x => x to
	line tutorial-meta grammar = (name ~~ ": " ~~ "\n".text-not-containing) map pair =>
		pair.a, pair.b
	lines tutorial-meta[] grammar = line separated-by "\n\t"
	(lines parse input)!

-tutorial-meta record
	key symbol
	title string

path string(a tutorial-meta)
	"tutorial/{a key}.html"

tutorial-nav string(cur-path string)
	lines = "" join for x : tutorial-metas
		key = x key
		path = x path
		name symbol = key == "index" ? "syntax" : key
		cur = path == cur-path ? " class=\"current\""
		"""<li><a href="/{path}"{cur}>{name}</a></li>"""
	"<nav><ul>{lines}</ul></nav>"

-tutorial-template path-and-content(page-path string, title string, content string, next tutorial-meta?)
	next-link = if n ?= next
		"""<a id="next" href="/{n path}">next: {n title}</a>"""
	page-path page-template title, """
	<div id="side-nav-wrapper">
		{page-path tutorial-nav}
		<main>
			<h1>{title}</h1>
			{content}
			{next-link}
		</main>
	</div>"""
