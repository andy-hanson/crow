import
	crow/io/duration: duration, milliseconds
	crow/js
	crow/js/dom

to-base-64 string(a string) trusted, js extern
	"btoa".js-global call a cast

from-base-64 string(a string) trusted, js extern
	"atob".js-global call a cast

fetch-js-object js-any(url string) trusted, js extern
	("fetch".js-global call url).await call-property "json" await

debouncer record mut
	-msec float
	-timeout mut js-any
new debouncer(time duration) trusted, js extern
	time.milliseconds, null
with-block void(a debouncer, action void mut()) trusted, js extern
	_ = "clearTimeout".js-global call a.timeout
	a.timeout := "setTimeout".js-global call action, a.msec

get-child-text string(node node) trusted, js extern
	child-nodes = node child-nodes
	assert child-nodes.size == 1
	child-nodes[0] get-text-from-node
-get-text-from-node string(node node) trusted, js extern
	assert node.as-js instanceof "Text".js-global
	node.as-js["data"].cast reduce-indent

-reduce-indent string(a string)
	# Find first non-whtespace
	a replace-all "\n{"\t"::string repeat a.count-indent}", "\n" strip
-count-indent nat(a string)
	i mut nat = 0
	for x : a
		match x
		as "\n"
			i := 0
			continue
		as "\t"
			i +:= 1
			continue
		else
			break i
	else
		0
