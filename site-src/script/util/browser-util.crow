import
	crow/js: ==, as-js, as-t, await, call-new, call-property, get, js-any, js-global, null, set
	./js-util: new

copy-text-to-clipboard void(text string) summon, trusted, js extern
	_ = "navigator".js-global get "clipboard" call-property "writeText", text await

download-text void(name string, text string) summon, trusted, js extern
	link = "document".js-global call-property "createElement", "a"::string
	blob = "Blob".js-global call-new (text,)::(string array), \
		(("type", "text/crow"::string.as-js),)::js-any
	url = "URL".js-global call-property "createObjectURL", blob
	link set "href", url
	link set "download", name
	_ = link call-property "click"

get-url-search string?(name string) summon, trusted, js extern
	res = "URLSearchParams".js-global call-new location.search call-property "get", name
	unless res == null
		res.as-t,

location record
	-inner js-any
to string(a location) trusted, js extern
	a.inner call-property "toString" as-t
location location() summon, trusted, js extern
	window = "window" js-global
	(window get "location"),
origin string(a location) trusted, js extern
	a.inner get "origin" as-t
pathname string(a location) trusted, js extern
	a.inner get "pathname" as-t
search string(a location) trusted, js extern
	a.inner get "search" as-t
hash string(a location) trusted, js extern
	a.inner get "hast" as-t

push-history void(url string) trusted, js extern
	history = "history" js-global
	_ = history call-property "pushState", (("path", url.as-js),)::js-any, ""::string, url
