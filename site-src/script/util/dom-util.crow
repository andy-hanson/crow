import
	crow/js:
		==, as-js, as-t, await, call-new, call-property, call-property-spread, get, js-any, js-cast,
		js-global, null, set
	./js-util: make-class, method

node extern

~= void(a node, b node) trusted, js extern
	_ = a.as-js call-property "appendChild", b
~~= void(a node, b node[]) js extern
	for x : b
		a ~= x

+create-node node(tag-name symbol) trusted, js extern
	"document".js-global call-property "createElement", tag-name as-t
+create-node node(tag-name symbol, attr string[symbol]) trusted, js extern
	node = tag-name create-node
	for key, value : attr
		_ = node.as-js call-property "setAttribute", key, value
	node
+create-node node(
	tag-name symbol,
	attr string[symbol],
	class-name string,
	children node[],
) trusted, js extern
	node = tag-name create-node attr
	node.as-js set "className", class-name
	_ = node.as-js call-property-spread "append", children.to
	node js-cast

+create-button node(
	title string,
	class-name string,
	children node[],
	on-click void mut(),
) trusted, js extern
	res = "button" create-node (("title", title),), class-name, children
	res.as-js set "onclick", on-click
	res

event extern

+create-input-text node(on-input void mut(x event), value string) trusted, js extern
	res = "input" create-node (("type", "text"), ("value", value))
	_ = res.as-js call-property "addEventListener", "input"::string, on-input
	res

+create-div node() js extern
	"" create-div
+create-div node(class-name string) js extern
	class-name create-div ()
+create-div node(class-name string, children node[]) js extern
	"div" create-node (), class-name, children

+create-span node(class-name string, children node[]) js extern
	class-name create-span (), children
+create-span node(class-name string, attr string[symbol], children node[]) js extern
	"span" create-node attr, class-name, children

+create-text-node node(text string) trusted, js extern
	"document".js-global call-property "createTextNode", text as-t

region query

+attributes record(node node) nominal, mut
+subscript string?(a attributes, attr-name symbol) trusted, js extern
	res = a.node.as-js call-property "getAttribute", attr-name
	unless res == null
		res.as-t,

+first-child node?(a node) trusted, js extern
	res = a.as-js get "firstChild"
	unless res == null
		res.as-t,

region mutate

+remove-all-children void(a node) trusted, js extern
	while child ?= a first-child
		_ = a.as-js call-property "removeChild", child

+query-selector node array(selector string) trusted, js extern
	"document".js-global call-property "querySelectorAll", selector array-of-node-list

+child-nodes node array(a node) trusted, js extern
	a.as-js get "childNodes" array-of-node-list

-array-of-node-list node array(node-list js-any) trusted, js extern
	"Array".js-global call-property "from", node-list as-t

region Custom elements

+define-custom-element void(element-name string, connected connected-callback) summon, js extern
	element-name define-custom-element "", connected
+define-custom-element void(
	element-name string,
	style-sheet string,
	connected connected-callback,
) summon, js extern
	element-name define-custom-element (_ => style-sheet), connected
+define-custom-element void(
	element-name string,
	style-sheet string mut(this custom-element-node),
	connected connected-callback,
) summon, trusted, js extern
	constructor method = (this, _) =>
		# Using JSON.parse to make sure it's synchronous
		args = "JSON".js-global call-property "parse", "\{\"mode\":\"open\"}"::string
		root = this call-property "attachShadow", args
		root.as-t set-style-sheet style-sheet[this as-t] as-js
	connected-callback method = (this, _) => connected[this as-t] as-js
	methods method[symbol] = ("connectedCallback", connected-callback),
	cls = "HTMLElement".js-global make-class constructor, methods
	_ = "customElements".js-global call-property "define", element-name, cls

+connected-callback alias
	void mut(this custom-element-node)

+custom-element-node extern
+as-node node(a custom-element-node) trusted, js extern
	a js-cast
+attributes attributes(a custom-element-node) js extern
	a.as-node attributes
+shadow-root node(a custom-element-node) trusted, js extern
	a.as-js get "shadowRoot" as-t

-set-style-sheet void(shadow-root node, css string) trusted, js extern
	style = "CSSStyleSheet".js-global call-new
	_ = style call-property "replace", css await
	_ = shadow-root.as-js set "adoptedStyleSheets", (style,)::(js-any array)
