import
	crow/col/arr-util: map
	crow/io/environ: get-environ
	crow/io/process: exit-code, spawn-and-wait-result, stderr, stdout
	crow/json: as-arr, as-bool, as-str, json, parse-json, subscript
	./module:
		alias-decl, builtin-decl, enum-decl, extern-ptr-decl, flags-decl, fun-decl, module,
		module-export, module-export-value, new, param, p-data, p-mut, p-sendable, purity,
		spec-decl, spec-ref, type-param, type-ref, record-decl, record-field, spec-builtin,
		spec-sig, spec-sigs, union-decl, union-member

get-modules module[](paths str[]) summon
	result = "bin/crow" spawn-and-wait-result (("doc",) ~ paths), get-environ
	unless result.exit-code == 0 && result.stderr.is-empty
		"Could not get documentation: exit code {result exit-code}, stderr: {result stderr}" throw
	result.stdout.parse-json! to-modules

.to-modules module[](a json)
	a.get-node-type is "root"
	a["modules"]!.as-arr! map x => x to-module

.to-module module(a json)
	a.get-node-type is "module"
	exports = a["exports"]!.as-arr! map x => x to-export
	a["path"]!.as-str!, a.get-comment, exports

.get-comment str(a json)
	if c ?= a["comment"]
		c.as-str!

.get-node-type str(a json)
	a["_type"]!.as-str!

.to-export module-export(a json)
	a.get-node-type is "export"
	type-params = if t ?= a["type-params"]
		t.as-arr! map x => x to-type-param
	value = a["value"]! to-export-value
	a.get-name, a.get-comment, type-params, value

.to-export-value module-export-value(a json)
	type = a get-node-type
	if type == "alias"
		a.to-alias-decl alias-decl
	elif type == "builtin"
		builtin-decl
	elif type == "enum"
		a.to-enum-decl enum-decl
	elif type == "extern-ptr"
		extern-ptr-decl
	elif type == "flags"
		a.to-flags-decl flags-decl
	elif type == "fun"
		a.to-fun-decl fun-decl
	elif type == "spec"
		a.to-spec-decl spec-decl
	elif type == "record"
		a.to-record-decl record-decl
	elif type == "union"
		a.to-union-decl union-decl
	else
		"unexpected export type {type}" throw

.to-alias-decl alias-decl(a json)
	target = a["target"]! to-type-ref
	target,

.to-enum-decl enum-decl(a json)
	a.get-enum-flags-members,

.to-flags-decl flags-decl(a json)
	a.get-enum-flags-members,

.get-enum-flags-members sym[](a json)
	a["members"]!.as-arr! map x => x.as-str! str-to-sym

.to-record-decl record-decl(a json)
	has-private = if h ?= a["has-private"]
		h.as-bool!
	fields = a["fields"]!.as-arr! map x => x to-record-field
	a.get-purity, has-private, fields

.get-purity purity(a json)
	if p ?= a["purity"]
		s = p.as-str!
		if s == "sendable"
			p-sendable
		else
			s is "mut"
			p-mut
	else
		p-data

.to-record-field record-field(a json)
	a.get-node-type is "field"
	is-mut = if m ?= a["mut"]
		m.as-bool!
	a.get-name, a.get-type, is-mut

.to-union-decl union-decl(a json)
	members = a["members"]!.as-arr! map x => x to-union-member
	a.get-purity, members

.to-union-member union-member(a json)
	a.get-node-type is "member"
	type type-ref? = if t ?= a["type"]
		t.to-type-ref,
	a.get-name, type

.get-name sym(a json)
	a["name"]!.as-str! str-to-sym

.to-type-param type-param(a json)
	a.get-name,

.get-type type-ref(a json)
	a["type"]! to-type-ref

.get-type-args type-ref[](a json)
	if args ?= a["type-args"]
		args.as-arr! map x => x to-type-ref

.to-fun-decl fun-decl(a json)
	variadic = if v ?= a["variadic"]
		v.as-bool!
	specs = if s ?= a["specs"]
		s.as-arr! map (x => x to-spec-ref)
	a.get-return-type, variadic, a.get-params, specs

.to-spec-decl spec-decl(a json)
	body-json = a["body"]!
	type = body-json get-node-type
	body_ = if type == "builtin"
		spec-builtin
	elif type == "sigs"
		body-json["sigs"]!.as-arr! map (x => x to-sig) spec-sigs
	else
		"unexpected spec type {type}" throw
	body_,

.to-sig spec-sig(a json)
	a.get-comment, a.get-name, a.get-return-type, a.get-params

.get-return-type type-ref(a json)
	a["return-type"]! to-type-ref

.get-params param[](a json)
	a["params"]!.as-arr! map x => x to-param

.to-param param(a json)
	a.get-node-type is "param"
	a.get-name, a.get-type

.to-type-ref type-ref(a json)
	node-type = a get-node-type
	name = a["name"]!.as-str! str-to-sym
	if node-type == "type-param"
		name, ()
	else
		node-type is "struct"
		name, a.get-type-args

.to-spec-ref spec-ref(a json)
	node-type = a get-node-type
	name = a["name"]!.as-str! str-to-sym
	if node-type == "special"
		name, ()
	else
		node-type is "spec"
		name, a.get-type-args
