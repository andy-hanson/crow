import
	crow/io/file: is-dir, names-in-dir
	crow/io/path: child-path

remove-include-and-crow string(a string)
	((a try-remove-start "include/")! try-remove-end ".crow")!

search-dir-recursive string[]^(path string) summon
	path search-dir-recursive _ => true

search-dir-recursive string[]^(path string, file-filter fun bool(string)) summon
	path search-dir-recursive (_ => true), file-filter

search-dir-recursive string[]^(path string, dir-filter fun bool(string), file-filter fun bool(string)) summon
	parts string[][] <- for name : path.names-in-dir parallel
		child = path child-path name
		if child is-dir
			if dir-filter[name]
				child search-dir-recursive dir-filter, file-filter
		elif file-filter[name]
			(child,),
	parts.flatten,
