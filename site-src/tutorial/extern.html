<section>
	<h2>"Extern" functions</h2>
	<p>
		Crow functions can call C functions directly.
		<br />
		To declare an external function, write the function header normally,
		then write <code>c extern</code> in spec position.
		<br />
		Simply omit the function body.
	</p>
	<p>
		<code class="keyword">extern</code> functions are <code class="keyword">bare</code> and
		<code class="keyword">unsafe</code> by default.
		<br />
		You can mark the function <code class="keyword">trusted</code>
		if you don't want it to be <code class="keyword">unsafe</code>.
		That indicates an external function that never does bad things no matter how you call it.
	</p>
	<aside class="danger small">
		<code class="keyword">extern</code> functions are <em>not</em> implicitly <code class="keyword">summon</code>,
		so add that if appropriate.
	</aside>

	<crow-runnable no-run name="regex.crow">
		import
			crow/c-types: c-int, size_t

		main void()
			"cro\\+w" test-regex ("crow", "crooow", "duck")
			"[0-9" test-regex ("42",)

		test-regex void(source string, candidates string[])
			trusted match source parse
			as ok regex
				for x : candidates
					info log "{source} matches {x}? {regex matches x}"
				regex free
			as error e
				error log "bad regex {source}: {e}"

		regex record by-ref, mut
			wrapped mut regex_t

		# Unsafe because the caller must 'free' the regex
		parse (regex, string) result(source string) unsafe
			res regex = (),
			err = &res.wrapped regcomp source.to, ()
			if err == REG_NOERROR
				res ok
			else
				buf char8 mut[] = 1000 n-of "\0"
				_ = err regerror &res.wrapped, buf.begin-pointer, buf.size
				res free
				"{buf.begin-pointer.as-const}" error

		free void(a regex) unsafe
			&a.wrapped regfree

		matches bool(a regex, candidate string)
			err = trusted &a.wrapped regexec candidate.to, (), (), ()
			if err == REG_NOERROR
				true
			elif err == REG_NOMATCH
				false
			else
				buf char8 mut[] = 1000 n-of "\0"
				trusted
					_ = err regerror &a.wrapped, buf.begin-pointer, buf.size
					throw buf.begin-pointer.as-const.to

		regex_t extern(64)

		regoff_t alias
			c-int

		regmatch_t extern

		reg_errcode_t enum(c-int) extern
			REG_ENOSYS = -1
			REG_NOERROR
			REG_NOMATCH
			_REG_BADPAT
			_REG_ECOLLATE
			_REG_ECTYPE
			_REG_EESCAPE
			_REG_ESUBREG
			_REG_EBRACK
			_REG_EPAREN
			_REG_EBRACE
			_REG_BADBR
			_REG_ERANGE
			_REG_ESPACE
			_REG_BADRPT

		regcomp reg_errcode_t(
			regex regex_t mut*,
			pattern c-string,
			cflags c-int,
		) c extern

		regfree void(preg regex_t mut*) c extern

		regexec reg_errcode_t(
			preg regex_t*,
			string c-string,
			nmatch size_t,
			pmatch regmatch_t mut*,
			eflags c-int,
		) c extern

		regerror size_t(
			errcode reg_errcode_t,
			preg regex_t*,
			errbuff char8 mut*,
			errbuf_size size_t,
		) c extern
	</crow-runnable>

	<p>
		This example can't be run on the web;
		you'll have to download and run it with <code>crow run ~/Downloads/regex.crow</code>.
		<br />
		This uses the <code>regex.h</code> library available on Linux.
		For an example on Windows, see <code>libraries/SDL2/SDL.crow</code> from where you downloaded Crow.
	</p>

	<aside>
		<h4>Specifying the library</h4>
		<p>
			<code>crow build</code> and <code>crow run</code> don't take linker options;
			these are inferred from your code.
			<br />
			If the library needs to be linked to with <code>-lfoo</code>,
			its functions should be declared with <code>foo extern</code> instead of <code>c extern</code>.
			<br />
			For example, SDL functions are declared with <code>SDL2 extern</code>.
			<br />
			If that function is included in the compile, the library name gets added to the linker options.
		</p>
	</aside>

</section>
<section>
	<h2>Extern types</h2>
	<p>
		This introduces a new kind of type declaration: <code>regex_t extern(64)</code>.
		<br />
		This declares an opaque type that takes up 64 bytes.
		This lets you use an external library without declaring implementation details.
		<br />
		By default its alignment is the largest power of 2 that the size is divisible by, up to 8,
		so 8 in this example.
		<br />
		You can specify a custom alignment like <code>extern(64, 1)</code>.
	</p>
	<p>
		<code>regmatch_t extern</code> declares an external type with unknown size.
		<br />
		This can be the target of a pointer, but you can't do anything with it.
		This is useful for types that are always passed as opaque pointers.
	</p>
</section>
<section>
	<p>
		<code class="type">reg_errcode_t</code> must be marked <code class="keyword">extern</code>.
		<br />
		This allows it to be used in <code class="keyword">extern</code> functions.
		<br />
		Otherwise, Crow would be free to layout the type any way it wants,
		which would break compatibility with external libraries.
		<br />
		A <code class="keyword">record</code> that is <code class="keyword">extern</code> is always
		<code class="keyword">by-val</code>.
		For compatibility, it's laid out just like a C <code class="keyword">struct</code>.
	</p>
	<p>
		Primitive types like <code class="type">nat64</code> that have C equivalents
		are all <code class="keyword">extern</code>.
		<br />
		Crow-specific types like <code class="type">string</code> are not.
	</p>

	<aside class="danger">
		<h4>C equivalent types</h4>
		<p>
			In Crow, <code class="type">int</code> always means <code class="type">int64</code> on all systems.
			<br />
			This is not the case in C, so when translating C headers,
			use an alias from {{doc/c-types}} like <code class="type">c-int</code>.
		</p>
	</aside>
</section>
<section>
	<h2>Version differences</h2>
	<p>
		Sometimes you need to do different things on different platforms.
		{{doc/version}} has functions to help with that.
	</p>
	<crow-runnable>
		import
			crow/version: is-windows
			system/stdio: stdout
			system/unistd: write
			system/win32: DWORD, GetStdHandle, STD_OUTPUT_HANDLE, WriteFile

		main void() summon
			text nat8 array = "hello\n" to-bytes
			trusted if is-windows
				out = STD_OUTPUT_HANDLE.GetStdHandle
				written mut DWORD = 0
				_ = out WriteFile text.begin-pointer, text.size.to, &written, null
			else
				_ = stdout write text.begin-pointer, text.size
			()
	</crow-runnable>

	<p>
		Both branches of <code>if is-windows</code> are type-checked on all systems.
		But after that, code generation keeps only the branch that matches your system.
		<br />
		On Windows, the <code>false</code> branch won't be compiled.
		On Linux (or in the browser), the <code>true</code> branch won't be compiled.
	</p>
	<p>
		Notice how all functions are imported unconditionally.
		Importing a function does <em>not</em> cause it to be included in the compilation.
		<br />
		Crow chooses what functions to include by starting with <code class="function">main</code>,
		then repeatedly adding any function called by an included function.
		<br />
		This process ignores the unused branch of a version check like <code>if is-windows</code>.
	</p>

	<aside class="small">
		There's currently no way to make the contents of a type conditional on the version.
		<br />
		As a workaround, you can use a <code class="keyword">union</code> and use different parts of the union
		depending on the version.
	</aside>
</section>
