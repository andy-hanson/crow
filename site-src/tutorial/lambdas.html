<section>
	<h2>Lambdas</h2>
	<p>
		A lambda is a value wrapping a function and its closure.
		<br />
		This is useful to be able to customize a function by passing in behaviors and not just values.
		<br />
		For example, <code class="function">filter</code> can filter a collection by any arbitrary predicate.
		<br />
		A lambda value is created using the <code class="keyword">=></code> syntax.
	</p>
	<crow-runnable>
		main void()
			xs nat[] = 1, 2, 3
			ys nat[] = xs filter x => x is-even
			info log "{ys.to::json}"
	</crow-runnable>

	<p>
		There is a <code class="function">subscript</code> function for calling lambdas, called like <code>f[x]</code>.
	</p>
	<crow-runnable>
		main void()
			info log ((x => "{x}") foo)

		foo string(a string data(x nat))
			a[1]
	</crow-runnable>

	<p>
		The syntax <code>void data(x nat)</code> defines a lambda type.
		The lambda returns <code class="type">void</code> and takes a <code class="type">nat</code>.
		The parameter name <code class="parameter">x</code> is just for documentation.
		<code class="keyword">data</code> will be explained in the section on <a href="#closures">Closures</a>.
	</p>
</section>
<section>
	<h2>Parameter destructuring</h2>
	<p>
		All lambdas take one parameter, but it can be a tuple.
	</p>
	<crow-runnable>
		main void()
			info log (((x, y) => "{x} {y}") foo)
			()

		foo string(a string data(x nat, y nat))
			# Constructs a tuple (1, 2) to pass to 'subscript'
			a[1, 2]
	</crow-runnable>

	<p>
		The syntax makes it look like there are multiple parameters.
		To show that it's really only one parameter:
	</p>
	<crow-runnable>
		main void()
			info log ((tuple => "{tuple.a} {tuple.b}") foo)

		foo string(a string data(x nat, y nat))
			tuple (nat, nat) = 1, 2
			a[tuple]
	</crow-runnable>

	<p>
		Similarly, a lambda can appear to take no parameter, but it actually takes <code class="type">void</code>.
	</p>
	<crow-runnable>
		main void()
			info log ((() => "hello") foo)

		foo string(a string data())
			# This is the same as 'a[()]'.
			a[]
	</crow-runnable>
</section>
<section id="closures">
	<h2>Closures</h2>
	<p>
		The code inside the lambda body (after the <code class="keyword">=></code>)
		can access variables outside.
		Those variables are called its <em>closure</em>.
	</p>
	<crow-runnable>
		main void()
			factor nat = 2
			f nat data(x nat) = x => x * factor
			info log "{f[3]}"
	</crow-runnable>

	<h3>Closure purity</h2>
	<p>
		The <code class="keyword">data</code> keyword is the purity of the closure.
		To allow the closure to include <code class="keyword">shared</code> or <code class="keyword">mut</code> data,
		use that keyword in the lambda type instead of <code class="keyword">data</code>.
	</p>
	<crow-runnable>
		main void()
			factor mut nat = 2
			f nat mut(x nat) = x => x * factor
			factor := 4
			info log "{f[3]}"
	</crow-runnable>

	<p>
		Most ordinary functions in the standard library allow a <code class="keyword">mut</code> closure.
	</p>

	<aside class="small">
		There is also a trick for having <code class="keyword">mut</code> closure
		in a <code class="keyword">shared</code> lambda explained in {{tutorial/fibers}}.
	</aside>
</section>
<section>
	<h2>Multi-line lambdas</h2>
	<p>If a lambda's body is long, you can put it in an indented block.</p>
	<crow-runnable>
		main void()
			xs nat[] = 1, 2, 3, 4, 5, 6
			ys nat[] = xs filter x =>
				if x > 3
					x is-even
				else
					x is-odd
			info log "{ys.to::json}"
	</crow-runnable>
	<p>
		Since this is only possible for the last argument to a funciton,
		most functions make sure a lambda argument comes last.
	</p>
</section>
<section id="callers">
	<h2>Lambdas in records</h2>
	<p>
		If a record has a lambda field, it generates a "caller" that gets the lambda and calls it.
		<br />
		(It also generates a getter as usual.)
	</p>
	<crow-runnable>
		main void()
			r r = do
				greet: x =>
					info log "Hello, {x}!"
			# Equivalent to 'r.greet["world"]'
			r greet "world"

		r record mut
			greet void mut(x string)
	</crow-runnable>
</section>
