<section>
	<h2>Creating and using options</h2>

	<p>
		A type <code>foo?</code> is an <em>optional</em> <code>foo</code>.
		<br />
		An option can be empty (have no value inside) or non-empty (have a value inside).
	</p>
	<p>
		This is useful for values that, in other languages, might be a special value like
		<code>null</code> or <code>-1</code>.
		<br />
		Explicitly making the value optional means that you won't have to remember that.
	</p>
	<crow-runnable>
		main nat^(_ string[])
			info log ().describe
			info log (1,).describe
			()

		describe string(a nat?)
			if x ?= a
				"non-empty {x}"
			else
				"empty"
	</crow-runnable>

	<p>
		Above you can see the most common way of using an option:
		<code class="keyword">if</code> with <code class="keyword">?=</code>.
		<br />
		The unwrapped value of the option becomes <code>x</code>,
		or if the option was empty, the <code class="keyword">else</code> branch will run.
	</p>
	<p>
		These can integrate with other <code class="keyword">if</code> chains.
	</p>
	<crow-runnable>
		main nat^(_ string[])
			info log (() describe "fizz")
			info log ((1,) describe "fizz")
			info log ((1,) describe "buzz")
			()

		describe string(a nat?, b string)
			if b == "buzz"
				"buzz"
			elif x ?= a
				"{x}"
			else
				b
	</crow-runnable>
</section>
<section>
	<h2>"??" and "||"</h2>

	<p>
		The <code class="fun">??</code> operator provides a default value for an option if it's empty.
	</p>
	<crow-runnable>
		main nat^(_ string[])
			info log (() ?? "bar")
			info log (("crow",) ?? "bar")
			()
	</crow-runnable>

	<p>
		The <code class="fun">||</code> operator is similar, but it takes two options and returns another option.
	</p>
	<crow-runnable>
		main nat^(_ string[])
			info log (() || ()).describe
			info log (() || (1,)).describe
			info log ((2,) || (3,)).describe
			()

		describe string(a nat?)
			if x ?= a
				"non-empty {x}"
			else
				"empty"
	</crow-runnable>
</section>
<section>
	<h2>Forcing an option</h2>
	<p>
		Use <code>x!</code> to get the value of an option or throw an exception if it is empty.
		<br />
		(Exceptions will be explained in {{tutorial/exceptions}}.)
	</p>
	<crow-runnable>
		main nat^(_ string[])
			empty string? = ()
			non-empty string? = "foo",
			info log non-empty!
			info log empty!
			()
	</crow-runnable>
</section>
