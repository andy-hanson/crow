<section>
	<h2>Creating and using options</h2>

	<p>
		A type <code>foo?</code> is an <em>optional</em> <code>foo</code>.
		<br />
		An option can be empty (have no value inside) or non-empty (have a value inside).
	</p>
	<p>
		This is useful for values that, in other languages, might be a special value like
		<code>null</code> or <code>-1</code>.
		<br />
		Explicitly making the value optional means that you won't have to remember that.
	</p>
	<crow-runnable>
		main void()
			info log ().describe
			info log (1,).describe

		describe string(a nat?)
			if x ?= a
				"non-empty {x}"
			else
				"empty"
	</crow-runnable>

	<p>
		Above you can see the most common way of using an option:
		<code class="keyword">if</code> with <code class="keyword">?=</code>.
		<br />
		The unwrapped value of the option becomes <code>x</code>,
		or if the option was empty, the <code class="keyword">else</code> branch will run.
	</p>
	<p>
		These can integrate with other <code class="keyword">if</code> chains.
	</p>
	<crow-runnable>
		main void()
			info log (() foo "fizz")
			info log ((1,) foo "fizz")
			info log ((1,) foo "buzz")

		foo string(a nat?, b string)
			if b == "buzz"
				"buzz"
			elif x ?= a
				"{x}"
			else
				b
	</crow-runnable>
</section>
<section>
	<h2>"??" and "||"</h2>

	<p>
		The <code class="function">??</code> operator provides a default value for an option if it's empty.
	</p>
	<crow-runnable>
		main void()
			info log (() ?? "bar")
			info log (("crow",) ?? "bar")
	</crow-runnable>

	<p>
		The <code class="function">||</code> operator is similar, but it takes two options and returns another option.
	</p>
	<crow-runnable>
		main void()
			info log (() || ()).describe
			info log (() || (1,)).describe
			info log ((2,) || (3,)).describe

		describe string(a nat?)
			if x ?= a
				"non-empty {x}"
			else
				"empty"
	</crow-runnable>
</section>
<section>
	<h2>Forcing an option</h2>
	<p>
		Use <code>x!</code> to get the value of an option or throw an exception if it is empty.
		<br />
		(Exceptions will be explained in {{tutorial/exceptions}}.)
	</p>
	<crow-runnable>
		main void()
			empty string? = ()
			non-empty string? = "foo",
			info log non-empty!
			info log empty!
	</crow-runnable>
</section>
<section>
	<h2>Optional calls</h2>
	<p>
		In contrast with forcing an option, <code>x?.f</code> calls <code>f</code> only if <code>x</code> is non-empty.
	</p>
	<crow-runnable>
		main void()
			empty string? = ()
			info log "{empty?.reverse}"
			non-empty string? = "foo",
			info log "{non-empty?.reverse}"
			()

		show string(a string?)
			a ?? "empty"
	</crow-runnable>

	<p>
		You can also use <code>x?[y]</code> to optionally subscript.
	</p>
	<crow-runnable>
		main void()
			empty string[]? = ()
			info log "{empty?[1]}"
			non-empty string[]? = ("foo", "bar"),
			info log "{non-empty?[1]}"
			()

		show string(a string?)
			a ?? "empty"
	</crow-runnable>

</section>
<section>
	<h2>Option conditions</h2>

	<p>
		The left hand side of <code>?=</code> is a destructure.
		Here <code>_, x</code> destructures a <code>(nat, string)</code>.
	</p>

	<crow-runnable>
		main void()
			opt (nat, string)? = (1, "one"),
			if _, x ?= opt
				info log x
	</crow-runnable>

	<p>
		Other expressions besides <code class="keyword">if</code> can use option conditions too.
		<br />
		Here's an example with <code>guard</code>:
	</p>

	<crow-runnable>
		main void()
			opt nat? = 1,
			guard x ?= opt
			info log "got {x}"
	</crow-runnable>

	<p>
		It works in <code>while</code> or <code>until</code> loops too.
	</p>

	<crow-runnable>
		main void()
			x mut nat = 111
			until tenth ?= x divide-exact 10
				x +:= 1
			info log "tenth is {tenth}"

			x := tenth
			while half ?= x divide-exact 2
				info log "half is {half}"
				x := half

		divide-exact nat?(a nat, divisor nat)
			if a is-multiple-of divisor
				a / divisor,
	</crow-runnable>

	<p>
		For a <code>while</code> loop, the variable is only visible <em>inside</em> the loop.
		<br />
		For an <code>until</code> loop, the variable is only visible <em>after</em> the loop.
	</p>
</section>