#!/usr/bin/env crow
import
	crow/io/file
	crow/io/net/http-server
	crow/io/net/http-types
	crow/io/path: child-path
	crow/io/print: out, print
	crow/io/stdin: read-stdin

main void() summon, (native, posix) extern
	port nat16 = 8080
	handler http-handler = req =>
		res = req handle-request
		info log "{req.path} {req.query} {res status}"
		res
	with _ : port http-server handler
		out print "serving on http://localhost:{port}/"
		out print "press enter to stop"
		_ = read-stdin

to string(a string[symbol])
	"&" join for x, y : a
		"{x}={y}"

-handle-request http-response(a http-request) summon, native extern
	path = "site" child-path a.path
	full-path = path.is-directory ? path child-path "index.html" : path
	try
		full-path file-response full-path.read-file
	catch path-not-found _
		throw http-not-found

file-response http-response(path string, content nat8[])
	200, "OK", (("Content-Type", path.content-type),), content

content-type string(path string)
	if path ends-with ".html"
		"text/html"
	elif path ends-with ".css"
		"text/css"
	elif path ends-with ".js"
		"text/javascript"
	elif path ends-with ".json"
		"text/json"
	elif path ends-with ".svg"
		"image/svg+xml"
	elif path ends-with ".wasm"
		"application/wasm"
	else
		"text/plain"
